(null
	#!DeepDive
	(declare
		(assoc
			action_feature (null)
			focus_features (null)
			features []
		)

		(declare (assoc
			context_features (filter (lambda (not (contains_value (append action_feature focus_features) (current_value)))) features)
			case_features (values (append features (if !tsTimeFeature [".series_index"] [])) .true)
		))

		;create a map of feature -> flag, set to true if the feature has any null values
		;that way there's no need to explicitly filter nulls out when querying for a feature that no nulls
		;or if it's inactive
		(declare (assoc
			ignore_null_action_feature
				(and
					(!= .false (get !featureNullRatiosMap (list action_feature "has_nulls")) )
					(not (contains_index !inactiveFeaturesMap action_feature) )
				)
			;create a backup of context_features for time series flows
			original_context_features (if !tsTimeFeature (replace context_features))
			num_features (size context_features)
		))
		(assign (assoc
			hyperparam_map
				(call !GetHyperparameters (assoc
					context_features features
					weight_feature weight_feature
					use_case_weights use_case_weights
				))
		))

		(call !UpdateCaseWeightParameters)

		(assign (assoc
			k_parameter (get hyperparam_map "k")
			p_parameter (get hyperparam_map "p")
			dt_parameter (get hyperparam_map "dt")
			query_feature_attributes_map (get hyperparam_map "featureDomainAttributes")

			;store an assoc of lag/rate/delta feature -> lag/order amount for time series flows
			ts_feature_lag_amount_map (if !tsTimeFeature (call !BuildTSFeatureLagAmountMap))
		))

		(declare (assoc
			enabled_num_features_probability_map (call !ComputeNumEnabledFeaturesProbabilitiesMap (assoc num_features num_features))
			all_features_map (zip features)
			feature_deviations (get hyperparam_map "featureDeviations")
		))

		(declare (assoc
			case_acs
				||(map
					(lambda (let
						(assoc
							case_id (current_value 1)
							;map of feature -> value for all the case values
							case_values_map (zip case_features (retrieve_from_entity (current_value 1) case_features) )
							time_series_filter_query (list)
							feature_weights (get hyperparam_map "featureWeights")
							context_features context_features
						)
						(declare (assoc case_feature_value (get case_values_map action_feature) ))

						(if !tsTimeFeature
							(assign (assoc
								context_features
									(call !FilterContextsBySeriesIndex (assoc
										all_context_features original_context_features
										series_index (get case_values_map ".series_index")
									))
							))
						)

						(declare (assoc
							;filter out a random number of context features
							react_context_features
								(rand
									context_features
									;number of features to enable is probabilistically weighted
									(if (and !tsTimeFeature (!= (size context_features) num_features))
										(rand
											(call !ComputeNumEnabledFeaturesProbabilitiesMap (assoc num_features (size context_features) ))
										)

										(rand enabled_num_features_probability_map)
									)
									;unique random features (not with replacement)
									.true
								)
						))

						;assoc of all removed features to their residual values, filter the action features to be those that are not context features
						(declare (assoc removed_features_map (remove all_features_map react_context_features) ))

						(if !tsTimeFeature
							(let
								(assoc context_map (keep case_values_map react_context_features) )
								(if (contains_index context_map !tsTimeFeature)
									(assign (assoc time_series_filter_query (call !ComputeTimeSeriesFilterFutureCasesQuery) ))
								)
							)
						)

						;use dynamic deviations subtrainee if present
						(if (get hyperparam_map "subtraineeName")
							(call !UseDynamicDeviationsAndWeights (assoc
								context_features react_context_features
								context_values (unzip case_values_map react_context_features)
								hyperparam_map hyperparam_map
							))
						)

						(declare (assoc
							dependent_queries_list
								;if any of the features being predicted are dependent to any of the contexts, then
								;append all the dependencies together for the query
								(if (and !hasDependentFeatures (size (keep !dependentFeatureMap (indices removed_features_map))) )
									(apply "append" (map
										(lambda
											(call !ComputeDependentQueries (assoc
												context_features react_context_features
												context_values (unzip case_values_map react_context_features)
												action_feature (current_value 1)
											))
										)
										;just the features to be predicted that have dependencies defined
										(indices (keep !dependentFeatureMap (indices removed_features_map)))
									))
								)
						))

						;do a num_reacts_per_case of robust reacts without any of the focus features
						(declare (assoc
							without_focus_features_action_diff
								(call !InterpolateAndComputeDiffToCase (assoc
									candidate_cases_lists
										(if (= 0 (size  react_context_features))
											[[][][]]
											(compute_on_contained_entities
												(query_not_in_entity_list [(replace case_id)])
												(if ignore_null_action_feature (query_not_equals action_feature (null)))
												(if (size context_condition_filter_query) context_condition_filter_query)
												dependent_queries_list
												time_series_filter_query
												(query_nearest_generalized_distance
													k_parameter
													(replace react_context_features)
													(replace (unzip case_values_map react_context_features))
													p_parameter
													feature_weights
													!queryDistanceTypeMap
													query_feature_attributes_map
													feature_deviations
													(null)
													dt_parameter
													(if valid_weight_feature weight_feature(null))
													tie_break_random_seed
													(null) ;radius
													!numericalPrecision
													action_feature
												)
											)
										)
								))
						))

						;;;;;;;;;;

						;append focus features
						(accum (assoc react_context_features focus_features ))
						(assign (assoc removed_features_map (remove removed_features_map focus_features) ))

						(if !tsTimeFeature
							(let
								(assoc context_map (keep case_values_map react_context_features) )
								(if (contains_index context_map !tsTimeFeature)
									(assign (assoc time_series_filter_query (call !ComputeTimeSeriesFilterFutureCasesQuery) ))
								)
							)
						)

						;use dynamic deviations subtrainee if present
						(if (get hyperparam_map "subtraineeName")
							(call !UseDynamicDeviationsAndWeights (assoc
								context_features react_context_features
								context_values (unzip case_values_map react_context_features)
								hyperparam_map hyperparam_map
							))
						)

						;if any of the features being predicted are dependent to any of the contexts, then
						;append all the dependencies together for the query
						(if (and !hasDependentFeatures (size (keep !dependentFeatureMap (indices removed_features_map))) )
							(assign (assoc
								dependent_queries_list
									(apply "append" (map
										(lambda
											(call !ComputeDependentQueries (assoc
												context_features react_context_features
												context_values (unzip case_values_map react_context_features)
												action_feature (current_value 1)
											))
										)
										;just the features to be predicted that have dependencies defined
										(indices (keep !dependentFeatureMap (indices removed_features_map)))
									))
							))
						)

						(declare (assoc
							with_focus_features_action_diff
								(call !InterpolateAndComputeDiffToCase (assoc
									candidate_cases_lists
										(compute_on_contained_entities
											(query_not_in_entity_list [(replace case_id)])
											(if ignore_null_action_feature (query_not_equals action_feature (null)))
											(if (size context_condition_filter_query) context_condition_filter_query)
											dependent_queries_list
											time_series_filter_query
											(query_nearest_generalized_distance
												k_parameter
												(replace react_context_features)
												(replace (unzip case_values_map react_context_features))
												p_parameter
												feature_weights
												!queryDistanceTypeMap
												query_feature_attributes_map
												feature_deviations
												(null)
												dt_parameter
												(if valid_weight_feature weight_feature(null))
												tie_break_random_seed
												(null) ;radius
												!numericalPrecision
												action_feature
											)
										)
								))
						))

						;output a pair of : [ AC for the focus feature, focus values ]
						[
							(-
								without_focus_features_action_diff
								with_focus_features_action_diff
							)
							(unzip case_values_map focus_features)
						]
					))
					case_ids
				)
		))

		(print (unparse focus_features) " : "  (/ (apply "+" (map (lambda (first (current_value))) case_acs)) (size case_ids)) "\n")

		(if (= 1 (size focus_features))
			(if (contains_index !nominalsMap (first focus_features))
				(call !ResponsibleNominal (assoc case_acs (map (lambda [(first (current_value 1)) (last (last (current_value 1)))]) case_acs)  ))
				(call !ResponsibleContinuous (assoc case_acs (map (lambda [(first (current_value 1)) (last (last (current_value 1)))]) case_acs)  ))
			)

			;else have to go through all possible combinations of values
			(call !ResponsibleValuesMultipleFeatures)
		)
	)



	;given case_acs
	#!ResponsibleContinuous
	(seq

		;sort is descending order
		(declare (assoc
			sorted_values (sort (lambda (< (last (current_value)) (last (current_value 1))) ) case_acs)
		))
		(declare (assoc
			num_unique_values (size (values (map (lambda (last (current_value))) sorted_acs) .true))
		))
		(declare (assoc
			num_buckets (min 30 num_unique_values)
			total_num (size case_acs)
		))

		(declare (assoc
			;num_buckets list of pairs, specifying the min and max percentage range for each bucket.
			;e.g., if there were two buckets, this would output: [ [0 0.5] [0.5 1] ]
			bucket_percent_range_pairs
				(range
					(lambda [
						(* (/ 1 num_buckets) (- (current_index 1) 1))
						(* (/ 1 num_buckets) (current_index 1))
					])
					1 num_buckets 1
				)
		))

		(map
			(lambda (let
				(assoc
					index_quantile_low (floor (* total_num  (first (current_value 1))))
					index_quantile_high (- (floor (* total_num (last (current_value 1)))) 1)
				)
				(declare (assoc
					bucket_acs
						(map
							(lambda (first (current_value)))
							(unzip sorted_values (range index_quantile_low index_quantile_high))
						)

				))

				;output bucket tuple of:  [ mean mode min max ]
				[
					(generalized_mean bucket_acs)
					(mode bucket_acs)
					(apply "min" bucket_acs)
					(apply "max" bucket_acs)
				]
			))
			bucket_percent_range_pairs
		)
	)

	#!ResponsibleNominal
	(seq
		(declare (assoc
			classes_ac_map
				;output an assoc of class -> pair of: [ mean_ac median_ac]
				(map
					(lambda
						;output mean and median ac values
						(if (~ [] (current_value))
							[
								(/ (apply "+" (current_value 1)) (size (current_value 1)) )
								(get (sort (current_value 1)) (/ (size (current_value 1)) 2))
							]

							[ (current_value 1) (current_value 1)]
						)
					)
					;accumulate the AC values for each class to create an assoc of class -> list of ac values
					(zip
						(lambda
							(append (current_value) (current_value 1))
						)
						(map (lambda (last (current_value))) case_acs)
						(map (lambda (first (current_value))) case_acs)
					)
				)
		))

		;sort classes by mean AC in descending order
		(sort
			(lambda (<
				(first (get classes_ac_map (current_value)))
				(first (get classes_ac_map (current_value 1)))
			))
			(indices classes_ac_map)
		)
	)

)