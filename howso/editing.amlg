;Contains methods for adding, editing and removal of cases and session data.
(null

	;removes replay specified by session and any references within cases
	;{idempotent .true}
	#delete_session
	(declare
		(assoc
			;{type "string" required .true}
			;the id of the session to remove
			target_session (null)
		)
		(call !ValidateParameters)
		(destroy_entities target_session)

		;if any case references this session, remove the session id
		(map
			(lambda
				(if (= target_session (retrieve_from_entity (current_value) !internalLabelSession))
					(assign_to_entities (current_value) (associate !internalLabelSession (null)))
				)
			)

			(call !AllCases)
		)

		;call the method to remove all cases without session references
		(call !RemoveCasesWithoutSessionReferences)

		(accum_to_entities (assoc !revision 1))

		(call !Return)
	)

	;removes the specified feature on all cases for a trainee that match the specified condition
	;if conditions are not specified, removes feature for all cases and from the trainee, if condition is an empty assoc, leaves the feature metadata in the trainee.
	;Updates the accumulated data mass for the dataset proportional to the number of cases modified.
	;{use_active_session .true}
	#remove_feature
	(declare
		(assoc
			;{ref "Condition"}
			;assoc of feature->value(s).
			;	  no value or (null) = select cases where that feature is (null)/missing
			;	- for continuous or numeric ordinal features:
			;	  one value = must equal exactly the value or be close to it for fuzzy match
			;	  two values = inclusive between, supports (null) on either side for less than/greater than conditions
			;	  [(null) (null)] = select all non-null cases
			;	- for nominal or string ordinal features:
			;	  n values = must match any of these values exactly
			condition (null)
			;{type "string"}
			;if specified ignores condition and operates on cases for the specified session id
			condition_session (null)
			;{type "string" required .true}
			;the feature name to remove
			feature (null)
			;{type "string"}
			;the session id when this call is being made
			session "none"
		)
		(call !ValidateParameters)
		(call !ValidateCondition)

		;trainee has changed so clear out these cached value
		(assign_to_entities (assoc
			!averageCaseEntropyAddition (null)
			!averageCaseEntropyRemoval (null)
			!averageCaseDistanceContribution (null)
			!storedCaseConvictionsFeatureAddition (null)
			!nominalClassProbabilitiesMap
				(if (contains_index !nominalClassProbabilitiesMap feature)
					(assoc)
					!nominalClassProbabilitiesMap
				)
		))

		;remove feature from dataset if no conditions are specified
		(declare (assoc
			remove_feature_from_dataset (and (= (null) condition) (= (null) condition_session))
		))

		;get the list of cases to operate on, if condition isn't specified, operate on all cases
		(declare (assoc
			entities
				(if (not remove_feature_from_dataset)
					;specify no limit for how many to match as long as they match the condition
					(call !GetCasesByCondition (assoc
						condition condition
						condition_session condition_session
						precision "exact"
						num_cases 0
					))

					(call !AllCases)
				)
			;prepend a # to the feature name, because retrieve_entity_root will add one on
			feature_with_label (concat "#" feature)
		))

		;record edit history for each case
		(map
			(lambda
				(call !AddCaseEditHistory (assoc
					edit_type "remove"
					case (current_value 1)
					feature feature
					session session
				))
			)
			entities
		)

		;remove the feature
		(map
			(lambda (assign_entity_roots
				(current_value)
				(filter
					(lambda
						(and
							(!= (current_index) feature_with_label)
							(not (contains_value (get_labels (current_value)) feature_with_label))
						)
					)
					(retrieve_entity_root (current_value))
				)
			))
			entities
		)

		(if remove_feature_from_dataset
			(seq
				(if (contains_index !nominalsMap feature)
					(assign_to_entities (assoc
						!nominalsMap (remove !nominalsMap feature)
						!uniqueNominalsSet (remove !uniqueNominalsSet feature)
						!numericNominalFeaturesMap (remove !numericNominalFeaturesMap feature)
						!stringNominalFeaturesSet (remove !stringNominalFeaturesSet feature)
					))
				)

				(if (contains_index !ordinalFeaturesSet feature)
					(seq
						(assign_to_entities (assoc
							!ordinalFeaturesSet (remove !ordinalFeaturesSet feature)
							!ordinalFeaturesValuesMap (remove !ordinalFeaturesValuesMap feature)
							!ordinalFeaturesRangesMap (remove !ordinalFeaturesRangesMap feature)
							!ordinalStringToOrdinalMap (remove !ordinalStringToOrdinalMap feature)
							!ordinalOrdinalToStringMap (remove !ordinalOrdinalToStringMap feature)
						))
						(if (= 0 (size !ordinalStringToOrdinalMap))
							(assign_to_entities (assoc !hasStringOrdinals .false))
						)
					)
				)

				(if (contains_index !editDistanceFeatureTypesMap  feature)
					(assign_to_entities (assoc !editDistanceFeatureTypesMap  (remove !editDistanceFeatureTypesMap  feature) ))
				)

				(if (contains_index !cyclicFeaturesMap feature)
					(seq
						(assign_to_entities (assoc !cyclicFeaturesMap (remove !cyclicFeaturesMap feature) ))
						(if (= 0 (size !cyclicFeaturesMap))
							(assign_to_entities (assoc !hasCyclicFeatures .false))
						)
					)
				)

				(if (contains_index !featureRoundingMap feature)
					(seq
						(assign_to_entities (assoc !featureRoundingMap (remove !featureRoundingMap feature) ))
						(if (= 0 (size !featureRoundingMap))
							(assign_to_entities (assoc !hasRoundedFeatures .false))
						)
					)
				)

				(if (contains_index !featureDateTimeMap feature)
					(seq
						(assign_to_entities (assoc !featureDateTimeMap (remove !featureDateTimeMap feature) ))
						(if (= 0 (size !featureDateTimeMap))
							(assign_to_entities (assoc !hasDateTimeFeatures .false))
						)
					)
				)

				;If 'feature' is a distance contribution feature, this removes the entry for only that particular feature
				;but does no other cleanup if it was being used as a context feature elsewhere.
				;It's up to the user to explicitly remove each distance contribution feature individually.
				(if (contains_value !storedDistanceContributionsFeatureMap feature)
					(assign_to_entities (assoc
						!storedDistanceContributionsFeatureMap
							(filter
								(lambda (!= feature (current_value)))
								!storedDistanceContributionsFeatureMap
							)
					))
				)

				(assign_to_entities (assoc
					!trainedFeatures (filter (lambda (!= feature (current_value))) !trainedFeatures)
					!trainedFeaturesContextKey (call !BuildContextFeaturesKey (assoc context_features (filter (lambda (!= feature (current_value))) !trainedFeatures) ))
					!reactIntoFeaturesList (filter (lambda (!= feature (current_value))) !reactIntoFeaturesList)
					!categoricalFeaturesSet (remove !categoricalFeaturesSet feature)
					!cachedFeatureMinResidualMap (remove !cachedFeatureMinResidualMap feature)
					!cachedFeatureHalfMinGapMap (remove !cachedFeatureHalfMinGapMap feature)
					!substitutionValueMap (remove !substitutionValueMap feature)
					!unSubstituteValueMap (remove !unSubstituteValueMap feature)
					!featureAttributes (remove !featureAttributes feature)
					!featureBoundsMap (remove !featureBoundsMap feature)
					!derivedFeaturesMap (remove !derivedFeaturesMap feature)
					!sourceToDerivedFeatureMap (remove !sourceToDerivedFeatureMap feature)
					!featureCustomDerivedMethods (remove !featureCustomDerivedMethods feature)
					!featureNullRatiosMap (remove !featureNullRatiosMap feature)
					!queryDistanceTypeMap (remove !queryDistanceTypeMap feature)

					;use rewrite to remove all instances of feature in the !hyperparameterMetadataMap
					!hyperparameterMetadataMap
						(rewrite
							(lambda
								(if (contains_index (current_value) feature)
									(remove (current_value) feature)
									(current_value)
								)
							)
							!hyperparameterMetadataMap
						)

					!expectedValuesMap
						(rewrite
							(lambda
								(if (contains_index (current_value) feature)
									(remove (current_value) feature)
									(current_value)
								)
							)
							!expectedValuesMap
						)
				))
				(call !UpdateDefinedFeatures)

				(if !inactiveFeaturesNeedCaching
					(call !UpdateInactiveFeatures)

					(contains_index !inactiveFeaturesMap feature)
					(if (> (size !inactiveFeaturesMap) 1)
						(assign_to_entities (assoc
							!inactiveFeaturesMap (remove !inactiveFeaturesMap feature)
						))

						;else removing the only inactive feature, clear out the properties
						(assign_to_entities (assoc !inactiveFeaturesMap (null) ))
					)
				)
			)
		)

		;accumulate data mass change equivalent to one feature being changed for each case that satisfies the condition.
		(accum_to_entities (assoc
			!dataMassChangeSinceLastAnalyze (* (size entities) (/ 1 (size !trainedFeatures)) )
			!dataMassChangeSinceLastDataReduction (* (size entities) (/ 1 (size !trainedFeatures)) )
		))

		(accum_to_entities (assoc !revision 1))

		;return completion
		(call !Return)
	)

	;creates a new case given a list of features and their values in feature_values
	; assigning its session and session_training_index
	;returns the id of the case created
	;parameters:
	; feature_values: list of feature values
	; features: list of feature names
	; session: session ID string
	; session_training_index: 0-based index of the case, ordered by training during the session;  is not changed
	#!CreateCase
	(first (create_entities ;(concat "case" session_training_index)
		(set_type
			(append
				;bring in context
				(zip_labels features feature_values)

				;add session info
				(zip_labels
					(list !internalLabelSession !internalLabelSessionTrainingIndex)
					(list session session_training_index)
				)
			)
			(null)
		)
	))

	;Adds the specified feature on all cases for a trainee that match the specified condition. overwrites features that
	;If condition are not specified, adds feature for all cases and to the trainee.  If condition is an empty assoc, will not modify feature metadata in the trainee.
	;If feature attributes are passed in, will also set the trainee's feature attributes.
	;Updates the accumulated data mass for the dataset proportional to the number of cases modified.
	;{use_active_session .true}
	#add_feature
	(declare
		(assoc
			;{type "string" required .true}
			;name of feature to odd
			feature (null)
			;{type "any"}
			;value for the feature
			feature_value (null)
			;{ref "Condition"}
			;assoc of feature->value(s).
			;	  no value or (null) = select cases where that feature is (null)/missing
			;	- for continuous or numeric ordinal features:
			;	  one value = must equal exactly the value or be close to it for fuzzy match
			;	  two values = inclusive between, supports (null) on either side for less than/greater than conditions
			;	  [(null) (null)] = select all non-null cases
			;	- for nominal or string ordinal features:
			;	  n values = must match any of these values exactly
			condition (null)
			;{type "string"}
			;if specified ignores condition and returns cases for the specified session id
			condition_session (null)
			;{type "boolean"}
			;if true, will overwrite any previously trained values under the same feature name
			overwrite .true
			;{type "string"}
			;the session id when this call is being made
			session "none"
			;{type "list" values "string"}
			;list of case ids to operate on. if specified, conditions will be ignored
			entities (null)
			;{type "boolean"}
			;flag, if set to true, will not update hyperparameter metadata map
			internal_feature .false
			;{ref "FeatureAttributes"}
			;assoc of feature specific attributes for this feature. If unspecified and conditions are not specified, will assume feature type as 'continuous'.
			feature_attributes (null)
		)
		(call !ValidateParameters)
		(call !ValidateCondition)

		(if (contains_index !untrainableFeatureCharacterSet (first feature))
			(conclude
				(call !Return (assoc errors (list "Failed to add feature: ensure it does not start with an invalid character.")  ))
			)
		)

		(if (and feature_attributes (contains_index !featureAttributes feature))
			(conclude
				(call !Return (assoc errors (list "Feature already defined, either call add_feature without specifying feature_attributes or call set_feature_attributes to update existing feature attributes.")  ))
			)
		)

		;add feature to dataset if no conditions are specified
		(declare (assoc
			add_feature_to_dataset (and (= (null) condition) (= (null) condition_session))
		))

		;get all cases that need to be updated
		(if (= (null) entities)
			(assign (assoc
				entities
					(if (not add_feature_to_dataset)
						;specify no limit for how many to match as long as they match the condition
						(call !GetCasesByCondition (assoc
							condition condition
							condition_session condition_session
							precision "exact"
							num_cases 0
						))

						(call !AllCases)
					)
			))
		)

		;only add feature to dataset if it is not an internal feature (e.g., .imputed) or if feature_attributes were provided or feature isn't defined
		(assign (assoc
			add_feature_to_dataset
				(and
					(not internal_feature)
					(not (contains_index !featureAttributes feature))
					(or
						feature_attributes
						add_feature_to_dataset
						(not (contains_index !featureAttributes feature))
					)
				)
		))

		(if add_feature_to_dataset
			(seq
				;all features are added as inactive since they start with a single value
				(if !inactiveFeaturesMap
					(accum_to_entities (assoc !inactiveFeaturesMap (associate feature feature_value) ))

					(assign_to_entities (assoc !inactiveFeaturesMap (associate feature feature_value) ))
				)

				;update hyperparameters and clear the cached residuals flags
				(assign_to_entities (assoc
					!inactiveFeaturesNeedCaching .true
					!hyperparameterMetadataMap (call !UpdateHyperparametersWithNewFeature (assoc hp_map !hyperparameterMetadataMap))
					!defaultHyperparameters (call !UpdateHyperparametersWithNewFeature (assoc hp_map !defaultHyperparameters))

					!trainedFeatures
						(if (not (contains_value !trainedFeatures feature))
							(sort (append !trainedFeatures feature))
							!trainedFeatures
						)
					!trainedFeaturesContextKey
						(call !BuildContextFeaturesKey (assoc
							context_features
								(if (not (contains_value !trainedFeatures feature))
									(append !trainedFeatures feature)
									!trainedFeatures
								)
						))
				))
				(call !UpdateDefinedFeatures)

				;if attributes were not provided, default to continuous
				(if (= (null) feature_attributes)
					(assign (assoc
						feature_attributes
							(assoc
								"type" "continuous"
								"bounds" (assoc "allow_null" .true)
							)
					))
				)
				;append attributes to the existing ones and re-set attributes
				(assign (assoc
					feature_attributes
						(append
							(get (call get_feature_attributes) (list 1 "payload"))
							(associate feature feature_attributes)
						)
				))

				(call set_feature_attributes (assoc feature_attributes feature_attributes))
			)
		)

		;store the originally passed in value as-is for history tracking
		(declare (assoc unencoded_feature_value (get_value feature_value)))

		;encode feature values if necessary
		(if !hasFeaturesNeedEncodingFromInput
			(assign (assoc
				feature_value
					(first
						(call !ConvertFromInput (assoc
							feature_values (list feature_value)
							features (list feature)
						))
					)
			))
		)

		;create at assoc of case id -> value
		(declare (assoc
			case_values_map
				(if (= (null) feature_value)
					(zip entities)

					(zip
						entities
						(map feature_value entities)
					)
				)
		))

		;record edit history for each case
		(map
			(lambda (let
				(assoc case_id (current_value 1))

				(call !AddCaseEditHistory (assoc
					edit_type "set"
					case case_id
					feature feature
					feature_value unencoded_feature_value
					session session
				))

				;make a map of all the imputed features (if there are any) for fast lookup and deletion
				(declare (assoc imputed_map (zip (retrieve_from_entity case_id !internalLabelImputed))))

				;check if this case has this feature on its imputed list, and if so remove it from there since its value was just overwritten
				(if (contains_index imputed_map feature)
					;remove this feature from the imputed_map and overwrite the stored imputed features using the resulting list
					(call !StoreCaseValues (assoc
						label_name !internalLabelImputed
						case_values_map (associate case_id (indices (remove imputed_map feature)))
						overwrite 1
					))
				)
			))
			entities
		)

		(call !StoreCaseValues (assoc
			case_values_map case_values_map
			label_name feature
			overwrite overwrite
		))

		;accumulate data mass change equivalent to one feature being changed for each case that satisfies the condition.
		(accum_to_entities (assoc
			!dataMassChangeSinceLastAnalyze (* (size entities) (/ 1 (size !trainedFeatures)) )
			!dataMassChangeSinceLastDataReduction (* (size entities) (/ 1 (size !trainedFeatures)) )
		))

		(accum_to_entities (assoc !revision 1))

		;return completion
		(call !Return)
	)

	;stores the associated value with each entity id (the keys of case_values_map) to the respective entity
	; using the label_name specified
	;if the entity does not have the label, but its root node is a null, then it will add the label and store the data
	; parameters:
	; ovewrite: flag, whether to overwrite values for features that already exist
	; case_values_map : assoc of case id -> value
	; label_name : name of label for the feature to be stored
	#!StoreCaseValues
	(declare
		(assoc
			case_values_map (assoc)
			label_name ""
			overwrite 1
		)
		(map
			(lambda (if
				;see whether the entity has the label
				(contains_label (current_index) label_name)

				;entity has label, overwite if overwrite flag is set, otherwise do nothing
				(if overwrite
					(assign_to_entities (current_index) (associate label_name (get case_values_map (current_index 1))))
				)

				;else need to append the label to the entity
				(accum_entity_roots (current_index) (list (set_labels (get case_values_map (current_index 1)) (list label_name))))

			))
			case_values_map
		)
	)

	;automatically creates a feature with a default value for a set of cases
	; parameters:
	; feature_name: name of the case_weight feature to add to all cases, defaults to ".case_weight"
	; entities: the list of case entities to add the case_weight feature to. if (null), all cases missing feature_name are found
	; default_value: the default value for the feature in each case, defaults to 1.0
	; has_rebalance_features: flag, if set to true will also create and populate !internalLabelProbabilityMass
	#!CreateCaseWeights
	(declare
		(assoc
			feature_name ".case_weight"
			entities (null)
			default_value 1.0
			has_rebalance_features .false
		)

		;query for at least one case without feature_name
		;to check if any operations are necessary
		(declare (assoc
			random_case
				(first
					(contained_entities
						(query_exists !internalLabelSession)
						(query_not_exists feature_name)
						(query_select 1 0)
					)
				)
		))

		;conclude if no case is found without weight feature_name (ie, all cases have the weight feature already)
		(if (and (= random_case (null)) (not has_rebalance_features))
			(conclude)
		)

		(declare (assoc probability_mass_feature_only .false ))

		;all cases have the weight feature already but since there are rebalance features
		;need to check that probability mass feature also exists
		(if (and (= (null) random_case) has_rebalance_features)
			;if all cases already have probability mass feature for rebalancing, no need to create any features
			(if (=
					(null)
					(first
						(contained_entities
							(query_exists !internalLabelSession)
							(query_not_exists !internalLabelProbabilityMass)
							(query_select 1 0)
						)
					)
				)
				(conclude)

				;else
				(assign (assoc probability_mass_feature_only .true ))
			)
		)

		;if no entitites were supplied, all cases without feature_name are found
		(if (= (null) entities)
			(assign (assoc
				entities
					(contained_entities
						(query_exists !internalLabelSession)
						(query_not_exists (if probability_mass_feature_only !internalLabelProbabilityMass feature_name) )
					)
			))
		)

		(if has_rebalance_features
			(if probability_mass_feature_only
				(map
					(lambda
						(accum_entity_roots (current_value) [ (set_labels default_value [!internalLabelProbabilityMass] ) ] )
					)
					entities
				)

				;else create both features
				(map
					(lambda
						(accum_entity_roots
							(current_value)
							(zip_labels [ feature_name !internalLabelProbabilityMass ] [ default_value default_value ] )
						)
					)
					entities
				)
			)

			;else create only the weight feature
			(map
				(lambda
					(accum_entity_roots (current_value) [ (set_labels default_value [feature_name] ) ] )
				)
				entities
			)
		)
	)
)