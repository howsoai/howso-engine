;Contains helper methods for calculating influences (MDa) for details and explanations.
(null

	;Helper method to compute and add to output local feature accuracy contributions
	#!ComputeFeatureAC
	(let
		(assoc
			case_ids
				(if (!= (null) cached_candidate_cases_map)
					(indices cached_candidate_cases_map)

					(contained_entities
						filtering_queries
						(query_nearest_generalized_distance
							k_parameter
							context_features
							context_values
							p_parameter
							feature_weights
							!queryDistanceTypeMap
							query_feature_attributes_map
							feature_deviations
							(null)
							dt_parameter
							(if valid_weight_feature weight_feature (null))
							;use a fixed random seed to guarantee deterministic behavior for reacts (named "fixed rand seed")
							"fixed rand seed"
							(null) ;radius
							!numericalPrecision
						)
					)
				)
		)

		;for robust computation, increase the number of cases to improve accuracy
		(if robust
			(let
				(assoc
					;number of robust cases is max(100, K * ( ln(num_features) + 1) )
					num_robust_cases
						(max
							300
							(*
								(if (~ 0 k_parameter) k_parameter 15)
								(+ 1 (log (size (values features (true) ))))
							)
						)
				)
				;pick the needed number of cases randomly with replacement from the set of specified case_ids
				(assign (assoc case_ids (rand case_ids num_robust_cases) ))
			)
		)

		(accum (assoc
			output
				(if robust
					(assoc "feature_robust_accuracy_contributions"
						(call !CalculateFeatureAccuracyContributions (assoc
							context_features context_features
							action_features action_features
							case_ids case_ids
							regional_data_only (true)
							robust (true)
						))
					)
					(assoc "feature_full_accuracy_contributions"
						(call !CalculateFeatureAccuracyContributions (assoc
							context_features context_features
							action_features action_features
							case_ids case_ids
							regional_data_only (true)
							robust (false)
						))
					)
				)
		))
	)

	;Helper method to compute and add to output local feature ex-post accuracy contributions
	#!ComputeFeatureACExPost
	(let
		(assoc
			;pull targetless hyperparameters because local data will include the action value and therefore is not targeted
			targetless_hyperparam_map
				(call !GetHyperparameters (assoc
					context_features context_features
					feature (null)
					weight_feature weight_feature
				))
		)

		(declare (assoc
			case_ids
				(contained_entities
					filtering_queries
					(query_nearest_generalized_distance
						(get targetless_hyperparam_map "k")
						features
						(append context_values action_values)
						(get targetless_hyperparam_map "p")
						(get targetless_hyperparam_map "featureWeights")
						!queryDistanceTypeMap
						(get targetless_hyperparam_map "featureDomainAttributes")
						(get targetless_hyperparam_map "featureDeviations")
						(null)
						(get targetless_hyperparam_map "dt")
						(if valid_weight_feature weight_feature (null))
						;use a fixed random seed to guarantee deterministic behavior for reacts (named "fixed rand seed")
						"fixed rand seed"
						(null) ;radius
						!numericalPrecision
					)
				)
			targetless_k (get targetless_hyperparam_map "k")
		))

		;for robust computation, increase the number of cases to improve accuracy
		(if robust
			(let
				(assoc
					;number of robust cases is max(100, K * ( ln(num_features) + 1) )
					num_robust_cases
						(max
							100
							(*
								(if (~ 0 targetless_k) targetless_k 15)
								(+ 1 (log (size (values features (true) ))))
							)
						)
				)
				;pick the needed number of cases randomly with replacament from the set of specified case_ids
				(assign (assoc case_ids (rand case_ids num_robust_cases) ))
			)
		)

		(accum (assoc
			output
				(if robust
					(assoc "feature_robust_accuracy_contributions_ex_post"
						(call !CalculateFeatureAccuracyContributions (assoc
							context_features context_features
							action_features action_features
							case_ids case_ids
							regional_data_only (true)
							robust (true)
						))
					)
					(assoc "feature_full_accuracy_contributions_ex_post"
						(call !CalculateFeatureAccuracyContributions (assoc
							context_features context_features
							action_features action_features
							case_ids case_ids
							regional_data_only (true)
							robust (false)
						))
					)
				)
		))
	)

	;Helper method to compute and add to output local cases MDA
	#!ComputeCaseAccuracyContributions
	(let
		(assoc
			local_data_cases_tuple
				(compute_on_contained_entities
					filtering_queries
					(query_nearest_generalized_distance
						k_parameter
						context_features
						context_values
						p_parameter
						feature_weights
						!queryDistanceTypeMap
						query_feature_attributes_map
						feature_deviations
						(first action_features)
						1 ; dt_parameter return actual distance
						(if valid_weight_feature weight_feature (null))
						;use a fixed random seed to guarantee deterministic behavior for reacts (named "fixed rand seed")
						"fixed rand seed"
						(null) ;radius
						!numericalPrecision
						(first action_features) ;output tuple of ordered lists
					)
				)

			action_feature (first action_features)
			action_is_nominal (contains_index !nominalsMap (first action_features))
			output_categorical_action_probabilities (false)
			cases_to_expected_values_map (assoc)
			cases_mdas (list)
			original_cap_map categorical_action_probabilities_map
		)

		;can't output mda if no action features specified
		(if (= (null) action_feature)
			(conclude (accum (assoc output (assoc (if robust "case_robust_accuracy_contributions" "case_full_accuracy_contributions") (null)) )))
		)

		(assign (assoc
			cases_to_expected_values_map (zip (first local_data_cases_tuple) (last local_data_cases_tuple))
			output_categorical_action_probabilities action_is_nominal
		))

		(assign (assoc
			cases_mdas
				;iterate over each case in the local data, hold it out and compute acccuracy of local data without it. ie, nested accuracy flow
				(map
					(lambda (let
						(assoc
							mda_case_id (current_value 1)
							remaining_k_cases_map (remove cases_to_expected_values_map (current_value 1))
							predictions (list)
							mda (null)
							robust_remaining_k_cases_map (null)
						)

						;for robust residuals ensure at least 30 cases per case are used by duplicating the remaining cases list enough times
						(if robust
							(assign (assoc
								robust_remaining_k_cases_map
									(if (< (size remaining_k_cases_map) 30)
										(apply "append" (range remaining_k_cases_map 1 (ceil (/ 30 (size remaining_k_cases_map))) 1 ))

										remaining_k_cases_map
									)
							))
						)

						;predict action feature for all the remaining k cases to compute the mda
						(assign (assoc
							predictions
								;remove any nulls
								(filter (values
									(map
										(lambda (let
											(assoc
												react_case (current_index 1)
												expected_value (current_value 1)
												reaction (null)
												ignore_case_list (null)
												;categorical_action_probabilities_map needs to be cleared before each reaction so that it can be accumulated correctly
												categorical_action_probabilities_map (assoc)
												abs_error_with_case 0
												abs_error_without_case 0
											)

											(assign (assoc
												ignore_case_list
													;remove a random selection of local cases in addition to the react case
													(if robust
														(append
															(list react_case)
															(filter (lambda (< (rand) .5)) (indices (remove remaining_k_cases_map react_case)) )
														)

														(list react_case)
													)
											))

											(assign (assoc
												reaction_with_case
													(call !GenerateReaction (assoc
														custom_extra_filtering_queries (query_not_in_entity_list ignore_case_list)
														context_values (retrieve_from_entity react_case context_features)
													))
											))

											;if nominal, return the caterogorical action probability for the expected class
											(if action_is_nominal
												(seq
													(assign (assoc
														reaction_with_case
															(get categorical_action_probabilities_map (list action_feature expected_value) )
													))
													;cap needs to be cleared before each reaction so that it can be accumulated correctly
													(assign (assoc categorical_action_probabilities_map (assoc)))
												)

												(assign (assoc
													abs_error_with_case (abs (- expected_value reaction_with_case))
												))
											)

											(assign (assoc
												reaction_without_case
													(call !GenerateReaction (assoc
														custom_extra_filtering_queries (query_not_in_entity_list (append mda_case_id ignore_case_list))
														context_values (retrieve_from_entity react_case context_features)
													))
											))
											(if action_is_nominal
												(assign (assoc
													reaction_without_case
														(get categorical_action_probabilities_map (list action_feature expected_value) )
												))

												(assign (assoc
													abs_error_without_case (abs (- expected_value reaction_without_case))
												))
											)

											;output delta between with case and without case, if both are null output a 0
											(if (or (= (null) reaction_with_case) (= (null) reaction_without_case))
												0

												(if action_is_nominal
													;if removing a case dropped accuracy from .9 to .8, we want a positive value since this case contributed 0.1 to accuracy
													;but if removing a case improves accuracy, this diff should be negative
													(- reaction_with_case reaction_without_case)

													;else take the differences in the absolute errors for continuous
													;if error without case increased, this case contributed positively to accuracy.
													(- abs_error_without_case abs_error_with_case)
												)
											)
										))

										;iterate over the possibly larger list if robust
										(if robust
											robust_remaining_k_cases_map

											remaining_k_cases_map
										)
									)
								))
						))

						(assign (assoc mda (generalized_mean predictions) ))

						(append
							(retrieve_from_entity mda_case_id (zip (list !internalLabelSession !internalLabelSessionTrainingIndex)))
							(assoc "accuracy_contribution" mda)
						)
					))
					(first local_data_cases_tuple)
				)
		))
		;restore categorical action probabilities map to whatever it was for the original react
		(assign (assoc categorical_action_probabilities_map original_cap_map))

		(accum (assoc
			output
				(if robust
					(assoc "case_robust_accuracy_contributions" cases_mdas)
					(assoc "case_full_accuracy_contributions" cases_mdas)
				)
		))
	)

	;Helper method to compute and add to output local data case contributions
	#!ComputeCaseContributionsFull
	(let
		(assoc
			local_cases_tuple
				(compute_on_contained_entities
					filtering_queries
					(query_nearest_generalized_distance
						k_parameter
						context_features
						context_values
						p_parameter
						feature_weights
						!queryDistanceTypeMap
						query_feature_attributes_map
						feature_deviations
						(first action_features)
						dt_parameter
						(if valid_weight_feature weight_feature (null))
						;use a fixed random seed to guarantee deterministic behavior for reacts (named "fixed rand seed")
						"fixed rand seed"
						(null) ;radius
						!numericalPrecision
						(first action_features)
					)
				)
			original_local_cases (list)
			extended_cases_map (assoc)
			action_value (first action_values)
			action_feature (first action_features)
			nom_action_prob (null)
			original_cap_map categorical_action_probabilities_map
		)

		;can't output contributions if no action features specified
		(if (= (null) action_feature)
			(conclude (accum (assoc output (assoc "case_full_prediction_contributions" (null)) )))
		)

		(assign (assoc
			original_local_cases (first local_cases_tuple)
			extended_cases_map (zip (first local_cases_tuple) (get local_cases_tuple 1))
			extended_cases_values_map (zip (first local_cases_tuple) (last local_cases_tuple))
		))

		;for nominals, the delta is computed off the categorical action probability of this action value
		(if (contains_index !nominalsMap action_feature)
			(assign (assoc
				nom_action_prob (get categorical_action_probabilities_map (list action_feature action_value))
			))
		)

		;populate assoc of local case id -> assoc of session value and session training index value
		(declare (assoc
			local_case_session_values_map
				(compute_on_contained_entities
						(query_in_entity_list original_local_cases)
						(query_exists !internalLabelSession)
						(query_exists !internalLabelSessionTrainingIndex)
				)
		))

		(declare (assoc
			case_contributions_map
				(map
					(lambda (let
						(assoc
							filtered_extended_cases_map (remove extended_cases_map (current_index 1))
							total_influence 0
							case_action_value (null)
							delta_value 0
						)

						;update influences to ignore the current case
						(assign (assoc
							filtered_extended_cases_map (normalize filtered_extended_cases_map)
							categorical_action_probabilities_map (assoc)
						))

						(assign (assoc
							case_action_value
								(if (and (not allow_nulls) (= 0 (size filtered_extended_cases_map)))
									(call !CalculateFeatureExpectedValue (assoc feature action_feature allow_nulls (false)))

									(call !InterpolateActionValues (assoc
										action_feature action_feature
										candidate_case_ids (indices filtered_extended_cases_map)
										candidate_case_weights (values filtered_extended_cases_map)
										candidate_case_values (unzip extended_cases_values_map (indices filtered_extended_cases_map))
										allow_nulls allow_nulls
									))
								)
						))

						(assign (assoc
							delta_value
								;nominal features return delta in probabilities of selecting this action value
								(if (contains_index !nominalsMap action_feature)
									(let
										(assoc case_action_prob (get categorical_action_probabilities_map (list action_feature action_value)) )
										;if this action value wasn't in the local data, that's same as having a 0 case_action_prob
										(if (= (null) case_action_prob)
											(- 0 nom_action_prob)

											(- case_action_prob nom_action_prob)
										)
									)

									;else continuous, return the delta if it can be computed (non-null)
									(if (and allow_nulls (contains_value (list case_action_value action_value) (null)) )
										(null)

										(- case_action_value action_value)
									)
								)
						))

						;output assoc with session, session index and delta value
						(append
							(get local_case_session_values_map (current_index))
							(assoc "full_prediction_contribution" delta_value)
						)
					))
					(zip original_local_cases)
				)
		))

		;restore categorical action probabilities map to whatever it was for the original react
		(assign (assoc categorical_action_probabilities_map original_cap_map))

		(accum (assoc output (assoc "case_full_prediction_contributions" (unzip case_contributions_map original_local_cases)) ))
	)

	;Helper method to compute and add to output robust local data case contributions
	#!ComputeCaseContributionsRobust
	(let
		(assoc
			extended_local_cases_tuple
				(compute_on_contained_entities
					filtering_queries
					(query_nearest_generalized_distance
						;use 30 for dynamic k
						(if (~ 0 k_parameter) (* 2 k_parameter) 30)
						context_features
						context_values
						p_parameter
						feature_weights
						!queryDistanceTypeMap
						query_feature_attributes_map
						feature_deviations
						(first action_features)
						dt_parameter
						(if valid_weight_feature weight_feature (null))
						;use a fixed random seed to guarantee deterministic behavior for reacts (named "fixed rand seed")
						"fixed rand seed"
						(null) ;radius
						!numericalPrecision
						(first action_features)
					)
				)
			original_local_cases (list)
			local_cases_map (assoc)
			non_local_cases (list)
			non_local_cases_map (assoc)
			action_value (first action_values)
			action_feature (first action_features)
			nom_action_prob (null)
			original_cap_map categorical_action_probabilities_map
			;set local_k to be 15 for dynamic k
			local_k (if (~ 0 k_parameter) k_parameter 15)
		)

		(assign (assoc
			original_local_cases (trunc (first extended_local_cases_tuple) local_k)
			non_local_cases (tail (first extended_local_cases_tuple) local_k)
		))
		(assign (assoc
			local_cases_map
				(zip
					original_local_cases
					(trunc (get extended_local_cases_tuple 1) local_k)
				)
			non_local_cases_map
				(zip
					non_local_cases
					(tail (get extended_local_cases_tuple 1) local_k)
				)
			extended_local_values_map (zip (first extended_local_cases_tuple) (last extended_local_cases_tuple))
		))

		;for nominals, the delta is computed off the categorical action probability of this action value
		(if (contains_index !nominalsMap action_feature)
			(assign (assoc
				nom_action_prob (get categorical_action_probabilities_map (list action_feature action_value))
			))
		)

		;populate assoc of local case id -> assoc of session value and session training index value
		(declare (assoc
			local_case_session_values_map
				(compute_on_contained_entities
						(query_in_entity_list original_local_cases)
						(query_exists !internalLabelSession)
						(query_exists !internalLabelSessionTrainingIndex)
				)
		))

		;iterate for every case in the local data:
		;hold it out and then 100x hold out random sample of other local cases, replace the holdouts with nearest non local ones and average out the action value
		(declare (assoc
			case_contributions_map
				(map
					(lambda (let
						(assoc
							filtered_local_cases_map (remove local_cases_map (current_index 1))
							total_influence 0
							case_action_value (null)
							robust_deltas (list)
						)

						;run 100 iterations of holding out random cases, replacing them from the extended data and computing the delta
						;average out the delta at the end
						(assign (assoc
							robust_deltas
								(range
									(lambda (let
										(assoc
											;randomly hold out random local cases
											robust_cases_map (filter (lambda (< (rand) .5)) filtered_local_cases_map)
											num_to_replace 0
										)
										(assign (assoc num_to_replace (- k_parameter (size robust_cases_map)) ))

										;fill up the remaining local cases with non local extended data ones
										(accum (assoc
											robust_cases_map (keep non_local_cases_map (trunc non_local_cases num_to_replace))
										))

										;convert distances to weights
										(assign (assoc
											robust_cases_map (normalize robust_cases_map)
											categorical_action_probabilities_map (assoc)
										))

										(assign (assoc
											case_action_value
												(if (and (not allow_nulls) (= 0 (size robust_cases_map)))
													(call !CalculateFeatureExpectedValue (assoc feature action_feature allow_nulls (false)))

													(call !InterpolateActionValues (assoc
														action_feature action_feature
														candidate_case_ids (indices robust_cases_map)
														candidate_case_weights (values robust_cases_map)
														candidate_case_values (unzip extended_local_values_map (indices robust_cases_map))
														allow_nulls allow_nulls
													))
												)
										))

										;nominal features return delta in probabilities of selecting this action value
										(if (contains_index !nominalsMap action_feature)
											(let
												(assoc case_action_prob (get categorical_action_probabilities_map (list action_feature action_value)) )
												;if this action value wasn't in the local data, that's same as having a 0 case_action_prob
												(if (= (null) case_action_prob)
													(- 0 nom_action_prob)

													(- case_action_prob nom_action_prob)
												)
											)

											;else continuous, return the delta
											(- case_action_value action_value)
										)
									))
									1 100 1
								)
						))

						;output assoc with session, session index and the average delta value
						(append
							(get local_case_session_values_map (current_index))
							(assoc "robust_prediction_contribution" (/ (apply "+" robust_deltas) 100) )
						)
					))
					local_cases_map
				)
		))

		;restore categorical action probabilities map to whatever it was for the original react
		(assign (assoc categorical_action_probabilities_map original_cap_map))

		(accum (assoc output (assoc "case_robust_prediction_contributions" (unzip case_contributions_map original_local_cases)) ))
	)

	;Helper method to compute and output local feature contributions
	#!ComputeFeatureContributions
	(let
		(assoc
			case_ids
				(if (!= (null) cached_candidate_cases_map)
					(indices cached_candidate_cases_map)

					(contained_entities
						filtering_queries
						(query_nearest_generalized_distance
							k_parameter
							context_features
							context_values
							p_parameter
							feature_weights
							!queryDistanceTypeMap
							query_feature_attributes_map
							feature_deviations
							(first action_feature)
							dt_parameter
							(if valid_weight_feature weight_feature (null))
							;use a fixed random seed to guarantee deterministic behavior for reacts (named "fixed rand seed")
							"fixed rand seed"
							(null) ;radius
							!numericalPrecision
						)
					)
				)
			action_feature (first action_features)

			;use larger amount of robust computes per case for details than global
			; min(2000, 30 * 2^f) to get enough coverage for robust reacts for each case
			num_robust_prediction_contributions_samples_per_case
				(if robust
					;use specified value if it was provided
					(if (> (get details "num_robust_prediction_contributions_samples_per_case") 1)
						(get details "num_robust_prediction_contributions_samples_per_case")

						(min 2000 (* 30 (pow 2 (size context_features)) ) )
					)
				)
		)

		(declare (assoc
			feature_contributions_pair
				(call !CalculateFeaturePredictionContributions (assoc
					context_features context_features
					action_feature action_feature
					robust robust
					case_ids case_ids
					num_robust_prediction_contributions_samples_per_case num_robust_prediction_contributions_samples_per_case
					weight_feature weight_feature
					run_on_local_model (true)
				))
		))

		(accum (assoc
			output
				(if robust
					(assoc
						"feature_robust_prediction_contributions" (first feature_contributions_pair)
						"feature_robust_directional_prediction_contributions" (last feature_contributions_pair)
					)
					(assoc
						"feature_full_prediction_contributions" (first feature_contributions_pair)
						"feature_full_directional_prediction_contributions" (last feature_contributions_pair)
					)
				)
		))
	)

	;Helper method to compute and output individual case feature contributions
	#!ComputeCaseFeatureContributions
	(let
		(assoc
			;params needed for contributions code
			context_set (zip context_features)
			all_context_features context_features
			all_context_features_indices_map (zip context_features (indices context_features))
			action_is_nominal (contains_index !nominalsMap (first action_features))
			hyperparam_map
				(call !GetHyperparameters (assoc
					feature (first action_features)
					context_features context_features
					weight_feature weight_feature
				))
			num_features (size context_features)
			action_feature (first action_features)

			;use larger amount of robust computes per case for details than global
			; min(2000, 30 * 2^f) to get enough coverage for robust reacts for each case
			num_robust_prediction_contributions_samples_per_case
				(if robust
					;use specified value if it was provided
					(if (> (get details "num_robust_prediction_contributions_samples_per_case") 1)
						(get details "num_robust_prediction_contributions_samples_per_case")

						(min 2000 (* 30 (pow 2 (size context_features)) ) )
					)
				)

			case_id (if ignore_case ignore_case)

			correct_value (first action_values)
			case_values_map (zip features (append context_values action_values))

			;populated by !ComputeRobustContributionReactsPerCase
			local_case_reaction_pairs (list)
			feature_contributions_pair_map (assoc)
		)

		;create an assoc of feature -> [feature_contribution, directional_feature_contribution]
		(assign (assoc
			feature_contributions_pair_map
				(if robust
					(seq
						;populate local_case_reaction_pairs, then the call !below computes the deltas for each feature
						(call !ComputeRobustContributionReactsPerCase)
						(zip context_features (call !ComputeRobustWithAndWithoutDeltaPerCase))
					)

					(map
						(lambda (let
							(assoc
								feature (get_value (current_index 1))
								filtered_context_features (indices (remove context_set (current_index 1)))
							)
							;output a delta value between react with and without the feature
							(call !ComputeFullContributionForCase)
						))
						(if details_context_features
							(zip details_context_features)

							context_set
						)
					)
				)
		))

		(if (and robust details_context_features)
			(assign (assoc feature_contributions_pair_map (keep feature_contributions_pair_map details_context_features)))
		)

		(accum (assoc
			output
				(if robust
					(assoc
						"feature_robust_prediction_contributions_for_case"  (map (lambda (first (current_value))) feature_contributions_pair_map)
						"feature_robust_directional_prediction_contributions_for_case"  (map (lambda (last (current_value))) feature_contributions_pair_map)
					)
					(assoc
						"feature_full_prediction_contributions_for_case"  (map (lambda (first (current_value))) feature_contributions_pair_map)
						"feature_full_directional_prediction_contributions_for_case"  (map (lambda (last (current_value))) feature_contributions_pair_map)
					)
				)
		))
	)
)