(null

	#!Clustering
	(seq
		(declare (assoc
			weight_feature_param
			 	(if (and use_case_weights (or !hasPopulatedCaseWeight (!= weight_feature ".case_weight")))
			 		weight_feature
			 	)
			effective_dimensions
				(+
					;include an extra dimension for another potential fractal dimension in the data
					1
					(round
						(exp
							(generalized_mean (values (map
								(lambda (entropy
									(values (remove (current_value) [(current_index 1)]))
								))
								(get hyperparam_map "featureMdaMap")
							)))
						)
						1
					)
				)
		))

		;add cluster id to all clustered cases
		(map
			(lambda
				;write clusters as new feature value in 'clustering' (default value of ".cluster_id") to all the cases
				(accum_entity_roots (current_index) (set_labels (current_value) [clustering]))
			)
			(compute_on_contained_entities
				||(query_entity_clusters
					;ensure enough cases are captured, but range between 1x and 3x the effective dimension
					;include an additional points on the end to capture potential clusters connectivity
					[0.05 (max 5 effective_dimensions) (max 20 (* 3 effective_dimensions) ) 3]
					features
					3  ;cluster size must be at least 3 points (+ effective_dimensions 1)
					p_parameter
					feature_weights
					!queryDistanceTypeMap
					query_feature_attributes_map
					feature_deviations
					(null)
					(if (= "surprisal_to_prob" dt_parameter) "surprisal" 1)
					weight_feature_param
					"tie_break_random_seed"
					(null) ;radius
					!numericalPrecision
				)
			)
		)
	)

	;Helper method for react's !GenerateReaction to prepare the prediction of cluster id by computing
	;the DC and SC of the case to use in the contexts for the prediction
	#!PreparePredictionClusterId
	(let
		(assoc
			computed_sc_map
				(if existing_case_id
					(zip
						["distance_contribution" "similarity_conviction"]
						(retrieve_from_entity existing_case_id
							[
							(get !clusteringParametersMap "distance_contribution_feature")
							(get !clusteringParametersMap "similarity_conviction_feature")
							]
						)
					)

					;else compute DC and SC
					(call !SimilarityConviction (assoc
						features context_features
						feature_values context_values
						filtering_queries filtering_queries
						use_case_weights use_case_weights
						weight_feature weight_feature
						distance_contribution_feature (get !clusteringParametersMap "distance_contribution_feature")
					))
				)
		)

		;place these computed DC and SC values into details
		(if details
			(accum (assoc
				details
					{
						"non_clustered_distance_contribution" (get computed_sc_map "distance_contribution")
						"non_clustered_similarity_conviction" (get computed_sc_map "similarity_conviction")
					}
			))
			(assign (assoc
				details
					{
						"non_clustered_distance_contribution" (get computed_sc_map "distance_contribution")
						"non_clustered_similarity_conviction" (get computed_sc_map "similarity_conviction")
					}
			))
		)

		;output the cluster id stored in the case
		(if existing_case_id
			(conclude
				(retrieve_from_entity existing_case_id (get !clusteringParametersMap "feature"))
			)
		)

		(assign (assoc
			unmodified_context_features (replace context_features)
			unmodified_context_values (replace context_values)
		))

		;add computed action DC and SC into contexts
		(assign (assoc
			restore_contexts .true
			context_features
				(append
					context_features
					(get !clusteringParametersMap "distance_contribution_feature")
					(get !clusteringParametersMap "similarity_conviction_feature")
				)
			context_values
				(append
					context_values
					(get computed_sc_map "distance_contribution")
					(get computed_sc_map "similarity_conviction")
				)
		))
	)

)