(null
	#!ComputeValueRobustAccuracyContributions
	(declare
		(assoc
			action_feature (null)
			focus_features (null)
			max_num_buckets (null)
			features []
		)

		(declare (assoc
			context_features (filter (lambda (not (contains_value (append action_feature focus_features) (current_value)))) features)
			case_features (values (append features (if !tsTimeFeature [".series_index"] [])) .true)
		))

		;create a map of feature -> flag, set to true if the feature has any null values
		;that way there's no need to explicitly filter nulls out when querying for a feature that no nulls
		;or if it's inactive
		(declare (assoc
			ignore_null_action_feature
				(and
					(!= .false (get !featureNullRatiosMap (list action_feature "has_nulls")) )
					(not (contains_index !inactiveFeaturesMap action_feature) )
				)
			;create a backup of context_features for time series flows
			original_context_features (if !tsTimeFeature (replace context_features))
			num_features (size context_features)

			feature_is_nominal (contains_index !nominalsMap action_feature)
			feature_is_edit_distance (contains_index !editDistanceFeatureTypesMap action_feature)
			feature_is_dependent (contains_index !dependentFeatureMap action_feature)
		))
		(if (= (null) hyperparam_map)
			(assign (assoc
				hyperparam_map
					(call !GetHyperparameters (assoc
						context_features features
						weight_feature weight_feature
						use_case_weights use_case_weights
					))
			))
		)

		(call !UpdateCaseWeightParameters)

		(assign (assoc
			k_parameter (get hyperparam_map "k")
			p_parameter (get hyperparam_map "p")
			dt_parameter (get hyperparam_map "dt")
			query_feature_attributes_map (get hyperparam_map "featureDomainAttributes")

			;store an assoc of lag/rate/delta feature -> lag/order amount for time series flows
			ts_feature_lag_amount_map (if !tsTimeFeature (call !BuildTSFeatureLagAmountMap))
		))

		(declare (assoc
			enabled_num_features_probability_map (call !ComputeNumEnabledFeaturesProbabilitiesMap (assoc num_features num_features))
			all_features_map (zip features)
			feature_deviations (get hyperparam_map "featureDeviations")
			feature_is_non_string_edit_distance (and feature_is_edit_distance (!= "string_mixable" (get !editDistanceFeatureTypesMap action_feature)))
		))

		(declare (assoc
			case_acs
				||(map
					(lambda (let
						(assoc
							case_id (current_value 1)
							;map of feature -> value for all the case values
							case_values_map (zip case_features (retrieve_from_entity (current_value 1) case_features) )
							time_series_filter_query (list)
							feature_weights (get hyperparam_map "featureWeights")
							context_features context_features
							categorical_action_probabilities_map (assoc)
						)
						(declare (assoc case_feature_value (get case_values_map action_feature) ))

						(if !tsTimeFeature
							(assign (assoc
								context_features
									(call !FilterContextsBySeriesIndex (assoc
										all_context_features original_context_features
										series_index (get case_values_map ".series_index")
									))
							))
						)

						(declare (assoc
							;filter out a random number of context features
							react_context_features
								(rand
									context_features
									;number of features to enable is probabilistically weighted
									(if (and !tsTimeFeature (!= (size context_features) num_features))
										(rand
											(call !ComputeNumEnabledFeaturesProbabilitiesMap (assoc num_features num_features))
										)

										(rand enabled_num_features_probability_map)
									)
									;unique random features (not with replacement)
									.true
								)
						))

						;assoc of all removed features to their residual values, filter the action features to be those that are not context features
						(declare (assoc removed_features_map (remove all_features_map react_context_features) ))

						(if !tsTimeFeature
							(let
								(assoc context_map (keep case_values_map react_context_features) )
								(if (contains_index context_map !tsTimeFeature)
									(assign (assoc time_series_filter_query (call !ComputeTimeSeriesFilterFutureCasesQuery) ))
								)
							)
						)

						;use dynamic deviations subtrainee if present
						(if (get hyperparam_map "subtraineeName")
							(call !UseDynamicDeviationsAndWeights (assoc
								context_features react_context_features
								context_values (unzip case_values_map react_context_features)
								hyperparam_map hyperparam_map
							))
						)

						(if feature_is_dependent
							(declare (assoc
								dependent_queries_list
									(call !ComputeDependentQueries (assoc
										context_features react_context_features
										context_values (unzip case_values_map react_context_features)
										action_feature action_feature
									))
							))
						)

						;do a num_reacts_per_case of robust reacts without any of the focus features
						(declare (assoc
							without_focus_features_action_diff
								(call !InterpolateAndComputeDiffToCase (assoc
									feature action_feature
									output_categorical_action_probabilities .true
									candidate_cases_lists
										(if (= 0 (size  react_context_features))
											[[][][]]
											(compute_on_contained_entities
												(query_not_in_entity_list [(replace case_id)])
												(if ignore_null_action_feature (query_not_equals action_feature (null)))
												(if (size context_condition_filter_query) context_condition_filter_query)
												dependent_queries_list
												time_series_filter_query
												(query_nearest_generalized_distance
													k_parameter
													(replace react_context_features)
													(replace (unzip case_values_map react_context_features))
													p_parameter
													feature_weights
													!queryDistanceTypeMap
													query_feature_attributes_map
													feature_deviations
													(null)
													dt_parameter
													(if valid_weight_feature weight_feature)
													tie_break_random_seed
													(null) ;radius
													!numericalPrecision
													action_feature
												)
											)
										)
								))
						))

						;append focus features
						(accum (assoc react_context_features focus_features ))
						(assign (assoc
							removed_features_map (remove removed_features_map focus_features)
							categorical_action_probabilities_map (assoc)
						))

						(if !tsTimeFeature
							(let
								(assoc context_map (keep case_values_map react_context_features) )
								(if (contains_index context_map !tsTimeFeature)
									(assign (assoc time_series_filter_query (call !ComputeTimeSeriesFilterFutureCasesQuery) ))
								)
							)
						)

						;use dynamic deviations subtrainee if present
						(if (get hyperparam_map "subtraineeName")
							(call !UseDynamicDeviationsAndWeights (assoc
								context_features react_context_features
								context_values (unzip case_values_map react_context_features)
								hyperparam_map hyperparam_map
							))
						)

						(if feature_is_dependent
							(assign (assoc
								dependent_queries_list
									(call !ComputeDependentQueries (assoc
										context_features react_context_features
										context_values (unzip case_values_map react_context_features)
										action_feature action_feature
									))
							))
						)

						(declare (assoc
							with_focus_features_action_diff
								(call !InterpolateAndComputeDiffToCase (assoc
									feature action_feature
									output_categorical_action_probabilities .true
									candidate_cases_lists
										(compute_on_contained_entities
											(query_not_in_entity_list [(replace case_id)])
											(if ignore_null_action_feature (query_not_equals action_feature (null)))
											(if (size context_condition_filter_query) context_condition_filter_query)
											dependent_queries_list
											time_series_filter_query
											(query_nearest_generalized_distance
												k_parameter
												(replace react_context_features)
												(replace (unzip case_values_map react_context_features))
												p_parameter
												feature_weights
												!queryDistanceTypeMap
												query_feature_attributes_map
												feature_deviations
												(null)
												dt_parameter
												(if valid_weight_feature weight_feature)
												tie_break_random_seed
												(null) ;radius
												!numericalPrecision
												action_feature
											)
										)
								))
						))

						;output a pair of : [ AC for the focus feature, focus values ]
						[
							(-
								without_focus_features_action_diff
								with_focus_features_action_diff
							)
							(unzip case_values_map focus_features)
						]
					))
					case_ids
				)
		))

		;a index map to match index of focus feature to focus value
		(declare (assoc
			focus_feature_index_map (zip focus_features (range 0 (- (size focus_features) 1)))
		))

		;map of continuous feature -> buckets map (assoc of bucket -> [min max])
		(declare (assoc
			continuous_feature_buckets_map
				(map
					(lambda (let
						(assoc focus_feature_index (get focus_feature_index_map (current_index 1)) )
						(call !BinValuesByQuantiles (assoc
							max_num_buckets max_num_buckets
							sorted_unique_values
								;sort is descending order
								(sort
									(lambda (< (current_value) (current_value 1)) )
									(values
										(map (lambda (get (current_value) [1 focus_feature_index])) case_acs)
										.true
									)
								)
						))
					))
					(zip (filter
						(lambda (not (contains_index !nominalsMap (current_value))))
						focus_features
					))
				)
		))

		;Iterate over the focus features, outputing values
		;process each feature in the order of focus_features, such that for each value of feature 1, it outputs the sorted values of feature 2, etc.
		(declare (assoc
			values_map
				(call !RecursivelyProcessFocusFeatures (assoc
					remaining_focus_features (tail focus_features)
					feature_case_ac_map
						(if (contains_index !nominalsMap (first focus_features))
							(call !ComputeNominalValueRobustACMap (assoc
								case_acs
									(map
										(lambda [ (first (current_value 1)) (get (current_value 1) [1 0]) ])
										case_acs
									)
							))
							(call !ComputeContinuousValueRobustACMap (assoc
								case_acs
									(map
										(lambda [ (first (current_value 1)) (get (current_value 1) [1 0]) ])
										case_acs
									)
							))
						)
					next_focus_index 1
					feature_indices [0]
					feature_values []
				))
		))

		(declare (assoc
			all_values_lists
				(apply "append" (values
					(call !CollateNestedAssocIntoLists (assoc
						nested_assoc values_map
						values_lists []
					))
				))
		))

		;split the lists of all values into two corresponding lists for output: just the unique focus values and their corresponding ACs
		{
			"focus_values"
				;replace buckets with pairs of [min max] for each continuous feature
				(if (size continuous_feature_buckets_map)
					(map
						(lambda
							;iterate over the values in the row
							(map
								(lambda
									(if (contains_index !nominalsMap (get focus_features (current_index)))
										(current_value)
										;else convert bucket to pair of min-max for this continuous feature
										(get continuous_feature_buckets_map [(get focus_features (current_index 1)) (current_value 1)])
									)
								)
								(trunc (current_value))
							)
						)
						all_values_lists
					)

					;else truncate the ac values, keep the rest as-is
					(map (lambda (trunc (current_value))) all_values_lists)
				)
			"ac_values" (map (lambda (last (current_value))) all_values_lists)
		}
	)

	;sort the class/buckets descending by AC, and output the corresponding values to the class/bucket
	;if there are remaining focus features, process them recursively
	;
	;parameters:
	; feature_case_ac_map: the current feature's case_ac_map of class/bucket -> [ ac [min max] ]
	; remaininy_focus_features: remaining focus features if there were multiple features
	; next_focus_index: the index of the next focus feature (relative to the original focus_features parameter), if there are any remaining focus features
	; feature_indices: list of the previous focus feature indices, if there are remaining focus features
	; feature_values: list of previous focus feature values, if there are remaining focus features
	#!RecursivelyProcessFocusFeatures
	;iterate over the sorted classes/buckets (by descending AC)
	(map
		(lambda
			;sort the classes/buckets of the next remaining feature
			(if (size remaining_focus_features)
				(let
					(assoc
						;flag if current focus feature being processed is continuous
						is_continuous (not (contains_index !nominalsMap (get focus_features (- next_focus_index 1)) ))
						current_focus_index (- next_focus_index 1)
					)

					;if this feature is continuous, get the min-max values for this bucket
					(declare (assoc
						previous_feature_values
							(append
								feature_values
								(if is_continuous
									;for continuous accumulate the pair of min/max of values this bucket
									(let
										(assoc
											min (get (current_value 2) [1 0] )
											max (get (current_value 2) [1 1] )
										)
										;output as a nested pair to ensure it accumulates to the list as a pair
										[[ min max ]]
									)

									;for nominal accumulate the class value itself
									(current_index 1)
								)
							)
					))

					;list of pairs of [ac next_feature_value ]
					(declare (assoc
						next_feature_acs
							(map
								(lambda [
									(first (current_value 1))
									(get (current_value 1) [1 next_focus_index])
								])

								(if (> (size previous_feature_values) 1)
									(filter
										(lambda
											(apply "and" (map
												(lambda
													;continuous values will be a pair of [min max] for a value to fall between
													(if (~ [] (first (current_value)))
														(and
															(<= (get (current_value) [0 0] ) (last (current_value)) )
															(>= (get (current_value) [0 1] ) (last (current_value)) )
														)

														;else nominal, must match exactly
														(= (first (current_value)) (last (current_value)))
													)
												)
												;grab the values to filter by
												previous_feature_values
												;grab the values for this case's ac
												(unzip (last (current_value)) feature_indices)
											))
										)
										case_acs
									)

									;same code but optimized for the one current feature
									(if is_continuous
										(filter
											(lambda (and
												;continuous values will be a pair of [min max] for a value to fall between
												(<= (get previous_feature_values [0 0]) (get (current_value) [1 current_focus_index]) )
												(>= (get previous_feature_values [0 1]) (get (current_value) [1 current_focus_index]) )
											))
											case_acs
										)

										;else if current focus feature is nominal
										(filter
											(lambda
												;must match current nominal value exactly
												(= (first previous_feature_values) (get (current_value) [1 current_focus_index]) )
											)
											case_acs
										)
									)
								)
							)
					))

					(call !RecursivelyProcessFocusFeatures (assoc
						remaining_focus_features (tail remaining_focus_features)
						feature_case_ac_map
							(if (contains_index !nominalsMap (first remaining_focus_features))
								(call !ComputeNominalValueRobustACMap (assoc case_acs next_feature_acs ))
								(call !ComputeContinuousValueRobustACMap (assoc
									case_acs next_feature_acs
									feature (first remaining_focus_features)
								))
							)
						next_focus_index (+ 1 next_focus_index)
						feature_indices (append feature_indices next_focus_index)
						feature_values previous_feature_values
					))
				)

				;else output the AC values for this class/bucket
				(first (current_value))
			)
		)
		feature_case_ac_map
	)

	;output responsible values for a continuous feature
	;output map of bucket -> pair of: [ mean ac  [ min value, max value ] ]
	;
	;parameters:
	; case_acs: list of pairs of [ac_value feature_value ]
	#!ComputeContinuousValueRobustACMap
	(map
		(lambda (let
			(assoc
				bucket (current_index 1)
				min (first (current_value 1))
				max (last (current_value 1))
			)

			;output a tuple of:  [ mean ac, [values tuple]]
			[
				(generalized_mean
					;all the ac values for this bucket of continuous values inbetween [min, max]
					(map
						(lambda (first (current_value)))
						(filter
							(lambda  (and
								(>= (last (current_value)) min)
								(<= (last (current_value)) max)
							))
							case_acs
						)
					)
				)
				;values tuple of [mean, min, max]
				(current_value 1)
			]
		))
		(get continuous_feature_buckets_map feature)
	)

	;output responsible values for a nominal feature
	;output an assoc of class -> [mean ac]
	;
	;parameters:
	; case_acs: list of pairs of [ac_value feature_value ]
	#!ComputeNominalValueRobustACMap
	(map
		(lambda
			;output mean ac values
			(if (~ [] (current_value))
				[(generalized_mean (current_value 1))]
				[(current_value 1)]
			)
		)
		;accumulate the AC values for each class to create an assoc of class -> list of ac values
		(zip
			(lambda
				(append (current_value) (current_value 1))
			)
			(map (lambda (last (current_value))) case_acs)
			(map (lambda (first (current_value))) case_acs)
		)
	)

	;given a case_acs for a continuous feature, bin them into buckets by quantiles
	;outputs map of bucket -> values pair of: [ min, max]
	;
	;parameters:
	; sorted_unique_values: list of all sorted unique values in descending order
	; max_num_buckets: optional number of maximum buckets to bin values
	#!BinValuesByQuantiles
	(declare
		(assoc
			sorted_unique_values []
			max_num_buckets 30
		)

		(declare (assoc
			num_buckets (min max_num_buckets (size sorted_unique_values))
			total_num (size sorted_unique_values)
		))

		;assoc of bucket -> values pair of:  min, max] of all the unique values that go into that bucket
		(zip
			(range 0 (- num_buckets 1))

			(range
				(lambda (let
					(assoc
						;start and end percentage for each bucket
						;e.g., if there were two buckets, first bucket these values would be 0 and 0.5, and for the second bucket they'd be 0.5 and 1
						start_quantile_percentage (* (/ 1 num_buckets) (- (current_index 1) 1))
						end_quantile_percentage (* (/ 1 num_buckets) (current_index 1))
					)
					(declare (assoc
						index_quantile_low (floor (* total_num  start_quantile_percentage))
						index_quantile_high (- (floor (* total_num end_quantile_percentage)) 1)
					))
					(declare (assoc
						sorted_bucket_values (unzip sorted_unique_values (range index_quantile_low index_quantile_high))
					))

					;values pair of [ min, max]
					[ (last sorted_bucket_values) (first sorted_bucket_values) ]
				))
				1 num_buckets 1
			)
		)
	)

	;helper method to collate nested assocs into flat lists of all possible unique values,
	;while filtering out any list whose leaf node is null
	;
	;example:
	; {
	;	A {
	;		b 0
	;		c 1
	;		d { g 2 }
	;	}
	;	X {
	;		y 3
	;		z 4
	;	}
	; }
	; will be collated into:
	; [
	; 	[A b 0]
	; 	[A c 1]
	; 	[A d g 2]
	; 	[X y 3]
	; 	[X z 4]
	; ]
	#!CollateNestedAssocIntoLists
	(filter (map
		(lambda
			(if (~ (assoc) (current_value))
				(apply "append" (values
					(call !CollateNestedAssocIntoLists (assoc
						nested_assoc (current_value 1)
						values_lists (append values_lists (current_index 1))
					))
				))

				;else (current_value) is a leaf value, append it as the last value in the list
				(!= (null) (current_value))
				[(append values_lists (current_index 1) (current_value 1))]

				;leave null leaf values so they'll be filtered out
			)
		)
		nested_assoc
	))

)