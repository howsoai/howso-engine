;Contains methods for retrieving sessions.
(null

	;returns an array of ids of all replay sessions that exist in this trained model
	#!GetSessionIds
	(contained_entities (list (query_exists ".replay_steps") ))

	;returns a list of all of the training sessions, assoc of id->session, and whatever other attributes specified.
	#get_sessions
	(declare
		(assoc
			;{type "list" values "string"}
			;optional list of metadata attributes to return from the session
			attributes (list)
		)
		(call !ValidateParameters)
		(call !Return (assoc
			payload
				(map
					(lambda (let
						(assoc session (current_value 1))
						(append
							(assoc "id" session )

							(if (> (size attributes) 0)
								; create an assoc of attribute -> metadata attribute value
								(zip
									attributes
									(map
										(lambda
											(get (retrieve_from_entity session ".metadata") (current_value))
										)
										attributes
									)
								)

								(assoc)
							)
						)
					))
					(call !GetSessionIds)
				)
		))
	)


	;returns all the metadata for a specified session
	#get_session_metadata
	(declare
		(assoc
			;{type "string"}
			;name of session to return metadata for
			session (null)
		)
		(call !ValidateParameters)
		(if (not (contains_entity session))
			(call !Return (assoc errors (list (concat "Session " session " does not exist for this trainee."))))

			(call !Return (assoc payload (retrieve_from_entity session ".metadata") ))
		)
	)


	;set session metadata for a specified session.
	#set_session_metadata
	(declare
		(assoc
			;{type "string"}
			;id of session to modify.
			session "none"
			;{type "assoc"}
			;any arbitrary metadata.
			metadata (assoc)
		)
		(call !ValidateParameters)

		;update this session id's metadata
		(assign_to_entities session (assoc ".metadata" metadata ))

		(call !Return)
	)

	;return list of all session indices for a specified session.
	;session indices are 0-based index of number of the case for the session used for replays; may change if cases are removed
	#get_session_indices
	(declare
		(assoc
			;{type "string"}
			;id of session
			session ""
		)
		(call !ValidateParameters)
		(call !Return (assoc
			payload (indices (retrieve_from_entity session ".replay_steps"))
		))
	)


	;return list of all session training indices for a specified session.
	;session training indices are 0-based index of the case, ordered by training during the session; is not changed
	#get_session_training_indices
	(declare
		(assoc
			;{type "string"}
			;id of session
			session ""
		)
		(call !ValidateParameters)
		(call !Return (assoc
			payload
				(map
					(lambda (retrieve_from_entity (current_value) !internalLabelSessionTrainingIndex))
					;list of all cases trained for specified session
					(retrieve_from_entity session ".replay_steps")
				)
		))
	)

)
