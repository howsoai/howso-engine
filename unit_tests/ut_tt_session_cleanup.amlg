(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call init_unit_test (assoc name "ut_tt_session_cleanup.amlg"))

	;load a trainee, call the model "model1"
	(declare (assoc trainee "cleanup"))
	(print "loaded model: " (load_entity "../trainee_template.amlg" trainee (false) (false)) "\n")
	(call_entity trainee "Initialize")
	(assign_to_entities trainee (assoc
		closestN 2
		lpSpaceParameter 0.4
	))


	(declare (assoc
		training_data
			(list
				(list 3 -30000 1 3 3)
				(list 3.1 2000 1 3 3)
			)
		training_data2
			(list
				(list 8 15 8 4 4)
				(list 12 11 11 12 12)
				(list 12 11 11 12 12)
			)
		training_data3
			(list
				(list 1 5 1)
				(list 2 1 1)
			)

		context_features (list "A" "B" "C")
		action_features (list "D" "E")
	))

	;train
	(call_entity trainee "Train" (assoc
		input_cases training_data
		features (append context_features action_features)
		session "session_A"
	))
	(call_entity trainee "Train" (assoc
		input_cases training_data2
		features (append context_features action_features)
		session "session_B"
	))
	(call_entity trainee "Train" (assoc
		input_cases training_data3
		features context_features
		session "session_C"
	))

	(create_entities (list trainee "INVALID_ENTITY")
		(lambda (null ##Bad_label 86))
	)

	(declare (assoc result (contained_entities trainee)))

	(print "has invalid entity: ")
	(call assert_true (assoc obs (contains_value result "INVALID_ENTITY")))

	;removes entities that aren't cases or sessions
	(call_entity trainee "RemoveIrrelevantEntities")

	(assign (assoc result (contained_entities trainee)))

	(print "has no invalid entities: ")
	(call assert_false (assoc obs (contains_value result "INVALID_ENTITY")))


	;VERIFY TRAINING AND MODEL SIZE
	(print "model size: ")
	(declare (assoc model_size (call_entity trainee "!GetNumTrainingCases")))
	(call assert_same (assoc
		exp 7
		obs model_size
	))
	(call exit_if_failures (assoc msg "Removal of invalid entities"))

	;removes any cases that don't have both the specified context and action features
	(call_entity trainee "RemoveIncompleteCases" (assoc context_features context_features action_features  action_features ))

	;VERIFY TRAINING AND MODEL SIZE
	(print "new model size: ")
	(assign (assoc model_size (call_entity trainee "!GetNumTrainingCases")))
	(call assert_same (assoc
		exp 5
		obs model_size
	))

	(call exit_if_failures (assoc msg "Removal of incomplete cases"))

	;removes any replay steps that are non-existent cases
	(call_entity trainee "RemoveInvalidReplaySteps")

	(print "session_C replay steps have been cleared out: ")
	;need to have these two fields match, so take intersection
	(declare (assoc
		replay_steps_clear_needed_values
			(lambda (null
				##.replay_steps (list)
				##.trained_instance_count 2
			))
	))
	(call assert_same (assoc
		exp replay_steps_clear_needed_values
		obs (intersect replay_steps_clear_needed_values (retrieve_entity_root (list trainee "session_C")))
	))


	(call exit_if_failures (assoc msg "Removal of invalid replay steps"))

	;removes duplicate (identical) cases
	(call_entity trainee "MergeDuplicateCases" (assoc features (append context_features action_features)))

	;VERIFY TRAINING AND MODEL SIZE
	(print "new model size after dupe removal: ")
	(assign (assoc model_size (call_entity trainee "!GetNumTrainingCases")))
	(call assert_same (assoc
		exp 4
		obs model_size
	))

	(call exit_if_failures (assoc msg "Duplicate case removal"))


	(destroy_entities (list trainee "session_B"))

	;removes cases with null or invalid sessions
	(call_entity trainee "RemoveCasesWithoutSessionReferences")

	;VERIFY TRAINING AND MODEL SIZE
	(print "model size after session removal and case cleanup: ")
	(assign (assoc model_size (call_entity trainee "!GetNumTrainingCases")))
	(call assert_same (assoc
		exp 2
		obs model_size
	))

	(call exit_if_failures (assoc msg "Removal of session and unreferenced case cleanup"))


	;removes empty sessions
	(call_entity trainee "RemoveUnreferencedSessions")

	(print "empty session_C removed, only session_A remains: ")
	(assign (assoc result (call_entity trainee "GetSessionIds")))
	(call assert_same  (assoc
		exp (list "session_A")
		obs result
	))


	(call exit_if_failures (assoc msg "Removal of unreferenced sessions"))

	(call exit_if_failures (assoc msg unit_test_name))
)
