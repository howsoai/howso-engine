(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_id_features.amlg" retries 1))

	(call_entity "howso" "set_feature_attributes" (assoc
		feature_attributes
			(assoc
				;data_types should be ignored because they are not in the set of number or boolean
				"color" (assoc "type" "nominal" "id_feature" (true) "data_type" "string")
				"size" (assoc "type" "nominal" "id_feature" (true) "data_type" "string")
			)
	))

	(declare
		(assoc
			result (null)
			context_key (null)
			features (list "height" "width" "length" "size" "color")
			data
				(list
					;		"h" "w" "l" 	"size" 	"color"		"popularity"
					(list 	1 	.7 	1 		"small" 	"red"	1)
					(list 	1.1 .6 	.7		"small" 	"red" 	1)
					(list 	.6 	.4 	.5 		"small" 	"red"	1.1)
					(list 	.7 	.5 	.6 		"small" 	"green"	2)

					(list 	1.5	1.5	1.5		"small" 	"yellow" 1)
					(list 	2.1 1.6 2.7		"small" 	"blue"	 10)
					(list 	2.6	2.7	2.2		"medium" 	"blue"	7)
					(list 	3.6	3.1	3.5 	"medium" 	"red"	1.2)

					(list 	2 	1 	3 		"medium" 	"red"	1.3)
					(list 	2.1 1.1	4.7		"medium" 	"yellow" 1.3)

					(list 	7.5 4.8 5.0		"large" 	"red"	1.4)
					(list 	8.6	5.4	5.5		"large" 	"green" 2)
			)
	))


	(call_entity "howso" "train" (assoc
		features (append features "popularity")
		cases data
		session "unit_test"
	))

	(call_entity "howso" "analyze" (assoc
		context_features (trunc features)
		action_features (list (last features))
		targeted_model "single_targeted"
		k_values (list 3 5 8)
		p_values (list 0.1 0.5 1 2)
		use_case_weights (true)
		rebalance_features ["color" "size"]

	))
	(assign (assoc
		context_key (apply "concat" (weave (sort (trunc features)) "."))
		result (call_entity "howso" "debug_label" (assoc label "!hyperparameterMetadataMap"))
	))

	(print "single target has expected attributes: ")
	(call assert_true (assoc
		obs
			(and
				(!= (null) (get result ["targeted" "color" context_key ".case_weight" "k"]))
				(!= (null) (get result ["targeted" "color" context_key ".case_weight" "p"]))
				(!= (null) (get result ["targeted" "color" context_key ".case_weight" "dt"]))
			)
	))

	(assign (assoc
		result
			(call_entity "howso" "get_cases" (assoc
				features (list ".session_training_index" ".case_weight")
				session "unit_test"
			))

	))

	(print "calculated case and id feature weights: ")
	(call assert_approximate (assoc
		obs (get result (list 1 "payload" "cases"))
		exp
			(list
				(list 0 0.3333)
				(list 1 0.3333)
				(list 2 0.3333)
				(list 3 1)
				(list 4 1)
				(list 5 1)
				(list 6 1.5)
				(list 7 0.5)
				(list 8 0.5)
				(list 9 1.5)
				(list 10 1)
				(list 11 3)
			)
		percent .01
	))

	(call_entity "howso" "analyze" (assoc
		context_features (trunc features)
		action_features (list (last features))
		targeted_model "single_targeted"
		use_case_weights (true)
		weight_feature "width"
	))
	(assign (assoc
		result (call_entity "howso" "debug_label" (assoc label "!hyperparameterMetadataMap"))
	))

	(print "now has color weight attributes: ")
	(call assert_true (assoc
		obs
			(and
				(!= (null) (get result ["targeted" "color" context_key "width" "k"]))
				(!= (null) (get result ["targeted" "color" context_key "width" "p"]))
				(!= (null) (get result ["targeted" "color" context_key "width" "dt"]))
			)
	))

	(assign (assoc
		result
			(call_entity "howso" "single_react" (assoc
				context_features (trunc features)
				action_features (list (last features))
				context_values (list 5 5 5 "medium")
				use_case_weights (false)
			))
	))
	(print "should be 'red' without weights: ")
	(call assert_same (assoc
		exp "red"
		obs (get result (list 1 "payload" "action_values" 0))
	))


	(assign (assoc
		result
			(call_entity "howso" "single_react" (assoc
				context_features (trunc features)
				action_features (list (last features))
				context_values (list 5 5 5 "medium")
				use_case_weights (true)
			))
	))
	(print "should be 'green' with weights because it's rarer: ")
	(call assert_same (assoc
		exp "green"
		obs (get result (list 1 "payload" "action_values" 0))
	))


	(call exit_if_failures (assoc msg "ID weighted analyze and discriminative react"))


	(call_entity "howso" "analyze" (assoc
		context_features features
		targeted_model "targetless"
		use_case_weights (true)
	))
	(assign (assoc
		context_key (apply "concat" (weave (sort features) "."))
		result (call_entity "howso" "debug_label" (assoc label "!hyperparameterMetadataMap"))
	))

	(print "targetless has .case_weight attributes: ")
	(call assert_true (assoc
		obs
			(and
				(!= (null) (get result ["targetless" context_key ".case_weight" "k"]))
				(!= (null) (get result ["targetless" context_key ".case_weight" "p"]))
				(!= (null) (get result ["targetless" context_key ".case_weight" "dt"]))
			)
	))


	(declare (assoc
		small 0
		medium 0
		large 0
		red 0
		green 0
		blue 0
		yellow 0
		joint_map (assoc)
	))

	(range
		(lambda (let
			(assoc
				case
					(get
						(call_entity "howso" "single_react" (assoc
							action_features features
							desired_conviction 5
							use_regional_residuals (false)
							use_case_weights (false)
						))
						(list 1 "payload" "action_values")
					)
			)
			(if
				(= "small" (get case 3))
				(accum (assoc small 1))

				(= "medium" (get case 3))
				(accum (assoc medium 1))

				(= "large" (get case 3))
				(accum (assoc large 1))
			)

			(if
				(= "red" (get case 4))
				(accum (assoc red 1))

				(= "green" (get case 4))
				(accum (assoc green 1))

				(= "blue" (get case 4))
				(accum (assoc blue 1))

				(= "yellow" (get case 4))
				(accum (assoc yellow 1))
			)

			(accum (assoc
				joint_map
					(associate
						(concat (get case 3) (get case 4))
							(+ 1 (or (get joint_map (concat (get case 3) (get case 4)))))
					)
			))
		))
		1 1000 1
	)

	(print "expected approximately original distribution of all nominals: \n")
	(print "small " small " ")
	(call assert_approximate (assoc
		obs small
		exp 500
		percent .33
	))
	(print "medium " medium " ")
	(call assert_approximate (assoc
		obs medium
		exp 333
		percent .33
	))
	(print "large " large " ")
	(call assert_approximate (assoc
		obs large
		exp 167
		percent .33
	))

	(print "red " red " ")
	(call assert_approximate (assoc
		obs red
		exp 500
		percent .33
	))
	(print "green " green " ")
	(call assert_approximate (assoc
		obs green
		exp 167
		percent .33
	))

	(print "blue " blue " " )
	(call assert_approximate (assoc
		obs blue
		exp 167
		percent .33
	))
	(print "yellow " yellow " ")
	(call assert_approximate (assoc
		obs yellow
		exp 167
		percent .33
	))


	(print "small-red should be a quarter: ")
	(call assert_approximate (assoc
		obs (get joint_map "smallred")
		exp 250
		percent .33
	))

	(print "large-green should be LESS than a twelvth: ")
	(call assert_approximate (assoc
		obs (get joint_map "largegreen")
		exp 69
		thresh 30
	))


	(call exit_if_failures (assoc msg "non-weighted targetless analyze and generation"))

	(assign (assoc
		small 0
		medium 0
		large 0
		red 0
		green 0
		blue 0
		yellow 0
		joint_map (assoc)
	))

	(map
		(lambda (let
			(assoc
				case
					(get
						(call_entity "howso" "single_react" (assoc
							action_features features
							desired_conviction 1
							use_regional_residuals (false)
							use_case_weights (true)
						))
						(list 1 "payload" "action_values")
					)
			)
			(if
				(= "small" (get case 3))
				(accum (assoc small 1))

				(= "medium" (get case 3))
				(accum (assoc medium 1))

				(= "large" (get case 3))
				(accum (assoc large 1))
			)

			(if
				(= "red" (get case 4))
				(accum (assoc red 1))

				(= "green" (get case 4))
				(accum (assoc green 1))

				(= "blue" (get case 4))
				(accum (assoc blue 1))

				(= "yellow" (get case 4))
				(accum (assoc yellow 1))
			)

			(accum (assoc
				joint_map
					(associate
						(concat (get case 3) (get case 4))
						(+ 1 (or (get joint_map (concat (get case 3) (get case 4)))))
					)
			))
		))
		(range 1 2000)
	)

	(print "expected approximately equally weighted distribution of all nominals: \n")
	(print "small " small " ") ;expected: .33
	(call assert_approximate (assoc
		obs small
		exp 667
		percent .33
	))
	(print "medium " medium " ") ;expected: .33
	(call assert_approximate (assoc
		obs medium
		exp 667
		percent .33
	))
	(print "large " large " ") ;expected: .33
	(call assert_approximate (assoc
		obs large
		exp 667
		percent .33
	))

	(print "red " red " ")  ;expected: .25
	(call assert_approximate (assoc
		obs red
		exp 500
		percent .33
	))
	(print "green " green " ") ;expected: .25
	(call assert_approximate (assoc
		obs green
		exp 500
		percent .33
	))

	(print "blue " blue " ") ;expected: .25
	(call assert_approximate (assoc
		obs blue
		exp 500
		percent .33
	))
	(print "yellow " yellow " " ) ;expected: .25
	(call assert_approximate (assoc
		obs yellow
		exp 500
		percent .33
	))


	(print "small-red should be much LESS than a quarter: " (get joint_map "smallred") " ")
	(call assert_true (assoc
		obs (< (get joint_map "smallred") 250)
	))

	(print "large-green should be MORE than a twelvth: " (get joint_map "largegreen") " ")
	(call assert_true (assoc
		obs (> (get joint_map "largegreen") 110)
	))


	(call exit_if_failures (assoc msg "ID weighted targetless analyze and generation"))


	;analyze with a continuous rebalance feature
	(call_entity "howso" "analyze" (assoc
		context_features (append features "popularity")
		targeted_model "targetless"
		k_values [5]
		rebalance_features ["popularity"]
	))

	(assign (assoc
		result
			(call_entity "howso" "get_cases" (assoc features [".case_weight"] ))
	))
	(call keep_result_payload)

	(print "Continuous rebalance features compute and cache distance contribution as weights: ")
	(call assert_approximate (assoc
		obs (get result "cases")
		exp
			[
				[0.0171]
				[0.0171]
				[0.0179]
				[0.2214]
				[0.0171]
				[0.9999]
				[0.9998]
				[0.0244]
				[0.0206]
				[0.0206]
				[0.0659]
				[0.2214]
			]
		percent 0.1
	))

	(call_entity "howso" "train" (assoc
		features (append features "popularity")
		session "unit_test"
		cases
			[
				(list 	2.1 1.6 5		"large" 	"blue"	 12)
				(list 	2.6	2.9	4		"large" 	"blue"	 8)
			]
	))

	(assign (assoc
		result
			(call_entity "howso" "get_cases" (assoc features [".case_weight"] ))
	))
	(call keep_result_payload)

	(print "New cases automatically train with distance contribution as weights: ")
	(call assert_approximate (assoc
		obs (get result "cases")
		exp
			[
				[0.0171]
				[0.0171]
				[0.0179]
				[0.2214]
				[0.0171]
				[0.9999]
				[0.9998]
				[0.0244]
				[0.0206]
				[0.0206]
				[0.0659]
				[0.2214]
				;the two new, notably larger weighted cases:
				[0.9407]
				[0.6944]
			]
		percent 0.1
	))

	(call exit_if_failures (assoc msg "Continuous rebalance features."))


	;analyze with continuous and nominal rebalance features
	(call_entity "howso" "analyze" (assoc
		context_features (append features "popularity")
		targeted_model "targetless"
		k_values [5]
		rebalance_features ["popularity" "size"]
	))

	(assign (assoc
		result
			(call_entity "howso" "get_cases" (assoc features [".case_weight"] ))
	))
	(call keep_result_payload)

	(print "Mixed rebalance features are a product of their weights: ")
	(call assert_approximate (assoc
		obs (get result "cases")
		exp
			[
				[0.0013]
				[0.0013]
				[0.0013]
				[0.0204]
				[0.0013]
				[0.3432]
				[0.6053]
				[0.0028]
				[0.0024]
				[0.0024]
				[0.0078]
				[0.0305]
				[0.6822]
				[0.5003]
			]
		percent 0.1
	))

	(call_entity "howso" "train" (assoc
		features (append features "popularity")
		session "unit_test"
		cases
			[
				(list 	0.2 1.6 3		"small" 	"blue"	 1)
				(list 	3.7	1.9	3		"large" 	"blue"	 11)
			]
	))

	(assign (assoc
		result
			(call_entity "howso" "get_cases" (assoc features [".case_weight"] ))
	))
	(call keep_result_payload)

	(print "New cases automatically train with distance contribution as weights: ")
	(call assert_approximate (assoc
		obs (get result "cases")
		exp
			[
				[0.0013]
				[0.0013]
				[0.0013]
				[0.0204]
				[0.0013]
				[0.3432]
				[0.6053]
				[0.0028]
				[0.0024]
				[0.0024]
				[0.0078]
				[0.0305]
				[0.6822]
				[0.5003]
				;the two new cases, one similar to the smaller weighted, one similar to the larger weighted
				[0.0008]
				[0.3690]
			]
		percent 0.1
	))

	(call exit_if_failures (assoc msg "Mixed rebalance features."))

	(call exit_if_failures (assoc msg unit_test_name ))
)

