(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_hierarchy_by_name.amlg"))

	(declare (assoc result (null)))

	(call_entity "howso" "create_subtrainee" (assoc path ["A"] child_id "a"))

	(call_entity "howso" "create_subtrainee" (assoc path ["A" "b"] child_id "b" ))

	(call_entity "howso" "create_subtrainee" (assoc path ["A" "c"] child_id "c" ))

	(call_entity "howso" "create_subtrainee" (assoc path ["A" "c" "e"] child_id "e" ))

	;creating under non-existent child
	(assign (assoc
		result (call_entity "howso" "create_subtrainee" (assoc path ["nonexistent" "e"] child_id "n" ))
	))
	(print "Creating under invalid child: ")
	(call assert_same (assoc
		obs (get result (list 1 "detail"))
		exp "Invalid path specified."
	))

	;creating a dupe
	(assign (assoc
		result (call_entity "howso" "create_subtrainee" (assoc path ["A" "c" "e"] child_id "e" ))
	))
	(print "Creating an existing trainee: ")
	(call assert_same (assoc
		obs (get result (list 1 "detail"))
		exp "A trainee already exists in the hierarchy at path: A.c.e"
	))

	(call exit_if_failures (assoc msg "Creating subtrainees."))


	(assign (assoc
		result
			(call_entity "howso" "copy_subtrainee" (assoc
				target_path ["c_copy"]
				target_id "uniqueid"
				source_path ["invalid"]
			))
	))
	(print "Can't copy - bad child: ")
	(call assert_same (assoc
		obs (get result (list 1 "detail"))
		exp "source: Invalid path specified."
	))

	(assign (assoc
		result
			(call_entity "howso" "copy_subtrainee" (assoc
				target_path ["invalid" "c_copy"]
				target_id "uniqueid"
				source_path ["A" "c"]
			))
	))
	(print "Can't copy - bad parent: ")
	(call assert_same (assoc
		obs (get result (list 1 "detail"))
		exp "Invalid path specified."
	))

	(assign (assoc
		result
			(call_entity "howso" "copy_subtrainee" (assoc
				target_path ["A"]
				target_id "uniqueid"
				source_path ["A" "c"]
			))
	))
	(print "Can't copy - duplicate child: ")
	(call assert_same (assoc
		obs (get result (list 1 "detail"))
		exp "A trainee already exists in the hierarchy at path: A"
	))


	(call_entity "howso" "copy_subtrainee" (assoc
		target_path ["c_copy"]
		target_id "deleteme"
		source_path ["A" "c"]
	))

	(call exit_if_failures (assoc msg "Creating subtrainees."))

	(assign (assoc
		result
			(call_entity "howso" "rename_subtrainee" (assoc
				path ["invalid"]
				label "AA"
			))
	))
	(print "Can't rename - invalid child: ")
	(call assert_same (assoc
		obs (get result (list 1 "detail"))
		exp "Invalid path specified."
	))

	;alphanumeric validation checks
	(assign (assoc
		result
			(call_entity "howso" "rename_subtrainee" (assoc
				path ["invalid"]
				label "AA--.34"
			))
	))
	(print "Can't rename - non-alphanumeric name: ")
	(call assert_same (assoc
		obs (get result (list 1 "code"))
		exp "invalid"
	))

	(assign (assoc
		result
			(call_entity "howso" "copy_subtrainee" (assoc
				target_path ["A"]
				target_id "uniqueid"
				source_path ["A" "c.-32"]
			))
	))
	(print "Can't copy - non-alphanumeric label in path: ")
	(call assert_same (assoc
		obs (get result (list 1 "code"))
		exp "invalid"
	))

	(assign (assoc
		result
			(call_entity "howso" "copy_subtrainee" (assoc
				target_path ["A"]
				target_id "uniqueid"
				source_path
					[
						"A"
						(apply "concat" (range (lambda "a") 1 259 1))
					]
			))
	))
	(print "Can't copy - one too-large label in path: ")
	(call assert_same (assoc
		obs (get result (list 1 "code"))
		exp "invalid"
	))

	(assign (assoc
		result
			(call_entity "howso" "set_hierarchy_relationships" (assoc
				is_contained_map
					{
						".a3*71" (true)
					}
			))
	))
	(print "Can't set hierarchy relationships - non-alphanumeric index in is_contained_map: ")
	(call assert_same (assoc
		obs (get result 0)
		exp 0
	))
	;end of alphanumeric checks

	(assign (assoc
		result
			(call_entity "howso" "rename_subtrainee" (assoc
				path (list "A" "c")
				label "b"
			))
	))
	(print "Can't rename - duplicate name: ")
	(call assert_same (assoc
		obs (get result (list 1 "detail"))
		exp "Trainee with specified label already exists."
	))

	(call_entity "howso" "rename_subtrainee" (assoc
		path (list "A")
		label "AA"
	))

	(call_entity "howso" "rename_subtrainee" (assoc
		path (list "AA" "c")
		label "big_C"
	))

	(assign (assoc
		result (call_entity "howso" "get_hierarchy")
	))
	(call keep_result_payload)

	(print "Renames and copies: ")
	(call assert_same (assoc
		unordered (true)
		obs result
		exp
			{
				contained (true)
				id "model"
				path []
				children [
						{
							children [
									{
										children [
												{
													children []
													contained (true)
													id "e3"
													path ["AA" "big_C" "e"]
												}
											]
										contained (true)
										id "c2"
										path ["AA" "big_C"]
									}
									{
										children []
										contained (true)
										id "b"
										path ["AA" "b"]
									}
								]
							contained (true)
							id "A1"
							path ["AA"]
						}
						{
							children [
									{
										children []
										contained (true)
										id "e3"
										path ["c_copy" "e"]
									}
								]
							contained (true)
							id "deleteme"
							path ["c_copy"]
						}
					]

			}
	))

	(call exit_if_failures (assoc msg "Renaming."))


	(call_entity "howso" "train" (assoc
		features (list "a" "b" "c")
		cases
			(list
				(list 10 20 30)
				(list 11 22 33)
				(list 12 24 36)
				(list 13 26 39)
			)
	))

	(call_entity "howso" "execute_on_subtrainee" (assoc
		path (list "AA")
		method "train"
		payload
			(assoc
				features (list "x" "y")
				cases
					(list
						(list 1 1)
						(list 2 1)
						(list 3 3)
						(list 4 4)
						(list 5 5)
						(list 6 6)
					)
			)
	))


	(call_entity "howso" "move_cases" (assoc
		source_path (list "AA")
		target_path (list "AA" "b")
		num_cases 2
	))

	(assign (assoc
		result
			(get
				(call_entity "howso" "get_num_training_cases")
				(list 1 "payload" "count")
			)
	))

	(print "Cases in model: ")
	(call assert_same (assoc
		obs result
		exp 4
	))

	(assign (assoc
		result
			(get
				(call_entity "howso" "execute_on_subtrainee" (assoc
					path (list "AA")
					method "get_num_training_cases"
				))
				(list 1 "payload" "count")
			)
	))

	(print "Cases in child A1: ")
	(call assert_same (assoc
		obs result
		exp 4
	))


	(assign (assoc
		result
			(get
				(call_entity "howso" "execute_on_subtrainee" (assoc
					path (list "AA" "b")
					method "get_num_training_cases"
				))
				(list 1 "payload" "count")
			)
	))

	(print "Cases in grandchild b: ")
	(call assert_same (assoc
		obs result
		exp 2
	))

	(call exit_if_failures (assoc msg "Excute method and move cases."))


	(assign (assoc
		result
			(call_entity "howso" "save_subtrainee" (assoc
				filename "deleteme"
				filepath "./"
				path ["nonexistent"]
			))
	))
	(print "Save fails - invalid path: ")
	(call assert_same  (assoc
		obs (get result (list 1 "detail"))
		exp "Invalid path specified."
	))

	(call_entity "howso" "execute_on_subtrainee" (assoc
		path (list "c_copy")
		method "train"
		payload
			(assoc
				features (list "x" "y")
				cases
					(list
						(list 1 1)
						(list 2 1)
						(list 3 3)
						(list 4 4)
					)
			)
	))

	(call_entity "howso" "save_subtrainee" (assoc
		filename "deleteme"
		filepath "./"
		as_external (true)
		path ["c_copy"]
		;child_id "deleteme"
	))

	(assign (assoc
		result
			(call_entity "howso" "save_subtrainee" (assoc
				filename "deleteme"
				filepath "./"
				as_external (true)
				path ["c_copy"]
				;child_id "deleteme"
			))
	))

	(print "Save fails since it is now outside trainee: ")
	(call assert_same (assoc
		obs (get result (list 1 "detail"))
		exp "Specified an independently stored trainee."
	))

	(assign (assoc
		result (call_entity "howso" "get_hierarchy")
	))
	(call keep_result_payload)
	(print "Updated is_contained_map has all the c_copy trainees as not contained: ")
	(call assert_same (assoc
		unordered (true)
		obs result
		exp
			 {
				children [
						{
							children [
									{
										children [
												{
													children []
													contained (true)
													id "e3"
													path ["AA" "big_C" "e"]
												}
											]
										contained (true)
										id "c2"
										path ["AA" "big_C"]
									}
									{
										children []
										contained (true)
										id "b"
										path ["AA" "b"]
									}
								]
							contained (true)
							id "A1"
							path ["AA"]
						}
						{
							children []
							contained (false)
							id "deleteme"
							path ["c_copy"]
						}
					]
				contained (true)
				id "model"
				path []
			}
	))

	;load into trainee AA
	(call_entity "howso" "load_subtrainee" (assoc
		path ["AA" "loaded"]
		child_id "loaded"
		filename "deleteme"
		filepath "./"
	))

	(assign (assoc
		result
			(call_entity "howso" "execute_on_subtrainee" (assoc
				path (list "AA" "loaded")
				method "get_num_training_cases"
			))
	))
	(print "Loaded cases of saved trainee correctly: ")
	(call assert_same (assoc
		obs (get result (list 1 "payload" "count"))
		exp 4
	))

	;Modify relationships to "load" a trainee as an external relationship
	(assign (assoc
		result
			(call_entity "howso" "execute_on_subtrainee" (assoc
				method "add_hierarchy_relationship"
				path (list "AA" "b")
				payload
					(assoc
						id "deleteme2"
						entity_path_id "child_of_b"
					)
			))
	))

	(assign (assoc
		result (call_entity "howso" "get_hierarchy")
	))
	(call keep_result_payload)

	(print "Hierarchy for model after save and load is correct: ")
	(call assert_same (assoc
		unordered (true)
		obs result
		exp
			{
				children [
						{
							children [
									{
										children [
												{
													children []
													contained (true)
													id "e3"
													path ["AA" "big_C" "e"]
												}
											]
										contained (true)
										id "c2"
										path ["AA" "big_C"]
									}
									{
										children [
												{
													children []
													contained (true)
													id "e3"
													path ["AA" "loaded" "e"]
												}
											]
										contained (true)
										id "deleteme"
										path ["AA" "loaded"]
									}
									{
										children [
												{
													children []
													contained (false)
													id "deleteme2"
													path ["AA" "b" "child_of_b"]
												}
											]
										contained (true)
										id "b"
										path ["AA" "b"]
									}
								]
							contained (true)
							id "A1"
							path ["AA"]
						}
						{
							children []
							contained (false)
							id "deleteme"
							path ["c_copy"]
						}
					]
				contained (true)
				id "model"
				path
					;{type "string"}
					;path to this trainee as a list of path labels
					[]
			}
	))

	;cleanup saved test dataset
	(if (= (system "os") "Windows")
		(system "system" "del deleteme.*")

		;else posix
		(system "system" "rm -rf deleteme*")
	)

	(call exit_if_failures (assoc msg "save and load" ))

	(assign (assoc
		result
			(call_entity "howso" "execute_on_subtrainee" (assoc
				path (list "AA" "b" "child_of_b")
				method "get_num_training_cases"
			))
	))

	(print "execute fails on an independent trainee: ")
	(call assert_same (assoc
		obs (get result (list 1 "detail"))
		exp "Specified an independently stored trainee."
	))

	(assign (assoc
		result
			(call_entity (list "howso" ".trainee_container" "AA" ".trainee_container" "loaded") "debug_label" (assoc
				label "!parentId"
			))
	))
	(print "sanity check !parentId of 'loaded' trainee: ")
	(call assert_same (assoc
		exp "a"
		obs result
	))

	(assign (assoc
		result
			(call_entity (list "howso" ".trainee_container" "AA") "debug_label" (assoc
				label "!parentId"
			))
	))
	(print "sanity check !parentId of 'AA' trainee: ")
	(call assert_same (assoc
		obs result
		exp "model"
	))

	(assign (assoc
		result
			(get
				(call_entity "howso" "delete_subtrainee" (assoc
					path ["AA" "b" "child_of_b"]
				))
				(list 1 "warnings" 0)
			)
	))

	(print "delete only removes references since grandchild is outside trainee: ")
	(call assert_same (assoc
		obs result
		exp "Hierarchy has been updated but child trainee was not removed because it is stored independently of this trainee."
	))

	(call_entity "howso" "delete_subtrainee" (assoc
		path ["AA" "loaded" "e"]
	))

	(call_entity "howso" "delete_subtrainee" (assoc
		path ["c_copy"]
	))

	(assign (assoc
		result (call_entity "howso" "get_hierarchy")
	))
	(call keep_result_payload)

	(print "Hierarchy for model after deletion: ")
	(call assert_same (assoc
		unordered (true)
		obs result
		exp
			{
				children [
						{
							children [
									{
										children [
												{
													children []
													contained (true)
													id "e3"
													path ["AA" "big_C" "e"]
												}
											]
										contained (true)
										id "c2"
										path ["AA" "big_C"]
									}
									{
										children []
										contained (true)
										id "deleteme"
										path ["AA" "loaded"]
									}
									{
										children []
										contained (true)
										id "b"
										path ["AA" "b"]
									}
								]
							contained (true)
							id "A1"
							path ["AA"]
						}
						{
							children []
							contained (false)
							id "deleteme"
							path ["c_copy"]
						}
					]
				contained (true)
				id "model"
				path []
			}
	))

	(call exit_if_failures (assoc msg "delete trainees." ))


	(call exit_if_failures (assoc msg unit_test_name ))
)
