(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_null_react.amlg"))


	(call_entity "howso" "set_feature_attributes" (assoc
		features (assoc "C" (assoc "type" "nominal" "data_type" "number") )
	))

	(declare (assoc
		data
			(list
				(list 1 1 1)  ;0
				(list 2 1 1)
				(list 1 2 1)
				(list 2 2 1)
				(list 2 3 1)
				(list 3 2 1)  ;5
				(list 3 4 2)
				(list 3 5 2)
				(list 4 4 2)
				(list 4 5 2)
				(list 5 5 (null))  ;10
				(list 6 6 3)	   ;11
				(list 6 4 (null))  ;12
				(list 6 7 3)
				(list 7 7 3)
				(list 8 8 3)  ;15
				(list 9 5 2)
				(list 9 7 3)
				(list 9 8 3)
				(list 9 9 4)
				(list 10 9 4) ;20
				(list 10 10 4)
			)
	))

	(declare (assoc
		features (list "X" "Y" "C")
		ctx_features (list "X" "Y")
		act_features (list "C")
		result (null)
	))



	(call_entity "howso" "train" (assoc
		features features
		input_cases data
	))

	(call_entity "howso" "set_internal_parameters" (assoc
		default_hyperparameter_map (assoc "k" 3 "p" 2 "dt" -1)
	))


	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features act_features
				context_features ctx_features
				context_values (list 6 5)
			))
	))
	(print "no nulls, nominal, where nulls are majority: ")
	(call assert_same (assoc
		exp (list 3)
		obs (get result (list 1 "payload" "action_values"))
	))


	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features act_features
				context_features ctx_features
				context_values (list 6 5)
				allow_nulls (true)
			))
	))

	(print "nulls allowed, nominal, where nulls are majority: ")
	(call assert_same (assoc
		exp (list (null))
		obs (get result (list 1 "payload" "action_values"))
	))




	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features act_features
				context_features ctx_features
				context_values (list 6 5.8)
			))
	))
	(print "no nulls, where nulls are minority, nominal: ")
	(call assert_same (assoc
		exp (list 3)
		obs (get result (list 1 "payload" "action_values"))
	))



	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features act_features
				context_features ctx_features
				context_values (list 6 5.8)
				allow_nulls (true)
			))
	))
	(print "nulls allowed, nominals, where nulls are minority ")
	(call assert_same (assoc
		exp (list 3)
		obs (get result (list 1 "payload" "action_values"))
	))

	(call exit_if_failures (assoc msg "Nominal Predictions with nulls." ))


	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features act_features
				context_features ctx_features
				context_values (list 6 4.99999999999)
				allow_nulls (false)
				details (assoc "case_contributions_full" (true))
			))
	))

	(print "Case contribution with their own nominal action values: ")
	(call assert_approximate (assoc
		obs ;sort list by training index
			(sort
				(lambda (> (get (current_value) ".session_training_index") (get (current_value 1) ".session_training_index")))
				(get result (list 1 "payload" "case_contributions_full"))
			)
		exp
			(list
				(assoc ".session_training_index" 9 ".session" "none" "case_contributions_full" 0.02)
				(assoc ".session_training_index" 11 ".session" "none" "case_contributions_full" -0.4)
				(assoc ".session_training_index" 13 ".session" "none" "case_contributions_full" -0.23)
			)
	))

	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features act_features
				context_features ctx_features
				context_values (list 6 5)
				allow_nulls (false)
				details (assoc "case_contributions_full" (true))
			))
	))

	(print "Robust Case contribution with their own nominal action values: ")
	(call assert_approximate (assoc
		obs ;sort list by training index
			(sort
				(lambda (> (get (current_value) ".session_training_index") (get (current_value 1) ".session_training_index")))
				(get result (list 1 "payload" "case_contributions_full"))
			)
		exp
			(list
				(assoc "case_contributions_full" -0.11 ".session_training_index" 9 ".session" "none")
				(assoc "case_contributions_full" -0.33 ".session_training_index" 11 ".session" "none")
				(assoc "case_contributions_full" -0.26 ".session_training_index" 13 ".session" "none")
			)
	))

	(call exit_if_failures (assoc msg "Nominal Case Contributions."))


	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features act_features
				context_features ctx_features
				context_values (list 6 5)
				allow_nulls (false)
				details (assoc "feature_contributions_robust" (true))
			))
	))
	(print "Robust Feature contributions for nominal: ")
	(call assert_approximate (assoc
		exp (assoc "X" 0.64 "Y" .48)
		obs (get result (list 1 "payload" "feature_contributions_robust"))
		thresh 0.1
	))

	(call exit_if_failures (assoc msg "Nominal Feature contributions." ))


	;create a new trainee from scratch
	(destroy_entities "howso")
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_pairwise_distances.amlg" skip_init (true)) )


	(call_entity "howso" "train" (assoc
		features features
		input_cases data
	))


	(call_entity "howso" "set_internal_parameters" (assoc
		default_hyperparameter_map (assoc "k" 3 "p" 2 "dt" -1)
	))


	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features act_features
				context_features ctx_features
				context_values (list 6 5)
				details (assoc "influential_cases" (true))
			))
	))
	(print "no nulls, where nulls are majority: ")
	(call assert_same (assoc
		exp (list 2.75)
		obs (get result (list 1 "payload" "action_values"))
	))


	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features act_features
				context_features ctx_features
				context_values (list 6 5)
				allow_nulls (true)
			))
	))

	(print "nulls allowed where nulls are majority: ")
	(call assert_same (assoc
		exp (list (null))
		obs (get result (list 1 "payload" "action_values"))
	))


	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features act_features
				context_features ctx_features
				context_values (list 6 5.8)
				details (assoc "influential_cases" (true))
			))
	))

	(print "no nulls, where nulls are minority: ")
	(call assert_same (assoc
		exp (list 3)
		obs (get result (list 1 "payload" "action_values"))
	))


	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features act_features
				context_features ctx_features
				context_values (list 6 5.8)
				allow_nulls (true)
			))
	))
	(print "nulls allowed, where nulls are minority: ")
	(call assert_same (assoc
		exp (list 3)
		obs (get result (list 1 "payload" "action_values"))
	))

	(call exit_if_failures (assoc msg "Predictions with nulls." ))


	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features act_features
				context_features ctx_features
				context_values (list 6 4.999999999999)
				allow_nulls (false)
				details (assoc "case_contributions_full" (true))
			))
	))

	(print "Case contribution with their own action values: ")
	(call assert_approximate (assoc
		obs ;sort list by training index
			(sort
				(lambda (> (get (current_value) ".session_training_index") (get (current_value 1) ".session_training_index")))
				(get result (list 1 "payload" "case_contributions_full"))
			)
		exp
			(list
				(assoc ".session_training_index" 9 ".session" "none" "case_contributions_full" 0.02)
				(assoc ".session_training_index" 11 ".session" "none" "case_contributions_full" -0.4)
				(assoc ".session_training_index" 13 ".session" "none" "case_contributions_full" -0.23)
			)
	))


	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features act_features
				context_features ctx_features
				context_values (list 6 5)
				allow_nulls (false)
				details (assoc "feature_contributions_full" (true))
			))
	))
	(print "Feature contributions: ")
	(call assert_approximate (assoc
		exp (assoc "X" .26 "Y" .07)
		obs (get result (list 1 "payload" "feature_contributions_full"))
		thresh 0.1
	))

	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features act_features
				context_features ctx_features
				context_values (list 6 5)
				allow_nulls (false)
				details (assoc "feature_contributions_robust" (true))
			))
	))
	(print "Robust Feature contributions: ")
	(call assert_approximate (assoc
		exp (assoc "X" 0.46 "Y" .4)
		obs (get result (list 1 "payload" "feature_contributions_robust"))
		thresh 0.1
	))

	(call exit_if_failures (assoc msg "Feature contributions." ))


	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features (list "Y")
				context_features (list "X" "C")
				context_values (list 4.1 (null))
				details (assoc "influential_cases" (true))
			))
	))

	(print "Nearest cases using null as a context value: ")
	(call assert_same (assoc
		;expected session indices of cases are those with X values of 4, 4 and 5 since they are closest to 4.1
		;and the null feature C is ignored
		exp (list 8 9 10)
		obs (list
			(get result (list 1 "payload" "influential_cases" 0 ".session_training_index"))
			(get result (list 1 "payload" "influential_cases" 1 ".session_training_index"))
			(get result (list 1 "payload" "influential_cases" 2 ".session_training_index"))
		)
		unordered (true)
	))

	(print "Influences of nearest cases using null as a context value: ")
	(call assert_approximate (assoc
		;expected weights of these nearest cases are all about 1/3rd because the distance to feature C is
		;taken into account as max distance due to the null, making the cases similar in distance/influence
		exp (list .33 .33 .33)
		obs (list
			(get result (list 1 "payload" "influential_cases" 0 ".influence_weight"))
			(get result (list 1 "payload" "influential_cases" 1 ".influence_weight"))
			(get result (list 1 "payload" "influential_cases" 2 ".influence_weight"))
		)
		unordered (true)
	))

	(call exit_if_failures (assoc msg "Influences of neigbors with null values." ))

	(call_entity "howso" "add_feature" (assoc
		feature "null_test"
	))


	(print "Null feature added as inactive: ")
	(call assert_same (assoc
		exp (assoc "null_test" 0)
		obs (call_entity "howso" "debug_label" (assoc label "!inactiveFeaturesMap"))
	))


	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features (list "Y")
				context_features (list "X" "C" "null_test")
				context_values (list 4.1 (null) (null))
			))
	))
	(print "React works with null feature and null context feature: ")
	(call assert_approximate (assoc
		exp 4.66
		obs (get result (list 1 "payload" "action_values" 0))
	))

	(assign (assoc
		result (call_entity "howso" "get_internal_parameters")
	))

	(print "Feature weights set to 1s for active features and 0 for null_test: ")
	(call assert_same (assoc
		exp (assoc "C" 1 "X" 1 "Y" 1 "null_test" 0)
		obs (get result (list 1 "payload" "default_hyperparameter_map" "featureWeights"))
	))

	(assign (assoc
		result
			(call_entity "howso" "analyze" (assoc
				action_features ["null_test"]
				context_features ["X" "Y" "C"]
			))
	))
	(call keep_result_warnings)

	(print "Analyzing a targeted inactive feature results in a targetless analyze: ")
	(call assert_true (assoc
		obs (contains_index result "There are no values to analyze for 'null_test', defaulting to 'targetless'.")
	))

	;train a value into the null features and verify that it's removed from inactives and feature weights are updated
	(call_entity "howso" "train" (assoc
		features (append features "null_test")
		input_cases
			(list
				(list 10 11 3 7)
			)
	))

	(print "training null_test feature has reset the !inactiveFeaturesNeedCaching flag: ")
	(call assert_same (assoc
		exp (true)
		obs (call_entity "howso" "debug_label" (assoc label "!inactiveFeaturesNeedCaching"))
	))

	;calling react updates inactive features
	(call_entity "howso" "react" (assoc
		action_features (list "Y")
		context_features (list "X" "C" "null_test")
		context_values (list 4.1 (null) (null))
	))

	(assign (assoc
		result (call_entity "howso" "get_internal_parameters")
	))

	(print "Feature weights set to 1s for ALL features:  ")
	(call assert_same (assoc
		exp (assoc "C" 1 "X" 1 "Y" 1 "null_test" 1)
		obs (get result (list 1 "payload" "default_hyperparameter_map" "featureWeights"))
	))
	(call exit_if_failures (assoc msg "Null feature testing." ))

	(call exit_if_failures (assoc msg unit_test_name ))
)
