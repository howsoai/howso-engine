(seq
	;import the unit_test 'library'
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_evaluate.amlg"))


	(declare (assoc
		;set to true to see full output
		verbose (false)
		;set to null to do full model
		submodel_size 5000
	    data (load "../performance_tests/performance_data/bank-full.csv")
	))

	(declare (assoc
		features (first data)
		training_data (tail data)
		start (system_time)
	))

	(call_entity "howso" "set_random_seed" (assoc seed "abcdef"))

	(call_entity "howso" "set_feature_attributes" (assoc
		feature_attributes
			(assoc
				"job" (assoc "type" "nominal"  "id_feature" (true))
				"marital" (assoc "type" "nominal")
				"education"  (assoc "type" "nominal")
				"default" (assoc "type" "nominal")
				"housing"  (assoc "type" "nominal")
				"loan" (assoc "type" "nominal")
				"contact" (assoc "type" "nominal")
				"month" (assoc "type" "nominal")
				"poutcome"  (assoc "type" "nominal")
				"y" (assoc "type" "nominal")
				"pdays" (assoc "type" "ordinal" "data_type" "number")
				"age" (assoc "type" "ordinal" "data_type" "number")
				"day" (assoc "type" "ordinal" "data_type" "number")
				"campaign" (assoc "type" "ordinal" "data_type" "number")
			)
	))

	(call_entity "howso" "train" (assoc
		features features
		cases (trunc training_data submodel_size)
	))

	(declare (assoc
		load_time (- (system_time) start)
		num_cases (get (call_entity "howso" "get_num_training_cases" ) (list 1 "payload" "count"))
	))
	(print "Loaded Bank: " num_cases "\n")
	(print "Load time: " load_time "\n")

    (declare (assoc
        features_to_code_map
            (assoc
                "age" "(+ 2 #age 0)"
                "y" "(concat \"TEST:\" #y 0)"
            )

        aggregation_code "(apply \"+\" #age 0)"
        start_time (system_time)
    ))

    (declare (assoc
        result
            (call_entity "howso" "evaluate" (assoc
                features_to_code_map features_to_code_map
                aggregation_code aggregation_code
            ))
    ))

    (declare (assoc
        eval_time (- (system_time) start_time)
        feat_to_idx (zip features (indices features))
        results (get result (list 1 "payload" "evaluated"))
		aggregate_result (get result (list 1 "payload" "aggregated"))
    ))

	(print "Evaluation time: " eval_time "\n")

	; Checking returned values for correctness
    (map
		(lambda (let
			(assoc
				feature (current_index 1)
				value_list (current_value 1)
			)

			(declare (assoc
				feat_idx (get feat_to_idx feature)
				custom_code (get features_to_code_map feature)
			))

			; Retrieving case values for given feature
			(declare (assoc
				case_values
					(map
						(lambda (get (current_value) feat_idx) )
						training_data
					)
			))

			; Iterating over each evaluated value and original case value
			(assign (assoc
				result
					(range
						(lambda (let
							(assoc
								eval_value (get value_list (current_index 1))
								original_value (get case_values (current_index 1))
							)

							(if (= feature "age")
								(= eval_value (+ 2 original_value))

								(= feature "y")
								(= eval_value (concat "TEST:" original_value))

								; else
								(true)
							)

						))
						0 (- (size value_list) 1) 1
					)
			))

			(print "Checking evaluate results on feature: " feature "\n")
			(call assert_true (assoc
				obs (apply "=" (append result (true)))
			))

			(if verbose
				(seq
					(print "Feature:			" feature "\n")
					(print "Custom Code:		" custom_code "\n")
					(print "Feature Index:		" feat_idx "\n")
					(print "Case Values:		" case_values "\n")
					(print "Evaluated Values:	" value_list "\n")
				)
			)
		))
		results
	)

	(print "Checking aggregate result: \n")
	(call assert_approximate (assoc
		obs aggregate_result
		exp (apply "+" (get results "age"))
		percent 0.00001
	))

	(call exit_if_failures (assoc msg unit_test_name))
)
