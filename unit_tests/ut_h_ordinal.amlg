(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_ordinal.amlg"))

	(call_entity "howso" "set_params" (assoc
		default_hyperparameter_map (assoc "k" 4 "p" 2 "dt" -1 "paramPath" (list ".default"))
	))

	(assign (assoc
		result
			(call_entity "howso" "set_feature_attributes" (assoc
				feature_attributes (assoc "y" (assoc "type" "ordinal"))
			))
	))
	(call keep_result_errors)
	(print "Ordinal fails with bad attributes: ")
	(call assert_same (assoc
		exp (concat
			"The ordinal feature(s): y must either have a list of ordered allowed values set if they "
			"are non numeric values or have the `data_type` attribute set to 'number' if it is a numeric value. To set the list of "
			"ordered allowed values, you can either use the `ordinal_feature_values` parameter when calling `infer_feature_attributes` in the python client, "
			"or manually set the feature attributes mapping by modifying the 'allowed' key in the 'bounds' attribute."
			)
		obs result
	))

	(call_entity "howso" "set_feature_attributes" (assoc
		feature_attributes
			(assoc
				"k" (assoc "type" "continuous")
				"y" (assoc "type" "ordinal" "data_type" "number")
			)
	))

	(assign (assoc
		result (call_entity "howso" "get_feature_attributes")
	))
	(call keep_result_payload)

	(print "Feature attributes after calling set_feature_attributes: " )
	(call assert_same (assoc
		obs result
		exp
			(assoc
				"k" (assoc "type" "continuous")
				"y" (assoc "type" "ordinal" "data_type" "number")
			)
	))

	(map
		(lambda
			(call_entity "howso" "train" (assoc
				features (list "x" "y")
				cases (list (current_value 2))
				session "my_session"
			))
		)
		(list
			(list 1 20)
			(list 2 30)
			(list 6 40)
			(list 7 20)
		)
	)

	;X: 1 	2 	3 	4 	5 	6 	7
	;Y: 20  30				40	20
	; if we want to react to X=4, the closest ordinal values are either 30 or 40, but the sum of the weights of the two 20s should outweigh the
	; closer values and thus the result here should be 20
	(assign (assoc
		result
			(call_entity "howso" "single_react" (assoc
				action_features (list "y")
				context_features (list "x")
				context_values (list 4)
			))
	))

	(print "Ordinal results matching greater number of farther neighbors: ")
	(call assert_same (assoc obs (get result (list 1 "payload" "action_values")) exp (list 20) ))


	;if reacting to X=3, that's close enough to the Y value of 30 that it'll outweigh all others and the result will still be 30
	(assign (assoc
		result
			(call_entity "howso" "single_react" (assoc
				action_features (list "y")
				context_features (list "x")
				context_values (list 3)
			))
	))

	(print "Ordinal result matching closest neighbor: ")
	(call assert_same (assoc obs (get result (list 1 "payload" "action_values")) exp (list 30) ))

	(call exit_if_failures (assoc msg unit_test_name))
)
