(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_null_null_react.amlg" retries 1))

	(call_entity "howso" "set_feature_attributes" (assoc
		feature_attributes (assoc "C" (assoc "type" "nominal") )
	))

	(declare (assoc
		data
			(list
				(list 10	10		"A"		10  ) ;0
				(list 10	(null)	(null)	20  ) ;1
				(list 10	(null)	"B"		20  ) ;2
				(list 20	(null)	"C"		30  ) ;3
				(list 20	(null)	"A"		20  ) ;4
				(list 20	20		"B"		20  ) ;5
				(list 30	30 		"C"		30  ) ;6
				(list 30	(null)	(null)	50  ) ;7
				(list 30	(null)	"A"		50  ) ;8
				(list 40 	40		"D"		40  ) ;9
				(list 50	50		"D"		40  ) ;10
				(list 50	50		"E"		50  ) ;11
				(list 40	(null)	(null)	50  ) ;12
				(list 50	(null)	(null)	40  ) ;13
				(list 100	100		"E"		100 ) ;14
			)
	))

	(declare (assoc
		features (list "A" "B" "C" "D")
		ctx_features (list "A" "B" "C")
		act_features (list "D")
		result (null)
	))

	(call_entity "howso" "train" (assoc
		features features
		cases data
	))

	(call_entity "howso" "analyze" (assoc
		k_values (list 3)
		p_values (list 0.5 1 2)
		use_deviations (false)
	))

	(declare (assoc
		opt_hp_map
			(get
				(call_entity "howso" "get_params")
				(list 1 "payload" "hyperparameter_map")
			)
	))

	(assign (assoc result (get opt_hp_map ["targetless" "A.B.C.D." ".none" "featureDeviations"]) ))
	(print "Analyzed with null deviations: ")

	(call assert_approximate (assoc
		obs (get result "B")
		exp (list (null) 90 30)
		thresh 20
	))
	(call assert_approximate (assoc
		obs (get result "C")
		exp (list (null) 1 0.5)
		thresh .3
	))

	(call_entity "howso" "set_params" (assoc
		hyperparameter_map
			(set
				opt_hp_map
				["targetless" "A.B.C.D." ".none" "featureDeviations"]
				(null)
			)
	))

	(assign (assoc
		result
			(call_entity "howso" "single_react" (assoc
				action_features act_features
				context_features ctx_features
				context_values (list 30 (null) (null))
				details (assoc "influential_cases" (true) )
			))
	))
	(print "Action predicted ~43.3 : ")
	(call assert_approximate (assoc
		obs (get result (list 1 "payload" "action_values" 0))
		exp 43.3
	))

	(print "Similar cases are equal in influence:\n")
	(call assert_approximate (assoc
		obs (get result (list 1 "payload" "influential_cases" 0 ".influence_weight"))
		exp 0.33
	))
	(call assert_approximate (assoc
		obs (get result (list 1 "payload" "influential_cases" 1 ".influence_weight"))
		exp 0.33
	))
	(call assert_approximate (assoc
		obs (get result (list 1 "payload" "influential_cases" 2 ".influence_weight"))
		exp 0.33
	))
	(call exit_if_failures (assoc msg "Null-value uncertainty react." ))

	(call_entity "howso" "set_params" (assoc
		hyperparameter_map opt_hp_map
	))

	(assign (assoc
		result
			(call_entity "howso" "single_react" (assoc
				action_features act_features
				context_features ctx_features
				context_values (list 30 (null) (null))
				details (assoc "influential_cases" (true) )
			))
	))

	(print "Action predicted > 45 : ")
	(call assert_true (assoc
		obs (> (get result (list 1 "payload" "action_values" 0)) 45)
	))

	(print "Similar cases are not equal in influence:\n")
	(call assert_approximate (assoc
		obs (get result (list 1 "payload" "influential_cases" 0 ".influence_weight"))
		exp 0.42
		thresh 0.1
	))
	(call assert_approximate (assoc
		obs (get result (list 1 "payload" "influential_cases" 1 ".influence_weight"))
		exp 0.32
		thresh 0.1
	))
	(call assert_approximate (assoc
		obs (get result (list 1 "payload" "influential_cases" 2 ".influence_weight"))
		exp 0.26
		thresh 0.1
	))

	(call exit_if_failures (assoc msg "Null-null uncertainty react." ))

	(declare (assoc
		old_null_dev
			(get
				(call_entity "howso" "get_params")
				(list 1 "payload" "hyperparameter_map" "targetless" "A.B.C.D." ".none" "featureDomainAttributes")
			)
	))
	(call_entity "howso" "set_feature_attributes" (assoc
		feature_attributes
			(assoc
				"C" (assoc "type" "nominal")
				"A" (assoc "type" "continuous" "bounds" (assoc "min" 0 "max" 120))
			)
	))

	(declare (assoc
		new_null_dev
			(get
				(call_entity "howso" "get_params")
				(list 1 "payload" "hyperparameter_map" "targetless" "A.B.C.D." ".none" "featureDomainAttributes")
			)
	))

	(print "Calling set feature attributes again updates null deviations correctly: ")
	;since we set A to be between 0 and 120, it should now also have that max uncertainty for feature A
	;while the previous values should remain the unchanged
	(call assert_same (assoc
		obs new_null_dev
		exp (append old_null_dev (assoc "A" 120))
	))


	(call exit_if_failures (assoc msg "Set feature attributes updates null deviations."))


	(call_entity "howso" "analyze" (assoc
		use_deviations (true)
	))
	(assign (assoc
		result
			(get
				(call_entity "howso" "get_params")
				(list 1 "payload" "hyperparameter_map" "targetless" "A.B.C.D." ".none" "featureDeviations" "C")
			)
	))
	(print "SDM and null deviations are stored correctly in hyperparameters: ")
	(print "tuple size of 3 [deviations, value-null, null-null]: ")
	(call assert_same (assoc
		exp 3
		obs (size result)
	))

	(print "first value in that tuple is a list (pair): ")
	(call assert_true (assoc
		obs (~ (list) (first result))
	))

	(print "first value n that pair is an assoc (SDM): ")
	(call assert_true (assoc
		obs (~ (assoc) (first (first result)))
	))

	;missing_value_accuracy test
	(assign (assoc
		result
			(call_entity "howso" "get_prediction_stats" (assoc
				details
					(assoc
						"prediction_stats" (true)
						"selected_prediction_stats" ["missing_value_accuracy"]
					)
			))
	))
	(call keep_result_payload)

	(print "Missing value accuracy is as expected: ")
	(call assert_same (assoc
		obs result
		exp
			(assoc
				B (assoc missing_value_accuracy 0.875)
				C (assoc missing_value_accuracy 0.75)
			)
	))

	(call exit_if_failures (assoc msg unit_test_name ))
)
