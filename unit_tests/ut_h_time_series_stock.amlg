(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_time_series_stock.amlg" debug .false retries 3))

	(declare (assoc
		data
			(list
				(list "stk_A"	8	100)
				(list "stk_A"	9	101)
				(list "stk_A"	10	103)
				(list "stk_A"	11	105)
				(list "stk_A"	12	104)
				(list "stk_A"	13	106)
				(list "stk_A"	14	104)
				(list "stk_A"	15	103)
				(list "stk_A"	16	105)
				(list "stk_A"	17	106)
				(list "stk_A"	18	107)
				(list "stkB"	8	110)
				(list "stkB"	9	109)
				(list "stkB"	10	110)
				(list "stkB"	11	109)
				(list "stkB"	12	108)
				(list "stkB"	13	109)
				(list "stkB"	14	108)
				(list "stkB"	15	106)
				(list "stoC"	5	94)
				(list "stoC"	6	96)
				(list "stoC"	7	95)
				(list "stoC"	8	93)
				(list "stoC"	9	95)
				(list "stoC"	10	98)
				(list "stoC"	11	99)
				(list "stoC"	12	98)
				(list "stoC"	13	95)
				(list "stoC"	14	96)
				(list "stoC"	15	94)
				(list "stoC"	16	93)
				(list "stoC"	17	91)
				(list "stoC"	18	92)
				(list "stoC"	19	90)
				(list "stoC"	20	91)
				(list "stoC"	21	89)
				(list "stoC"	22	91)
			)

		features (list "stock" "time" "value")
		result (null)
		result_features (null)
	))

	(call_entity "howso" "set_params" (assoc
		default_hyperparameter_map (assoc "k" 3 "p" .1 "dt" -1)
	))

	(call_entity "howso" "set_feature_attributes" (assoc
		feature_attributes
			(assoc
				"stock"
					(assoc
						"type" "nominal"
						"id_feature" .true
						"time_series" (assoc "type" "delta")
					)

				"time"
					(assoc
						"type" "continuous"
						"decimal_places" 0
						"time_series" (assoc "time_feature" .true "type" "delta" "universal" .false)
						"bounds"
							(assoc
								"allow_null" .false
								"min" 1
								"max" 30
							)
					)
				"value"
					(assoc
						"type" "continuous"
						"decimal_places" 0
						"time_series" (assoc "type" "delta" "num_lags" 2)
						"bounds"
							(assoc
								"allow_null" .true
								"min" 80
								"max" 125
							)
					)
				"over106"
					(assoc
						"type" "continuous"
						"decimal_places" 0
						"auto_derive_on_train"
							(assoc
								"derive_type" "custom"
								"series_id_features" (list "stock" )
								"ordered_by_features" (list "time")
								;if any value hits 106 set case and all subsequent cases to 1
								"code" "(if (or (>= (call value {feature \"value\"}) 106) (= 1 (call value {feature \"over106\" lag 1})) ) 1 0)"
							)
					)
			)
	))

	(call_entity "howso" "train" (assoc
		cases (trunc data 16)
		features features
		session "session"
	))

	;break up the train into two separate calls where the second train trains only part of a series
	(call_entity "howso" "train" (assoc
		cases (tail data 21)
		features features
		session "session"
		start_index 20
	))

	(print "specified start_index on second train batch works:\n")
	;the 17th case should have training index 20
	(call assert_same (assoc
		obs
			(get
				(call_entity "howso" "get_cases" (assoc
					session "session"
					features [".session_training_index"]
				))
				[1 "payload" "cases" 16 0]
			)
		exp 20
	))

	(assign (assoc
		result
			(call_entity "howso" "react_series" (assoc
				num_series_to_generate 30
				action_features (list "stock" "time" "value")
				desired_conviction 5
				use_differential_privacy .true
			))
	))
	(call keep_result_payload)

	(declare (assoc series_list (get result "action_values") ))

	(declare (assoc
		series_lengths
			(map
				(lambda (size (current_value)))
				series_list
			)
	))

	(print "Average series length matches original lengths: ")
	(call assert_approximate (assoc
		obs (/ (apply "+" series_lengths) (size series_lengths))
		exp 12.5
		thresh 3.5
	))

	(call exit_if_failures (assoc msg "Expected series length synthesised."))

	(assign (assoc
		result
			(call_entity "howso" "react_series" (assoc
				num_series_to_generate 30
				action_features (list "stock" "time" "value")
				desired_conviction 5
				use_differential_privacy .true
				continue_series .true
				series_id_features (list "stock")
				series_id_values (list (list "stk_A"))
				init_time_steps (list 13)
			))
	))
	(call keep_result_payload)

	(assign (assoc series_list (get result "action_values") ))

	(assign (assoc
		series_lengths
			(map
				(lambda (size (current_value)))
				series_list
			)
	))

	(print "Continue series initial time steps correct: ")
	(call assert_same (assoc
		exp (range .true 1 30 1)
		obs
			;should results in a list of 30 .true statemenents
			(map
				(lambda (let
					(assoc first_series_case (first (current_value 1)) )
					;the time step of the first case in each synthed series is 14
					;where each case is a list of [id, time, value]
					(= 13 (get first_series_case 1))
				))
				series_list
			)
	))

	(print "Continued average series length from middle is medium length: ")
	(call assert_approximate (assoc
		obs (/ (apply "+" series_lengths) (size series_lengths))
		exp 6
		thresh 2
	))

	(call exit_if_failures (assoc msg "Conditioned start continue series."))

	(assign (assoc
		result
			(call_entity "howso" "react_series" (assoc
				num_series_to_generate 30
				action_features (list "stock" "time" "value" ".value_lag_2")
				desired_conviction 5
				use_differential_privacy .true
				continue_series .true
				init_time_steps (list 10)
				final_time_steps (list 17)
				series_id_features (list "stock")
				series_id_values (list (list "stk_A"))
			))
	))
	(call keep_result_payload)

	(assign (assoc series_list (get result "action_values") ))

	(assign (assoc
		series_lengths
			(map
				(lambda (size (current_value)))
				series_list
			)
	))

	;validate that each series stats with 10, and that the first two cases both have the correct previous 2-lag values
	(print "Continue series initial time and two lags are correct: ")
	(call assert_same (assoc
		exp (range .true 1 30 1)
		obs
			(map
				(lambda (let
					(assoc
						first_series_case (first (current_value 1))
						second_series_case (get (current_value 1) 1)
					)
					;
					(if (!= 10 (get first_series_case 1))
						.false

						(or
							;two .value_lag_2 values for stk_A
							(and
								(= 100 (last first_series_case))
								(= 101 (last second_series_case))
							)

							;two .value_lag_2 values for stkB
							(and
								(= 110 (last first_series_case))
								(= 109 (last second_series_case))
							)
							;two .value_lag_2 values stoC
							(and
								(= 93 (last first_series_case))
								(= 95 (last second_series_case))
							)
						)
					)
				))
				series_list
			)
	))

	(print "Continued average series length from start to end: ")
	;should all be ~8
	(call assert_approximate (assoc
		obs (/ (apply "+" series_lengths) (size series_lengths))
		exp 8
		thresh 1
	))

	(call exit_if_failures (assoc msg "Continue series initial and final time with two lags."))

	(assign (assoc
		result
			(call_entity "howso" "react_series" (assoc
				num_series_to_generate 5
				action_features (list "stock" "time" "value" ".value_lag_2")
				desired_conviction 5
				use_differential_privacy .true
				continue_series .true
				init_time_steps (list 10)
				output_new_series_ids .false
				series_id_features (list "stock")
				series_id_values (list (list "stk_A") (list "stk_A") (list "stkB") (list "stkB") (list "stoC"))
				leave_series_out .true
			))
	))
	(call keep_result_payload)

	(assign (assoc series_list (get result "action_values") ))

	(print "Continue for specific IDs and start times: ")
	(call assert_true (assoc
		obs
			(and
				;check that each series has at least one case where first feature is series ID and it is correct
				(=
					"stk_A"
					(get series_list (list 0 0 0))
					(get series_list (list 1 0 0))
				)
				(=
					"stkB"
					(get series_list (list 2 0 0))
					(get series_list (list 3 0 0))
				)
				(=
					"stoC"
					(get series_list (list 4 0 0))
				)
			)
	))

	(call exit_if_failures (assoc msg "Continue conditioned on IDs and start time."))

	(assign (assoc
		result
			(call_entity "howso" "react_series" (assoc
				num_series_to_generate 1
				action_features (list "stock" "time" "value" ".value_lag_2")
				desired_conviction 5
				use_differential_privacy .true
				continue_series .true
				init_time_steps (list 10)
				output_new_series_ids .false
				series_id_features (list "stock")
				series_id_values (list (list "fake_stock"))
			))
	))
	(call keep_result_warnings)

	(print "Warning given when using an ID that was not trained: ")
	(call assert_true (assoc
		obs (contains_value result "There is no series trained with the set of IDs:\nstock: fake_stock\n")
	))

	(assign (assoc
		result
			(call_entity "howso" "react_series" (assoc
				num_series_to_generate 1
				action_features (list "stock" "time" "value" ".value_lag_2")
				desired_conviction 5
				use_differential_privacy .true
				continue_series .true
				init_time_steps (list 10)
				output_new_series_ids .false
			))
	))
	(call keep_result_errors)

	(print "Error given when continue_series is set but no data is given: ")
	(call assert_same (assoc
		obs result
		exp "When using the continue_series flag, either series_id_values or series_context_values must be specified, but not both."
	))

	(assign (assoc
		result
			(call_entity "howso" "react_series" (assoc
				num_series_to_generate 1
				action_features (list "stock" "time" "value" ".value_lag_2")
				desired_conviction 5
				use_differential_privacy .true
				continue_series .true
				init_time_steps (list 10)
				output_new_series_ids .false
				series_id_features (list "stock")
				series_id_values (list (list "fake_stock"))
				series_context_features (list "stock" "time" "value")
				series_context_values [ [ ["stk_A" 1 3] ] ]
			))
	))
	(call keep_result_errors)

	(print "Error given when continue_series is set but both data are given: ")
	(call assert_same (assoc
		obs result
		exp "When using the continue_series flag, either series_id_values or series_context_values must be specified, but not both."
	))

	;set attributes to verify that continue doesn't work on terminated series
	(call_entity "howso" "set_feature_attributes" (assoc
		feature_attributes
			(assoc
				"stock"
					(assoc
						"type" "nominal"
						"id_feature" .true
						"time_series"
							(assoc
								"type" "delta"
							)
					)

				"time"
					(assoc
						"type" "continuous"
						"decimal_places" 0
						"time_series" (assoc "time_feature" .true "type" "delta")
						"bounds" (assoc "allow_null" .false "min" 1 "max" 30)
					)
				"value"
					(assoc
						"type" "continuous"
						"decimal_places" 0
						"time_series" (assoc "type" "delta" "num_lags" 2)
						"bounds" (assoc "allow_null" .true "min" 60 "max" 125)
					)
				"over106"
					(assoc
						"type" "continuous"
						"decimal_places" 0
						"auto_derive_on_train"
							(assoc
								"derive_type" "custom"
								"series_id_features" (list "stock" )
								"ordered_by_features" (list "time")
								;if any value hits 106 set case and all subsequent cases to 1
								"code" "(if (or (>= (call value {feature \"value\"}) 106) (= 1 (call value {feature \"over106\" lag 1})) ) 1 0)"
							)
					)
			)
	))



	(assign (assoc
		result
			(call_entity "howso" "react_series" (assoc
				num_series_to_generate 3
				action_features (list "stock" "time" "value")
				desired_conviction 5
				use_differential_privacy .true
				continue_series .true
				series_context_values
					(list
						(list
							(list "stoX"	3	85)
							(list "stoX"	4	86)
						)
					)

			))
	))
	(call keep_result_errors)

	(print "Errors out when series_context_features isn't specified: ")
	(call assert_same (assoc
		obs result
		exp "series_context_values is provided without series_context_features, please specify series_context_features."
	))

	(assign (assoc
		result
			(get
				(call_entity "howso" "react_series" (assoc
					num_series_to_generate 2
					action_features (list "stock" "time" "value")
					desired_conviction 5
					use_differential_privacy .true
					output_new_series_ids .false
					continue_series .true
					details {"series_residuals" .true "series_residuals_num_samples" 5}
					series_context_features features
					series_context_values
						(list
							;validate that the time series will be sorted correctly
							(list
								(list "stoX"	3	85)
								(list "stoX"	4	86)
							)
							(list
								(list "stoY"	24	64)
								(list "stoY"	25	66)
							)
						)
				))
				(list 1 "payload")
			)
	))

	;first row of first series
	(declare (assoc first_row (get result (list "action_values" 0 0)) ))

	(print "Continued first series stoX correctly: ")
	(call assert_same (assoc
		obs (unzip (zip features first_row) (list "stock" "time"))
		exp (list "stoX" 5)
	))

	(call assert_approximate (assoc
		obs (first (unzip (zip features first_row) (list "value")))
		exp 85
		thresh 5
	))

	;first row of second series
	(assign (assoc first_row (get result (list "action_values" 1 0)) ))

	(print "Continued second series stoY correctly: ")
	(call assert_same (assoc
		obs (unzip (zip features first_row) (list "stock" "time"))
		exp (list "stoY" 26)
	))

	(call assert_approximate (assoc
		obs (first (unzip (zip features first_row) (list "value")))
		exp 66
		thresh 5
	))

	(call exit_if_failures (assoc msg "Continue untrained series."))

	(print "Series uncertainty computes successfully: ")
	(call assert_true (assoc
		obs (<= 0 (get result ["series_residuals" 0 0 "value"]))
	))
	(call assert_true (assoc
		obs (<= 0 (get result ["series_residuals" 1 -1 "value"]))
	))

	(call_entity "howso" "train" (assoc
		cases
			(list
				(list "sd"	10	120)
				(list "sd"	11	133)
				(list "sd"	12	168)
				(list "sd"	13	201)
				(list "sd"	14	200)
				(list "sd"	15	202)
				(list "sd" 16 200)
				(list "sd" 17 198)
				(list "sd" 18 196)
				(list "sd" 19 193)
			)
		features features
		session "session"
	))

	(assign (assoc
		result
			(call_entity "howso" "single_react" (assoc
				context_features (list "stock" "time")
				context_values (list "sd" 11.5)
				action_features (list "value")
			))
	))
	(call keep_result_payload)

	;predicted value is below the value for timestamp 11 since it isn't allowed to look at the future for this ID
	(print "Doesn't use future data in react: ")
	(call assert_true (assoc
		obs (< (get result (list "action_values" 0)) 133)
	))


	(assign (assoc
		result (call_entity "howso" "get_cases" (assoc features ["stock" "over106"]))
	))
	(call keep_result_payload)

	(print "Stationary flag feature correctly marks each series: ")
	(call assert_same (assoc
		obs (get result "cases")
		exp
			(append
				(range (lambda ["stk_A" 1]) 1 11 1)
				(range (lambda ["stkB" 1]) 1 8 1)
				(range (lambda ["stoC" 0]) 1 18 1)
				(range (lambda ["sd" 1]) 1 10 1)
			)
	))

	(call_entity "howso" "set_feature_attributes" (assoc
		feature_attributes
			(assoc
				"stock"
					(assoc
						"type" "nominal"
						"id_feature" .true
						"time_series"
							(assoc
								"type" "delta"
							)
					)

				"time"
					(assoc
						"type" "continuous"
						"decimal_places" 0
						"time_series"
							(assoc
								"time_feature" .true
								"type" "delta"
								"universal" .true
							)
						"bounds" (assoc "allow_null" .false "min" 1 "max" 30)
					)
				"value"
					(assoc
						"type" "continuous"
						"decimal_places" 0
						"time_series" (assoc "type" "delta" "num_lags" 2)
						"bounds" (assoc "allow_null" .true "min" 60 "max" 125)
					)
			)
	))

	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				context_features (list "time")
				context_values (list (list 10.5))
				action_features (list "stock" "value")
				desired_conviction 5
				num_cases_to_generate 100
			))
	))
	(call keep_result_payload)

	;predicted value is below 125 because all cases with timestamp of 10 or less have smaller values
	(print "Time feature Universal flag prevents usage of any future data in react: ")
	(call assert_true (assoc
		obs
			(<
				(apply "max"
					;pull just the generated values
					(map
						(lambda (last (current_value)) )
						(get result (list "action_values"))
					)
				)
				130
			)
	))

	(call exit_if_failures (assoc msg "Ignore future values in reacts."))

	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features ["value" ".value_delta_1"]
				derived_action_features ["value" ]
				context_features
					[".time_delta_1"]
				context_values
					[
						[1000]
					]
				desired_conviction (null)
			))
	))
	(call keep_result_warnings)

	(call assert_true (assoc
		obs
			(or
				;the necessary features have no guaranteed order
				(contains_value
					result
					"\"value\" is specified in `derived_action_features` but not all of the features required for its derivation are specified as either context or action features. The features necessary for derivation are: \".value_delta_1\", \".value_lag_1\""
				)
				(contains_value
					result
					"\"value\" is specified in `derived_action_features` but not all of the features required for its derivation are specified as either context or action features. The features necessary for derivation are: \".value_lag_1\", \".value_delta_1\""
				)
			)
	))
	(call exit_if_failures (assoc msg "Warn on derived_action_features without necessary other features."))


	(call_entity "howso" "set_auto_ablation_params" (assoc min_num_cases 10 ))
	(call_entity "howso" "reduce_data" (assoc reduce_max_cases 10))
	(assign (assoc
		result (call_entity "howso" "get_cases" (assoc features [".series_index" ".reverse_series_index" "stock"]))
	))
	(call keep_result_payload)

	;get all cases that have a .series_index of 0
	(declare (assoc
		first_series_cases
			(filter
				(lambda (= 0 (first (current_value))))
				(get result "cases")
			)
	))
	(declare (assoc
		last_series_cases
			(filter
				(lambda (= 0 (get (current_value) 1)))
				(get result "cases")
			)
	))

	(print "There should be the same amount of 'first' and 'last' series cases: ")
	(call assert_same (assoc
		exp (size last_series_cases)
		obs (size first_series_cases)
	))

	(call exit_if_failures (assoc msg "Reducing data keeps first and last series cases if keeping a series."))

	(call exit_if_failures (assoc msg unit_test_name))
)