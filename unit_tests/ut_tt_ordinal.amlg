(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call init_unit_test (assoc name "ut_tt_ordinal.amlg"))

	(print "loaded model: " (load_entity "../trainee_template.amlg" "model6" (false) (false))  "\n")
	(call_entity "model6" "Initialize")

	(assign_to_entities "model6" (assoc
		defaultHyperparameters (assoc "k" 4 "p" 2 "dt" -1 "paramPath" (list ".default"))
	))

	(call_entity "model6" "set_feature_attributes" (assoc
		features
			(assoc
				"k" (assoc "type" "continuous")
				"y" (assoc "type" "ordinal")
			)
	))

	(assign (assoc
		result
			(get (call_entity "model6" "get_feature_attributes") "payload")
	))

	(print "Feature attributes after calling set_feature_attributes: " )
	(call assert_same (assoc
		obs result
		exp
			(assoc
				"k" (assoc "type" "continuous")
				"y" (assoc "type" "ordinal")
			)
	))

	(map
		(lambda
			(call_entity "model6" "train" (assoc
				features (list "x" "y")
				input_cases (list (current_value 2))
				session "my_session"
			))
		)
		(list
			(list 1 20)
			(list 2 30)
			(list 6 40)
			(list 7 20)
		)
	)

	;X: 1 	2 	3 	4 	5 	6 	7
	;Y: 20  30				40	20
	; if we want to react to X=4, the closest ordinal values are either 30 or 40, but the sum of the weights of the two 20s should outweigh the
	; closer values and thus the result here should be 20
	(assign (assoc
		result
			(call_entity "model6" "ReactDiscriminative" (assoc
				return_action_values_only (true)
				action_features (list "y")
				context_features (list "x")
				context_values (list 4)
			))
	))

	(print "Ordinal results matching greater number of farther neighbors: ")
	(call assert_same (assoc obs result exp (list 20) ))


	;if reacting to X=3, that's close enough to the Y value of 30 that it'll outweigh all others and the result will still be 30
	(assign (assoc
		result
			(call_entity "model6" "ReactDiscriminative" (assoc
				return_action_values_only (true)
				action_features (list "y")
				context_features (list "x")
				context_values (list 3)
			))
	))

	(print "Ordinal result matching closest neighbor: ")
	(call assert_same (assoc obs result exp (list 30) ))

	(call exit_if_failures (assoc msg unit_test_name))
)
