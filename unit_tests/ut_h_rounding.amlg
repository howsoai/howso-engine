(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_rounding.amlg"))

	(declare (assoc
		data
			(list
				(list 1	 112313.657464 "a")
				(list 2	 112313.657464 "b")
				(list 3	 112313.657464 "c")
				(list 4	 112313.657464 "d")
				(list 5	 112313.657464 "e")
				(list 6	 112313.657464 "a")
				(list 7	 112313.657464 "b")
				(list 8	 112313.657464 "c")
				(list 9	 112313.657464 "d")
				(list 10 112313.657464 "e")
				(list 11 112313.657464 "a")
				(list 12 112313.657464 "b")
				(list 13 112313.657464 "c")
				(list 14 112313.657464 "d")
				(list 15 112313.657464 "e")
				(list 16 112313.657464 "a")
				(list 17 112313.657464 "b")
				(list 18 112313.657464 "c")
				(list 19 112313.657464 "d")
				(list 20 112313.657464 "e")
				(list 21 112313.657464 "a")
				(list 22 112313.657464 "b")
				(list 23 112313.657464 "c")
				(list 24 112313.657464 "d")
				(list 25 112313.657464 "e")
			)
	))
	(declare (assoc
		features (list "id" "val" "nom" )
	))

	(call_entity "howso" "set_params" (assoc
		default_hyperparameter_map (assoc "k" 1 "p" 2 "dt" -1)
	))

	(call_entity "howso" "set_feature_attributes" (assoc
		feature_attributes
			(assoc
				"id"  (assoc "type" "nominal")
				"val" (assoc "type" "continuous" "significant_digits" 3)
				"nom" (assoc "type" "nominal")
			)
	))

	(call_entity "howso" "train" (assoc
		cases data
		features features
		session "session"
	))

	(assign (assoc
		result
			(get
				(call_entity "howso" "single_react" (assoc
					context_features (list "id" "nom")
					context_values (list 19.8 "e")
					action_features (list "val")

				))
				(list 1 "payload" "action_values")
			)
	))
	(print "rounding 3 significant digits: ")
	(call assert_same (assoc obs (first result) exp 112000))

	(call_entity "howso" "set_feature_attributes" (assoc
		feature_attributes
			(assoc
				 "id"  (assoc "type" "nominal")
				"val"  (assoc "type" "continuous" "decimal_places" 2)
				"nom"  (assoc "type" "nominal")
			)
	))

	(assign (assoc
		result
			(call_entity "howso" "single_react" (assoc
				context_features (list "id" "nom")
				context_values (list 19.8 "e")
				action_features (list "val")
				details (assoc "influential_cases" (true))
			))
	))
	(print "rounding 2 decimal places: ")
	(call assert_same (assoc obs (get result (list 1 "payload" "action_values" 0 )) exp 112313.66))

	(print "rounding in details: ")
	(call assert_same (assoc obs (get result (list 1 "payload" "influential_cases" 0 "val")) exp 112313.66))

	(assign (assoc
		result
			(call_entity "howso" "get_cases" (assoc features features))
	))
	(declare (assoc
		all_vals
			(append
				112313.66
				(map
					(lambda
						(get (current_value) 1)
					)
					(get result (list 1 "payload" "cases"))
				)
			)
	))

	(print "rounding in retrieve cases: ")
	(call assert_true (assoc obs (apply "=" all_vals)))


	(print "\nrounding and feature preservation in generate case 1: ")
	(assign (assoc
		result
			(get
				(call_entity "howso" "single_react" (assoc
					desired_conviction 10
					action_features (list "val" "nom")
					context_features (list "id")
					case_indices (list "session" 2)
					preserve_feature_values (list "id")
				))
				(list 1 "payload" "action_values")
			)
	))
	(call assert_approximate(assoc
		obs result
		exp (list 112313.66 "c")
		thresh 0.03
	))

	(print "rounding and feature preservation in generate case 2: ")
	(assign (assoc
		result
			(call_entity "howso" "single_react" (assoc
				desired_conviction 10
				action_features features
				context_features (list "id")
				context_values (list 6)
				case_indices (list "session" 5)
				preserve_feature_values (list "id")
			))
	))
	(call assert_approximate  (assoc
		obs (get result (list 1 "payload" "action_values"))
		exp (list 6 112313.66 "a")
		thresh 0.03
	))


	(call_entity "howso" "train" (assoc
		cases
			(list
				(list 30 (null) "e")
				(list 31 (null) "e")
				(list 32 (null) "e")
				(list 33 (null) "e")
			)
		features features
		session "session2"
	))

	(assign (assoc
		result
			(call_entity "howso" "single_react" (assoc
				desired_conviction 10
				action_features (list "val")
				context_features (list "id")
				context_values (list 33)
			))
	))
	(print "Rounding on null values results in null: ")
	(call assert_null (assoc
		obs (get result (list 1 "payload" "action_values" 0))
	))

	(call exit_if_failures (assoc msg unit_test_name ))
)