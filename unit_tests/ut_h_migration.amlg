(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_migration.amlg"))

	;restore 'saved' trainee from original so that it can be operated on
	(if (= (system "os") "Windows")
		(system "system" "copy /Y  unit_test_data\\saved_derived_Original.amlg unit_test_data\\saved_derived.amlg")

		(system "system" "cp  unit_test_data/saved_derived_Original.amlg unit_test_data/saved_derived.amlg")
	)

	(destroy_entities "howso")
	;load up the old trainee as 'howso'
	(load_entity "unit_test_data/saved_derived.amlg" "howso" (false) (false))

	;export it out as 'saved_derived'
	(call_entity "howso" "export_trainee" (assoc
		trainee "saved_derived"
		trainee_filepath "unit_test_data/"
		root_filepath "../"
		;decoded_cases (true)
	))

	;verify files have been exported
	(declare (assoc
		result (null)
		import_metadata_map (load "../migrations/saved_derived.meta.json")
		import_cases_and_sessions_map (load "../migrations/saved_derived.exp.json")
	))

	(print "Exported json: ")
	(call assert_true (assoc
		obs (and (!= (null) import_metadata_map) (!= (null) import_cases_and_sessions_map))
	))

	(print "Export version should be 64.0.0 : ")
	(call assert_same (assoc
		exp (list 64 0 0)
		obs
			(list
				(get import_metadata_map "!majorVersion")
				(get import_metadata_map "!minorVersion")
				(get import_metadata_map "!pointVersion")
			)
	))

	(call exit_if_failures (assoc msg "Exporting JSON." ))


	(call_entity "howso" "upgrade_trainee" (assoc
		trainee "saved_derived"
		trainee_filepath "unit_test_data/"
		root_filepath "../"
	))

	(store "unit_test_data/saved_derived.amlg" (flatten_entity "howso" (false)) )
	(load_entity "unit_test_data/saved_derived.amlg" "saved_derived" (false) (false))

	(print "\n")

	(print "Verify upgraded version is 0.0.0 : ")
	(call assert_same (assoc
		exp (list 0 0 0)
		obs (call_entity "saved_derived" "debug_label" (assoc label (list "!majorVersion" "!minorVersion" "!pointVersion") ))
	))


	(print "Verify feature limits are correct: ")
	(call assert_same (assoc
		obs
			(get
				(call_entity "saved_derived" "debug_label" (assoc label "!hyperparameterMetadataMap"))
				(list ".targetless" ".time_delta..time_end..time_lag..time_start..value_delta..value_lag.stock.time.value." "robust" ".none" "featureDomainAttributes")
			)
		exp
			(assoc
				".custom_nominal" 0
				".time_end" 2
				".time_lag" 21600
				".time_start" 2
				stock 2
				time 21600
				value 17
			)
	))

	(call exit_if_failures (assoc msg "Migration version script." ))

	;Sanity check that some cached values are persisted:
	(print "Persisted !cachedFeatureMinResidualMap: ")
	(call assert_same (assoc
		obs (call_entity "saved_derived" "debug_label" (assoc label "!cachedFeatureMinResidualMap"))
		exp
			 (assoc
				".time_delta" 2.727272727272727
				".time_end" 0.022727272727272728
				".time_lag" 15.874272644519806
				".time_start" 0.022727272727272728
				".value_delta" 0.004409520179674654
				".value_lag" 0.0034700315871958117
				stock 0.022727272727272728
				time 16.363636363636363
				value 0.011363636363636364
		)
	))

	(print "Persisted !featureCustomDerivedMethods correctly: ")
	(call assert_same (assoc
		obs (get (call_entity "saved_derived" "debug_label" (assoc label "!featureCustomDerivedMethods")) (list "stock" "react"))
		exp
			(lambda
				(if (and (= 1 0) (contains_index feature_values_map "stock") )
					(get feature_values_map "stock")
					(null)
				)
			)
	))

	(print "Persisted !featureCustomDerivedMethods correctly: ")
	(call assert_same (assoc
		obs (get (call_entity "saved_derived" "debug_label" (assoc label "!featureCustomDerivedMethods")) (list ".time_lag" "train"))
		exp
			(lambda
				(if (and (>= (- series_row_index 1) 0) (contains_index feat_index_map "time"))
					(get series_data (list (- series_row_index 1) (get feat_index_map "time")) )
					(null)
				)
			)
	))

	(print "Persisted !hyperparameterMetadataMap: ")
	(call assert_same (assoc
		obs (call_entity "saved_derived" "debug_label" (assoc label "!hyperparameterMetadataMap"))
		exp
			(assoc
				".targetless" (assoc
					".time_delta..time_end..time_lag..time_start..value_delta..value_lag.stock.time.value." (assoc
						robust (assoc
							".none" (assoc
								allFeatureResidualsCached (true)
								dt -1
								featureDeviations (assoc
									".time_delta" 931.3147273928937
									".time_end" 0.9858630952380952
									".time_lag" 6897.97588602826
									".time_start" 0.9858630952380952
									".value_delta" 1.2260481504030745
									".value_lag" 1.5864199026018293
									stock (list 0.23303326875548294 1 0.7)
									time 7902.125091005117
									value 1.7032763286779884
								)
								featureWeights (assoc
									".time_delta" 0.4987065787622407
									".time_end" 1
									".time_lag" 0.40896345735135664
									".time_start" 1
									".value_delta" 0.9812613140588551
									".value_lag" 0.9525978606678481
									stock 1.157461093425284
									time 0.4134584465891669
									value 0.9606120786030677
								)
								featureDomainAttributes (assoc
									".custom_nominal" 0
									".time_end" 2
									".time_lag" 21600
									".time_start" 2
									stock 2
									time 21600
									value 17
								)
								!nullUncertainties (assoc
											stock (list (null) 1 0.7 )
										)
								gridSearchError 75111.0192867884
								k 5
								p 0.1
								paramPath (list ".targetless" ".time_delta..time_end..time_lag..time_start..value_delta..value_lag.stock.time.value." "robust" ".none")
							)
						)
					)
				)
			)
		unordered (true)
	))

	(call exit_if_failures (assoc msg "Persisted model metadata." ))


	(print "Persisted cases correctly: ")
	(call assert_same (assoc
		obs (get (call_entity "saved_derived" "get_num_training_cases") (list "payload" "count"))
		exp 21
	))


	;pull data for case index 14
	(assign (assoc
		result
			(get
				(call_entity "saved_derived" "get_cases" (assoc
					case_indices (list (list "session" 14))
				))
				(list "payload")
			)
	))

	(print "Persisted particular case data correctly: ")
	(call assert_same (assoc
		obs (zip (get result "features") (get result (list "cases" 0)))
		exp
			(assoc
				".time_delta" 4320
				".time_end" (false)
				".time_lag" (format 1585735200 "number" "date:%Y-%m-%dT%H:%M:%S")
				".time_start" (false)
				".value_delta" -1
				".value_lag" 110
				stock "stkB"
				time (format 1585739520 "number" "date:%Y-%m-%dT%H:%M:%S")
				value 109
			)
	))

	(call exit_if_failures (assoc msg "Persisted cases." ))


	 ;cleanup saved test model
	(declare (assoc
		remove "del "
		slash "\\"
	))
	(if (!= (system "os") "Windows")
		(assign (assoc remove "rm " slash "/"))
	)

	(system "system" (concat remove "unit_test_data" slash "saved_derived.amlg") )
	(system "system" (concat remove ".." slash "migrations" slash "saved_derived.exp.json") )
	(system "system" (concat remove ".." slash "migrations" slash "saved_derived.meta.json") )

	(call exit_if_failures (assoc msg unit_test_name ))

)


