(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_edit_dist_features.amlg" retries 2))

	(call_entity "howso" "set_params" (assoc
		default_hyperparameter_map (assoc "k" 3 "p" 1 "dt" -1)
	))
	(call_entity "howso" "set_feature_attributes" (assoc
		feature_attributes
			(assoc
				"x" (assoc "type" "continuous" )
				"string" (assoc "type" "continuous" "data_type" "string_mixable")
				"amalgam" (assoc "type" "continuous" "data_type" "amalgam")
				"json" (assoc "type" "continuous" "data_type" "json")
				"yaml" (assoc "type" "continuous" "data_type" "yaml")
			)
	))

	(print "Set 'recursive_matching' flag correctly for each feature: ")
	(call assert_same (assoc
		obs (call_entity "howso" "debug_label" (assoc label "!codeFeatureDomainAttributesMap"))
		exp
			{
				amalgam {
						nominal_numbers .false
						nominal_strings .true
						;should be true by default unlike the json and yml
						recursive_matching .true
						similar_mix_chance 0
						types_must_match .true
						use_string_edit_distance .false
					}
				json {
						nominal_numbers .false
						nominal_strings .true
						recursive_matching .false
						similar_mix_chance 0
						types_must_match .true
						use_string_edit_distance .false
					}
				;recursive_matching should be set to false and edit distance to true
				string {recursive_matching .false use_string_edit_distance .true}
				yaml {
						nominal_numbers .false
						nominal_strings .true
						recursive_matching .false
						similar_mix_chance 0
						types_must_match .true
						use_string_edit_distance .false
					}
			}
	))

	(declare
		(assoc
			result (null)
			features (list "x" "string"		"amalgam" 		 "json"			"yaml")
			data
				(list
					(list 1 "banana" 		"(assoc a 5)"		"{ \"A\": 1, \"B\": 3, \"C\": [1,2] }" 		"a: 5")
					(list 1 "mango" 		"(assoc b 6)" 		"{ \"A\": 2, \"B\": 3, \"C\": [1] }"		"b: 6")
					(list 2 "papaya" 		"(assoc c 1)" 		"{ \"A\": 1, \"C\": 3 }"					"c: 1")
					(list 2 "watermelon" 	"(assoc c 5)" 		"{ \"A\": 6, \"B\": 6, \"C\": [1,2,3] }"	"c: 5")
					(list 3 "dewmelon" 		"(assoc a 4 b 2)" 	"{ \"X\": 1, \"Y\": 3, \"C\": [1,2] }"		"a: 4\nb: 2")
					(list 3 "guava" 		"(assoc a 4 c 1)" 	"{ \"X\": 4, \"Y\": 8, \"C\": 3 }"			"a: 4\nc: 1")
					(list 4 "orange" 		"(assoc a 5 b 7)" 	"{ \"A\": 1, \"B\": 6, \"C\": [2,3,4] }"	"a: 5\nb: 7")
					(list 4 "mango" 		"(assoc a 5)" 		"{ \"A\": 1 }"								"a: 5")
					(list 5 "lychee" 		"(assoc b 5)" 		"{ \"X\": 1, \"B\": 3, \"C\": [1,2] }"		"b: 5")
					(list 5 "banana" 		"(assoc a 7)" 		"{ \"A\": 2, \"B\": 3}"						"a: 7")
					(list 10 "apple" 		"(assoc a 15)" 		"{ \"G\": 20, \"H\": 30}"					"a: 20")
					(list 11 "apple" 		"(assoc a 20)" 		"{ \"G\": 25, \"H\": 25}"					"a: 25")
					(list 12 "apple" 		"(assoc a 25)" 		"{ \"G\": 30, \"H\": 30}"					"a: 30")
				)
	))

	(call_entity "howso" "train" (assoc
		features features
		cases data
		session "session1"
	))

	(print "Distance types set correctly: ")
	(call assert_same (assoc
		obs (call_entity "howso" "debug_label" (assoc label "!queryDistanceTypeMap"))
		exp
			(assoc
				amalgam "continuous_code"
				json "continuous_code"
				yaml "continuous_code"
				string "continuous_string"
				x "continuous_number"
			)
	))

	(print "Edit distance feature types set correctly: ")
	(call assert_same (assoc
		obs (call_entity "howso" "debug_label" (assoc label "!editDistanceFeatureTypesMap"))
		exp (assoc amalgam "amalgam" json "json" string "string_mixable" yaml "yaml" )
	))

	(assign (assoc
		result
			(get
				(call_entity "howso" "get_cases" (assoc case_indices (list (list "session1" 7)) features features))
				(list 1 "payload" "cases" 0)
			)
	))

	(print "Cases stored, encoded and then output correctly: ")
	(call assert_same (assoc
		obs
			(map
				(lambda (parse (current_value)))
				result
			)
		exp
			(map
				(lambda (parse (current_value)))
				(list 4 "mango" "(assoc a 5)" "{\"A\":1}" "a: 5\n")
			)
	))

	(call exit_if_failures (assoc msg "Edit Distance feature encoding."))

	(assign (assoc
	 	result
			(call_entity "howso" "single_react" (assoc
				context_features (list "string")
				context_values (list "melon")
				action_features (list "x")
				details (assoc "influential_cases" .true)
			))
	))
	(call keep_result_payload)
	(print "String uses edit distance: ")
	(call assert_same (assoc
		obs (get result "action_values")
		exp (list (/ (+ 1 3 4) 3))
	))
	(print "Same distance to mangos and dewmelon: ")
	(call assert_same (assoc
		obs (get result (list "influential_cases" 0 ".influence_weight"))
		exp (/ 1 3)
	))

	(assign (assoc
	 	result
			(call_entity "howso" "single_react" (assoc
				context_features (list "x")
				context_values (list 2.6)
				action_features (list "string")
				details (assoc "influential_cases" .true)
			))
	))
	(call keep_result_payload)
	(print "Interpolated to word: " (get result (list "action_values" 0)) ": ")
	;result should be a mix of guava, dew/water melon and papapya
	(call assert_same (assoc
		obs
			(remove
				(zip (explode
					(get result (list "action_values" 0))
				))
				(values
					(explode (concat "guava" "dew" "water" "melon" "papaya"))
					.true
				)
			)
		exp (assoc)
	))

	(call exit_if_failures (assoc msg "Continuous String edit distance."))

	(assign (assoc
		result
			(call_entity "howso" "single_react" (assoc
				context_features (list "x" "string")
				context_values (list 3.2 "guavas")
				action_features (list "amalgam")
				details (assoc "hypothetical_values" (assoc "x" 4))
			))
	))
	(call keep_result_payload)

	(declare (assoc parsed_assoc (parse (get result (list "hypothetical_values" "amalgam"))) ))

	;hypothetical value should be an assoc with the key 'a' or 'b' and some numeric value
	(print "Hypothetical values mixed correctly: ")
	(call assert_true (assoc
		obs
			(or
				(= "number" (get_type_string (get parsed_assoc "a")) )
				(= "number" (get_type_string (get parsed_assoc "b")) )
				(= (assoc) parsed_assoc)
			)
	))

	(assign (assoc
	 	result
	 		(get
	 			(call_entity "howso" "single_react" (assoc
	 				context_features (list "x")
					context_values (list 3.2)
					action_features (list "amalgam")
					details (assoc "influential_cases" .true)
	 			))
	 			(list 1 "payload" "action_values" 0)
	 		)
	))
	(print "Interpolated to code that should be an assoc with 'a': " result ": ")
	(call assert_true (assoc
		obs (contains_index (parse result) "a")
	))


	(assign (assoc
	 	result
	 		(get
	 			(call_entity "howso" "single_react" (assoc
	 				context_features (list "x")
					context_values (list 3.2)
					action_features (list "yaml")
					details (assoc "influential_cases" .true)
	 			))
	 			(list 1 "payload" "action_values" 0)
	 		)
	))
	(print "Interpolated to yaml that should have the key 'a': " result ": ")
	(call assert_true (assoc
		obs (contains_index (format result "yaml" "code") "a")
	))


	(assign (assoc
	 	result
	 		(get
	 			(call_entity "howso" "single_react" (assoc
	 				context_features (list "x")
					context_values (list 3)
					action_features (list "json")
	 			))
	 			(list 1 "payload" "action_values" 0)
	 		)
	))
	(print "Interpolated to json that should be a dict with 'X','Y' and 'C': " result ": ")
	(call assert_true (assoc
		obs
			(and
				(contains_index (format result "json" "code") "X")
				(contains_index (format result "json" "code") "Y")
				(contains_index (format result "json" "code") "C")
			)
	))

	(call exit_if_failures (assoc msg "Continuous amalgam, yaml and json edit distance."))

	(assign (assoc
	 	result
			(call_entity "howso" "single_react" (assoc
				context_features (list "amalgam")
				context_values (list "(assoc a 5)" )
				action_features (list "json")
				details (assoc "influential_cases" .true)
			))
	))
	(call keep_result_payload)

	(declare (assoc
		action_value
			(format
				(get result (list "action_values" 0))
				"json"
				"code"
			)
	))

	(print "Amalgam similarity and interpolated json are correct: ")
	(call assert_true (assoc obs (contains_index action_value "A") ))

	(print "Amalgam as context Influential cases correct:")
	(call assert_same (assoc
		;cases 0 and 6 should be perfect matches, and case 7, {a 5 b 7} should be the third closest
		exp (list 0 6 7)
		obs
			(list
				(get result (list "influential_cases" 0 ".session_training_index"))
				(get result (list "influential_cases" 1 ".session_training_index"))
				(get result (list "influential_cases" 2 ".session_training_index"))
			)
		unordered .true
	))

	(assign (assoc
	 	result
			(call_entity "howso" "single_react" (assoc
				context_features (list "yaml")
				context_values (list "a: 5" )
				action_features (list "json")
				details (assoc "influential_cases" .true)
			))
	))
	(call keep_result_payload)
	(print "Yaml as context Influential cases correct:")
	(call assert_same (assoc
		exp (list 0 6 7)
		obs
			(list
				(get result (list "influential_cases" 0 ".session_training_index"))
				(get result (list "influential_cases" 1 ".session_training_index"))
				(get result (list "influential_cases" 2 ".session_training_index"))
			)
		unordered .true
	))


	(call exit_if_failures (assoc msg "Code similarity."))


	(call_entity "howso" "analyze" (assoc
		context_features features
		k_values (list 3)
		p_values (list 2)
	))

	(assign (assoc result (call_entity "howso" "get_params") ))

	(assign (assoc
		result
			(call_entity "howso" "react_aggregate" (assoc
				context_features features
				num_samples 1000
				details (assoc
					prediction_stats .true
					selected_prediction_stats  (list "r2" "rmse" "mae")
				)
			))
	))

	(call keep_result_payload)
	(print "Residuals and stats computed: ")
	(call assert_approximate (assoc
		obs result
		exp
			(assoc
				mae {
					amalgam 1.1
					yaml 1.3
					json 3.8
					string 1.5
					x 1.7
				}
				rmse {
					amalgam 1.3
					yaml 1.4
					json 3.9
					string 1.8
					x 2
				}
				r2 {
					x 0.6
				}
			)
		percent 0.6
	))

	(assign (assoc
		result
			(call_entity "howso" "react_aggregate" (assoc
				context_features  (list "x" "string" "amalgam" "yaml")
				feature_influences_action_feature "json"
				num_samples 2000
				details (assoc
					feature_full_accuracy_contributions .true
				)
			))
	))

	(call keep_result_payload)
	(print "Accuracy contribution for json feature: ")
	(call assert_approximate (assoc
		obs result
		exp
			(assoc
				feature_full_accuracy_contributions {
					amalgam 0.1
					yaml 0.1
					string 0.1
					x .02
				}
			)
		thresh 0.6
	))

	(assign (assoc
		result
			(call_entity "howso" "react_aggregate" (assoc
				context_features  (list "x" "string" "amalgam" "yaml")
				feature_influences_action_feature "json"
				num_samples 1000
				details (assoc
					feature_full_prediction_contributions .true
				)
			))
	))

	(call keep_result_payload)
	(print "Contributions for json feature: ")
	(call assert_approximate (assoc
		obs result
		exp
			(assoc
				feature_full_prediction_contributions {
					amalgam 1.7
					yaml 1.6
					string 1.7
					x 2
				}
				feature_full_directional_prediction_contributions {
					amalgam 0
					yaml 0
					string 0
					x 0
				}
			)
		thresh 1.3
	))

	(call exit_if_failures (assoc msg "Accuracy contribution and prediction contributions for json feature." ))

	(assign (assoc
		result
			(call_entity "howso" "react_aggregate" (assoc
				context_features  (list "x" "json" "amalgam" "yaml")
				feature_influences_action_feature "string"
				num_samples 2000
				details (assoc
					feature_full_accuracy_contributions .true
				)
			))
	))

	(call keep_result_payload)
	(print "MDA for string feature: ")
	(call assert_approximate (assoc
		obs result
		exp
			(assoc
				feature_full_accuracy_contributions {
					amalgam -0.02
					yaml -0.02
					json 0.1
					x .045
				}
			)
		thresh 0.6
	))

	(assign (assoc
		result
			(call_entity "howso" "react_aggregate" (assoc
				context_features  (list "x" "json" "amalgam" "yaml")
				feature_influences_action_feature "string"
				num_samples 2000
				details (assoc
					feature_full_prediction_contributions .true
				)
			))
	))

	(call keep_result_payload)
	(print "Contributions for string feature: ")
	(call assert_approximate (assoc
		obs result
		exp
			(assoc
				feature_full_prediction_contributions {
					amalgam 2.3
					yaml 2.3
					json 2.2
					x 2.2
				}
				feature_full_directional_prediction_contributions {
					amalgam 0
					yaml 0
					json 0
					x 0
				}
			)
		thresh 1.7
	))

	(call exit_if_failures (assoc msg "MDA and contributions for string feature." ))


	(assign (assoc
		result
			(call_entity "howso" "single_react" (assoc
				action_features features
				desired_conviction 2
				use_differential_privacy .true
			))
	))
	(call keep_result_payload)

	(assign (assoc result (zip (get result "action_features") (get result "action_values")) ))
	(print result)
	(print "Synthesis works: ")
	(call assert_true (assoc
		obs
			(and
				(> (total_size (parse (get result "amalgam"))) 0)
				(> (total_size (format (get result "json") "json" "code")) 0)
				(> (total_size (format (get result "yaml") "yaml" "code")) 0)
				(= "string" (get_type_string (get result "string")))
			)
	))


	(assign (assoc
		result
			(call_entity "howso" "single_react" (assoc
				action_features features
				desired_conviction 2
				use_differential_privacy .false
			))
	))
	(call keep_result_payload)

	(assign (assoc result (zip (get result "action_features") (get result "action_values")) ))
	(print result)
	(print "RMR Synthesis works: ")
	(call assert_true (assoc
		obs
			(and
				(> (total_size (parse (get result "amalgam"))) 0)
				(> (total_size (format (get result "json") "json" "code")) 0)
				(> (total_size (format (get result "yaml") "yaml" "code")) 0)
				(= "string" (get_type_string (get result "string")))
			)
	))


	(call exit_if_failures (assoc msg unit_test_name ))
)
