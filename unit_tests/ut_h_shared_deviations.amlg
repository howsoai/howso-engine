(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_shared_deviations.amlg" retries 1))

	(declare
		(assoc
			result (null)
			features (list "height" "width" "length" "tart" "sweet" "size" "weight" "fruit" "color1" "color2")
			action_features (list  "fruit")
			context_features (list "height" "weight" "length" "tart" "sweet" "size" "weight"  "color1" "color2")
			fruit_data
				(list
					;		"h" "w" "l" 	"tart" "sweet" "size" 	"weight" 	"fruit" "     "color1"     "color2"
					(list 	1 	0	15 		11		1		(null) 	    1 		"strawberry"  "red-1"      "red-1")
					(list 	2   2 	20		21  	.45 	"small" 	.8 		"strawberry"  "red-2"      "red-2")
					(list 	3	4 	35 		31 		.42 	"small" 	1.2 	"strawberry"  "red-3"      "red-3")
					(list 	4	6 	40 		41		.49 	"small" 	1.1 	"strawberry"  "red-4"      "red-4")

					(list 	5	8	55		51 		.4		"small" 	2 		(null)        "green-1"    "green-1")
					(list 	6   10  60		61		.55 	"medium" 	3 		"apple"       "green-1"    "green-2")
					(list 	7   12	75		71		.52 	"medium" 	3.5 	"apple"       "green-1"    "green-3")
					(list 	8   14	80  	81   	.54 	"medium" 	4.5 	(null)        "green-1"    "green-4")

					(list 	9   16	95		91		.60		"small" 	(null) 	"banana"      "yellow-1"   "yellow-1")
					(list 	10  18	100  	101		.65 	"medium" 	(null)	"banana"      "yellow-2"   "yellow-2")
					(list 	11	20	115  	111		.69 	(null) 	    5.5	 	"banana"      "yellow-3"   "yellow-3")
					(list 	12	22	120		121		.62 	"medium" 	7 		"banana"      "yellow-4"   "yellow-4")
				)
		)
	)

	(assign (assoc
		result
			(call_entity "howso" "set_feature_attributes" (assoc
				feature_attributes
					(assoc
						"color1" (assoc "type" "nominal")
						"color2" (assoc "type" "nominal" "unique" .true)
						"fruit" (assoc "type" "nominal" "shared_deviations" (list "color2"))
					)
			))
	))

	(call keep_result_errors)

	(print "Invalid shared deviations feature groupings with unique nominals: ")
	(call assert_same (assoc
		obs result
		exp "Features with shared deviations may not be unique nominals, as they have null deviations. The following features are in a shared deviations group and is a unique nominal: color2."
	))

	(assign (assoc
		result
			(call_entity "howso" "set_feature_attributes" (assoc
				feature_attributes
					(assoc
						"color1" (assoc "type" "nominal")
						"color2" (assoc "type" "nominal")
						"sweet" (assoc "type" "nominal" "shared_deviations" (list "color2"))
						"tart" (assoc "type" "nominal" "shared_deviations" (list "color1"))
						"height" (assoc "type" "nominal" "shared_deviations" (list "weight"))
					)
			))
	))

	(call keep_result_errors)

	(print "Invalid shared deviations feature groupings with nominal and non-nominal featuers: ")
	(call assert_same (assoc
		obs result
		exp "Nominal features may not share deviations with non-nominal features."
	))

	(call_entity "howso" "set_feature_attributes" (assoc
		feature_attributes
			(assoc
				"fruit" (assoc "type" "nominal")
				"color1" (assoc "type" "nominal")
				"color2" (assoc "type" "nominal" "unique" .true)
				"weight" (assoc "type" "continuous" "shared_deviations" (list "height" "tart"))
				"length" (assoc "type" "continuous" "shared_deviations" (list "sweet"))
				"size" (assoc "type" "nominal" "shared_deviations" (list "fruit" "size"))
			)
	))

	(call_entity "howso" "train" (assoc
		features features
		cases fruit_data
		session "unit_test"
	))

	(call_entity "howso" "analyze" (assoc
		context_features features
		action_features (list (last features))
		use_deviations .true
	))

	(declare (assoc
		shared_feature_deviations
			(get (call_entity "howso" "get_params") (list 1 "payload" "hyperparameter_map" "targeted" "color2" "color1.fruit.height.length.size.sweet.tart.weight.width." ".none" "featureDeviations"))
	))

	(print "Features with shared deviations have same deviations: weight and height ")
	(call assert_same (assoc
		obs (get shared_feature_deviations "weight")
		exp	(get shared_feature_deviations "height")
	))
	(print "Features with shared deviations have same deviations: weight and tart ")
	(call assert_same (assoc
		obs (get shared_feature_deviations "weight")
		exp	(get shared_feature_deviations "tart")
	))
	(print "Features with shared deviations have same deviations: length and sweet ")
	(call assert_same (assoc
		obs (get shared_feature_deviations "length")
		exp	(get shared_feature_deviations "sweet")
	))
	(print "Features with shared deviations have same deviations: size and fruit ")
	(call assert_same (assoc
		obs (get shared_feature_deviations "size")
		exp	(get shared_feature_deviations "fruit")
	))

	(call exit_if_failures (assoc msg unit_test_name ))
)
