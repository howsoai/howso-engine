(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_ablate.amlg"))

	(declare (assoc
		cases
			(list
				(list 0 0)
				(list 0 1)
                (list 1 0)
                (list 1 1)

                (list 10 10)
                (list 10 12)
                (list 11 10)
                (list 13 13)
			)
		features (list "A" "B")
	))

    (call_entity "howso" "set_auto_ablation_params" (assoc
        auto_ablation_enabled .true
		min_num_cases 8
		auto_ablation_influence_weight_entropy_threshold 0.6
		batch_size 1
		ablated_cases_distribution_batch_size 1
		;a very low absolute threshold that will certainly not be satisfied.
		abs_threshold_map {accuracy {B 0.001}}
    ))
	(declare (assoc
		regular_train_payload
			(call_entity "howso" "train" (assoc
				features features
				cases cases
				session "unit_test"
			))
	))

	(print "first train call doesn't ablate: ")
	(call assert_same (assoc
		obs (get regular_train_payload (list 1 "payload" "ablated_indices"))
		exp (list)
	))

	(call exit_if_failures (assoc msg "Non-ablation train call") )

	(call_entity "howso" "set_auto_ablation_params" (assoc
        auto_ablation_enabled .true
		min_num_cases 8
		auto_ablation_influence_weight_entropy_threshold 0.6
		batch_size 1
		ablated_cases_distribution_batch_size 1
    ))

	(call_entity "howso" "set_auto_analyze_params" (assoc
		auto_analyze_enabled .true
		analyze_threshold 3
		analyze_growth_factor 2
	))

    (call_entity "howso" "analyze" (assoc
        context_features features
        k_values (list 2)
        p_values (list 1)
		targeted_model "targetless"

		;disable deviations as the small dataset leads to large deviations washing out
		;differences in distances. Disabling deviations makes the toy dataset evaluate
		;the ablation logic correctly.
		use_deviations .false
    ))

	(call_entity "howso" "set_auto_ablation_params" (assoc
        auto_ablation_enabled .true
		min_num_cases 8
		auto_ablation_influence_weight_entropy_threshold 0.6
		batch_size 1
		ablated_cases_distribution_batch_size 1
		;an asburdly high threshold that will instantly trigger, causing the two cases to be trained.
		abs_threshold_map {accuracy {B 1.0}}
    ))

	(call_entity "howso" "set_auto_ablation_params" (assoc
        auto_ablation_enabled .true
		min_num_cases 8
		auto_ablation_influence_weight_entropy_threshold 0.6
		batch_size 1
		ablated_cases_distribution_batch_size 1
    ))

	(declare (assoc
		ablate_train_payload
			(call_entity "howso" "train" (assoc
				features features
				cases
					(list
						(list 0.5 0.5)
						(list 11 11)
					)
				session "unit_test"
			))
	))

	(print "no warnings raised: ")
	(call assert_true (assoc
		obs (= (null) (get ablate_train_payload [1 "warnings"]))
	))
	(call exit_if_failures (assoc msg "No warnings raised") )

	(print "second train call ablates: ")
	(call assert_same (assoc
		obs (get ablate_train_payload (list 1 "payload" "ablated_indices"))
		exp (list 0)
	))

	(call exit_if_failures (assoc msg "Ablation train call") )

	(print "second train call accumulates more data mass than cases: ")
	(call assert_true (assoc
		obs
			(>
				(call_entity "howso" "debug_label" (assoc label "!dataMassChangeSinceLastAnalyze"))
				(get ablate_train_payload (list 1 "payload" "num_trained"))
			)
	))
	(call exit_if_failures (assoc msg "Accumulate data mass"))

	(declare (assoc
		dataset_size
			(get
				(call_entity "howso" "get_num_training_cases")
				(list 1 "payload" "count")
			)
	))
	(print "model size after training and ablating is less than total train data: ")
    (call assert_same (assoc
		obs dataset_size
		exp 9
	))

	(call exit_if_failures (assoc msg "Model size is less than training data size") )

	(assign (assoc
		ablate_train_payload
			(call_entity "howso" "train" (assoc
				features features
				cases
					(list
						(list 0 0)
						(list 1 0)
					)
				session "unit_test"
			))
	))

	(print "no warnings raised: ")
	(call assert_true (assoc
		obs (= (null) (get ablate_train_payload [1 "warnings"]))
	))
	(call exit_if_failures (assoc msg "No warnings raised") )

	(print "third train call ablates all duplicates:")
	(call assert_same (assoc
		obs (get ablate_train_payload (list 1 "payload" "ablated_indices"))
		exp (list 0 1)
	))

	(call exit_if_failures (assoc msg "Duplicate cases ablation train call") )

	(declare (assoc
		session_training_indices
			(get
				(call_entity "howso" "get_session_training_indices" (assoc session "unit_test"))
				[1 "payload"]
			)
	))
	(call assert_same (assoc
		obs session_training_indices
		exp [0 1 2 3 4 5 6 7 9]
	))
	(call exit_if_failures (assoc msg "Session indices matched") )

	(declare (assoc
		point_five_cases
			(call_entity "howso" "get_cases" (assoc
				features (list "A" "B")
				condition (assoc A 0.5 B 0.5)
				precision "exact"
			))
	))

	(print "high entropy case is not in model: ")
	(call assert_same (assoc
		obs (get point_five_cases (list 1 "payload" "cases"))
		exp (list)
	))

	(call exit_if_failures (assoc msg "High entropy case was ablated") )

	(declare (assoc
		eleven_cases
			(call_entity "howso" "get_cases" (assoc
				features (list "A" "B")
				condition (assoc A 11 B 11)
				precision "exact"
			))
	))

	(print "low entropy case is in model: ")
	(call assert_same (assoc
		obs (get eleven_cases (list 1 "payload" "cases"))
		exp (list (list 11 11))
	))

	(call exit_if_failures (assoc msg "Low entropy case was not ablated") )

	(call_entity "howso" "set_auto_ablation_params" (assoc
        auto_ablation_enabled .true
		auto_ablation_weight_feature "test_weight"
		min_num_cases 4
		max_num_cases 100
		auto_ablation_influence_weight_entropy_threshold 0.6
		reduce_data_influence_weight_entropy_threshold 0.6
    ))

	(call_entity "howso" "analyze" (assoc
        context_features features
        k_values (list 3)
        p_values (list 1)
		targeted_model "targetless"
    ))

	(assign (assoc
		result
			(call_entity "howso" "train" (assoc
				features features
				cases (list
					(list 0 0)
					(list 11 11)
				)
				session "unit_test"
			))
	))

	(print "fourth train call ablates all: ")
	(call assert_same (assoc
		obs (get result (list 1 "payload" "ablated_indices"))
		exp (list 0 1)
	))

	(assign (assoc
		pre_reduction_size
			(size (get (call_entity "howso" "get_cases")
				(list 1 "payload" "cases")
			))
	))
	(call_entity "howso" "reduce_data" (assoc reduce_max_cases 4))

	(print "Data reduction reduces model size by the expected amount: ")
	(call assert_same (assoc
		obs (get (call_entity "howso" "get_num_training_cases") [1 "payload" "count"])
		exp (floor (/ pre_reduction_size 2))
	))
	(call exit_if_failures (assoc msg "Data reduction does not reduce model size by the expected amount"))

	(declare (assoc
		alternate_weight_feature_payload
			(call_entity "howso" "get_cases" (assoc
				features (list "test_weight")
			))
	))

	(print "Alternative case weight feature is populated: ")
	(call assert_true (assoc
		obs
			(< 1 (size
					(filter
						(lambda (!= 1 (first (current_value))) )
						(get alternate_weight_feature_payload (list 1 "payload" "cases"))
					)
			))
	))

	(call assert_same (assoc
		obs
			(get (call_entity "howso" "get_auto_ablation_params") (list 1 "payload"))
		exp
			(assoc
				auto_ablation_enabled .true
				auto_ablation_weight_feature "test_weight"
				min_num_cases 4
				max_num_cases 100
				auto_ablation_influence_weight_entropy_threshold 0.6
				reduce_data_influence_weight_entropy_threshold 0.6
				exact_prediction_features (null)
				influence_weight_entropy_sample_size 2000
				tolerance_prediction_threshold_map (null)
				relative_prediction_threshold_map (null)
				residual_prediction_features (null)
				conviction_upper_threshold (null)
				conviction_lower_threshold (null)
				delta_threshold_map {}
				rel_threshold_map {}
				abs_threshold_map {}
				reduce_max_cases 50000
			)
	))
	(call exit_if_failures (assoc msg "Alternative case weight feature was not populated"))


	(declare (assoc
		pre_train_total_weight
			(apply "+" (map
				(lambda (first (current_value)))
				(get
					(call_entity "howso" "get_cases" (assoc features (list "test_weight")))
					(list 1 "payload" "cases")
				)
			))
	))

	;train a case with the weight feature specified as 3, then check the weight before/after
	(assign (assoc
		ablate_train_payload
			(call_entity "howso" "train" (assoc
				features (append features "test_weight")
				cases
					(list
						(list 0 0 3)
					)
				session "unit_test"
				train_weights_only .true
				accumulate_weight_feature "test_weight"
			))
	))

	(print "no warnings raised: ")
	(call assert_true (assoc
		obs (= (null) (get ablate_train_payload [1 "warnings"]))
	))
	(call exit_if_failures (assoc msg "No warnings raised") )

	(declare (assoc
		post_train_total_weight
			(apply "+" (map
				(lambda (first (current_value)))
				(get
					(call_entity "howso" "get_cases" (assoc features (list "test_weight")))
					(list 1 "payload" "cases")
				)
			))
	))

	(call assert_approximate (assoc
		obs (- post_train_total_weight pre_train_total_weight)
		exp 3
		;float inprecision here...
		percent 0.001
	))
	(call exit_if_failures (assoc msg "Training with values for weight feature accumulates properly"))

	(call exit_if_failures (assoc msg unit_test_name) )
)