(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_weighted_residuals_cont.amlg"))

	(declare (assoc
		cases
			(list
				(list 0 0 1)
				(list 0 0 1)
                (list 0 0 1)
                (list 1 0.01 100)

                (list 10 10 1)
                (list 10 10 1)
                (list 10 10 1)
                (list 10.01 30 100)
			)
		features (list "A" "B" "case_weight")
	))

    (call_entity "howso" "set_params" (assoc
		default_hyperparameter_map (assoc "k" 2 "p" 1 "dt" -1)
	))

    ;206 total case weight
    ;6/206 case weight will have 0 residuals for both features
    ;100/206 case weight will have residual=1.0 for A, residual=0.01 for B,
    ;100/206 case weight will have residual=0.01 for A, residual=20 for B,

    (declare (assoc
        expected_residuals
            (assoc
                A
                    (/ (dot_product
                        (list 6 100 100)
                        (list 0 1.0 0.01)
                    ) 206)
                B
                    (/ (dot_product
                        (list 6 100 100)
                        (list 0 0.01 20)
                    ) 206)
            )
    ))

	(declare (assoc
		regular_train_payload
			(call_entity "howso" "train" (assoc
				features features
				cases cases
				session "unit_test"
			))
	))


    (declare (assoc
        result
        (call_entity "howso" "react_aggregate" (assoc
            use_case_weights .true
            weight_feature "case_weight"
            context_features (list "A" "B")
            details (assoc
                prediction_stats .true
                selected_prediction_stats (list "mae")
            )
            num_samples 5000
        ))
    ))
	(call keep_result_payload)

    (print "MAE's are as expected: ")
    (call assert_approximate (assoc
        exp expected_residuals
        obs (get result "mae")
        ;these thresholds feel generous at the time of writing, we should investigate if these fail.
        thresh
            (assoc
                A 0.05
                B 0.5
            )
    ))

    (call exit_if_failures (assoc msg unit_test_name ))
)