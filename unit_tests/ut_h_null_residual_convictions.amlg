(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_null_residual_convictions.amlg" retries 1))

	(call_entity "howso" "create_trainee" (assoc trainee "model" ))
	(assign_to_entities "howso" (assoc trainee "model"))

	(call_entity "howso" "set_feature_attributes" (assoc
		features (assoc "class" (assoc "type" "nominal")  )
	))

	(declare (assoc dataset (load "unit_test_data/iris_nulls.csv")))

	(declare (assoc
		result (null)
		features (first dataset)
		data (tail dataset)
	))
	(declare (assoc
		context_features (trunc features)
		action_features (list (last features))
	))

	(call_entity "howso" "train" (assoc
		features features
		input_cases data
		session "session1"
	))

	(call_entity "howso" "analyze" (assoc k_values (list 8) p_values (list 2)))
	(call_entity "howso" "react_into_trainee" (assoc
		residuals (true)
	))

	(call_entity "howso" "react_into_features" (assoc
		familiarity_conviction_addition (true)
	))


	(assign (assoc
		result
			(get
				(call_entity "howso" "react" (assoc
					case_indices (list "session1" 10) ; a setosa case with null for 'sepal_width'
					preserve_feature_values features
					leave_case_out (true)
					details
						(assoc
							case_feature_residuals (true)
							global_case_feature_residual_convictions (true)
							local_case_feature_residual_convictions (true)
						)
				))
				"payload"
			)
	))

	;dataset has nulls for 20 of the setosa cases for 'sepal_width', so global conviction should be less than local
	(print "Continuous feature local residual conviction is > global: ")
	(call assert_true (assoc
		obs
			(<
				(get result (list "global_case_feature_residual_convictions" "sepal_width"))
				(get result (list "local_case_feature_residual_convictions" "sepal_width"))
			)
	))

	(assign (assoc
		result
			(get
				(call_entity "howso" "react" (assoc
					case_indices (list "session1" 149) ;a virginica case with null for 'class'
					preserve_feature_values features
					leave_case_out (true)
					details
						(assoc
							case_feature_residuals (true)
							global_case_feature_residual_convictions (true)
							local_case_feature_residual_convictions (true)
						)
				))
				"payload"
			)
	))

	;dataset has nulls for 20 of the virginica cases for 'class', so global conviction should be less than local
	(print "Nominal feature local residual conviction is > global: ")
	(call assert_true (assoc
		obs
			(<
				(get result (list "global_case_feature_residual_convictions" "class"))
				(get result (list "local_case_feature_residual_convictions" "class"))
			)
	))

	(call exit_if_failures (assoc msg unit_test_name ))
)
