(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_warnings.amlg"))

	(call_entity "howso" "set_feature_attributes" (assoc
		feature_attributes (assoc "target" (assoc "type" "nominal" "data_type" "number") )
	))

	(declare (assoc
		data (load "unit_test_data/iris.csv")
	))

	(declare (assoc
		features (first data)
		train_data (tail data)
		result (null)
	))

	(call_entity "howso" "train" (assoc
		features features
		cases (trunc train_data 30)
		session "default"
	))

	(assign (assoc
		result
			(call_entity "howso" "react_into_features" (assoc
				familiarity_conviction_addition (true)
			))
	))
	(call keep_result_warnings)

	(print "Warning about calling analyze(): ")
	(call assert_same (assoc
		obs (size result)
		exp 2
	))

	(assign (assoc
		result
			(call_entity "howso" "react_into_features" (assoc
				familiarity_conviction_addition (true)
			))
	))
	(call keep_result_errors)

	(print "Error about overwrite: ")
	(call assert_same (assoc
		obs result
		exp "The following features already exist: familiarity_conviction_addition, call react_into_features() with overwrite=True to overwrite the values for these features."
	))

	(call_entity "howso" "analyze" )

	(assign (assoc
		result
			(call_entity "howso" "react_into_features" (assoc
				familiarity_conviction_addition (true)
				overwrite (true)
			))
	))
	(call keep_result_warnings)

	(print "No warning after analyze is called: ")
	(call assert_null (assoc
		obs result
	))


	(assign (assoc
		result
			(call_entity "howso" "react_into_features" (assoc
				familiarity_conviction_addition (true)
				use_case_weights (true)
				overwrite (true)
			))
	))
	(call keep_result_warnings)

	(print "Warning about calling analyze() with case weights: ")
	(call assert_true (assoc
		obs (= 1 (size result))
	))

	(call exit_if_failures (assoc msg "Warnings for react_into_features()"))


	(assign (assoc
		result
			(call_entity "howso" "single_react" (assoc
				case_indices (list "default" 10)
				preserve_feature_values features
				leave_case_out (true)
				details
					(assoc
						feature_full_residual_convictions_for_case (true)
					)
			))
	))
	(call keep_result_warnings)

	(print "No warnings for case feature residuals even before react_aggregate has been called ")
	(call assert_null (assoc
		obs result
	))

	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				case_indices (list (list "default" 10) (list "default" 11))
				preserve_feature_values (trunc features)
				action_features (list (last features))
				leave_case_out (true)
				details
					(assoc
						feature_robust_residuals (true)
					)
				use_case_weights (true)
			))
	))
	(call keep_result_warnings)

	(print "Warning about calling react() for case weights, one warning per batch call: ")
	(call assert_true (assoc
		obs (= 1 (size result))
	))

	(call exit_if_failures (assoc msg "Warnings for react()"))


	(call_entity "howso" "analyze" (assoc
		action_features (list (last features))
		context_features (trunc features)
	))

	(assign (assoc
		result
			(call_entity "howso" "react_aggregate" (assoc
				details (assoc feature_full_accuracy_contributions (true))
				feature_influences_action_feature "target"
			))
	))
	(call keep_result_warnings)
	(print "No warning about calling analyze() after targeted analysis:")
	(call assert_same (assoc
        exp 0
		obs (size result)
	))

	(print "Errors when specifying untrained features: ")
	(assign (assoc
		result
			(call_entity "howso" "analyze" (assoc
				context_features (list "fake_feature1" "fake_feature2")
				action_features (list "fake_feature3")
			))
	))
	(call keep_result_errors)

	(call assert_same (assoc
		exp "list"
		obs (get_type_string result)
	))
	(call assert_same (assoc
		exp 2
		obs (size result)
	))

	(assign (assoc
		result
			(call_entity "howso" "single_react" (assoc
				action_features (list "fake_feature3")
				desired_conviction 5.0
			))
	))
	(call keep_result_errors)
	(call assert_same (assoc
		exp "string"
		obs (get_type_string result)
	))

	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features (list "fake_feature3")
				desired_conviction 5.0
				num_cases_to_generate 20
			))
	))
	(call keep_result_errors)

	(call assert_same (assoc
		exp "string"
		obs (get_type_string result)
	))

	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features (list "familiarity_conviction_addition")
				desired_conviction 5.0
				num_cases_to_generate 5
			))
	))

	(call assert_same (assoc
		exp 1
		obs (first result)
	))

	(call assert_same (assoc
		exp 5
		obs (size (get result (list 1 "payload" "action_values")))
	))

	(call_entity "howso" "remove_feature" (assoc
		feature "familiarity_conviction_addition"
	))

	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				action_features (list "familiarity_conviction_addition")
				desired_conviction 5.0
				num_cases_to_generate 5
			))
	))
	(call keep_result_errors)

	(call assert_same (assoc
		exp "string"
		obs (get_type_string result)
	))

	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				context_features features
                context_values [(last data)]
				details
					{not_real_detail (true)}
			))
	))
	(print "Calling react with an unknown key in details: ")
    (call assert_same (assoc
        obs 0
        exp (first result)
    ))

	(print "Error map has the right information: ")
	(call assert_true (assoc
		obs (contains_index (get result [1 "errors"]) "details")
	))

	(call exit_if_failures (assoc msg unit_test_name ))
)
