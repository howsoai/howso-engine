(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_warnings.amlg"))

	(call_entity "howso" "create_trainee" (assoc trainee "iris" ))

	(call_entity "howso" "set_feature_attributes" (assoc
		trainee "iris"
		features (assoc "target" (assoc "type" "nominal" "data_type" "number") )
	))


	(declare (assoc
		data (load "unit_test_data/iris.csv")
	))

	(declare (assoc
		features (first data)
		train_data (tail data)
		result (null)
	))

	(call_entity "howso" "train" (assoc
		features features
		input_cases (trunc train_data 30)
		trainee "iris"
		session "default"
	))

	(assign (assoc
		result
			(call_entity "howso" "react_into_features" (assoc
				trainee "iris"
				familiarity_conviction_addition (true)
			))
	))

	(print "Warning about calling analyze(): ")
	(call assert_same (assoc
		obs (size (get result "warnings"))
		exp 2
	))

	(call_entity "howso" "analyze" (assoc
		trainee "iris"
	))

	(assign (assoc
		result
			(call_entity "howso" "react_into_features" (assoc
				trainee "iris"
				familiarity_conviction_addition (true)
			))
	))

	(print "No warning after analyze is called: ")
	(call assert_true (assoc
		obs (= (null) (get result "warnings"))
	))


	(assign (assoc
		result
			(call_entity "howso" "react_into_features" (assoc
				trainee "iris"
				familiarity_conviction_addition (true)
				use_case_weights (true)

			))
	))

	(print "Warning about calling analyze() with case weights: ")
	(call assert_true (assoc
		obs (= 1 (size (get result "warnings")))
	))

	(call exit_if_failures (assoc msg "Warnings for react_into_features()"))


	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				trainee "iris"
				case_indices (list "default" 10)
				preserve_feature_values features
				leave_case_out (true)
				details
					(assoc
						global_case_feature_residual_convictions (true)
						local_case_feature_residual_convictions (true)
					)
			))
	))

	(print "Warning about calling react_into_trainee() with residuals: ")
	(call assert_true (assoc
		obs (= 1 (size (get result "warnings")))
	))

	(call_entity "howso" "react_into_trainee" (assoc
		trainee "iris"
		residuals (true)
	))

	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				trainee "iris"
				case_indices (list "default" 10)
				preserve_feature_values features
				leave_case_out (true)
				details
					(assoc
						global_case_feature_residual_convictions (true)
						local_case_feature_residual_convictions (true)
					)
			))
	))
	(print "No warning after storing matching residuals: ")
	(call assert_true (assoc
		obs (= (null) (get result "warnings"))
	))

	(assign (assoc
		result
			(call_entity "howso" "batch_react" (assoc
				trainee "iris"
				case_indices (list (list "default" 10) (list "default" 11))
				preserve_feature_values (trunc features)
				action_features (list (last features))
				leave_case_out (true)
				details
					(assoc
						global_case_feature_residual_convictions (true)
						robust_residuals (true)
					)
				use_case_weights (true)
			))
	))

	(print "Warning about calling react_into_trainee() for case weights, one warning per batch call: ")
	(call assert_true (assoc
		obs (= 1 (size (get result "warnings")))
	))

	(call exit_if_failures (assoc msg "Warnings for react_into_trainee()"))

	(assign (assoc
		result
			(call_entity "howso" "react_into_trainee" (assoc
				trainee "iris"
				mda (true)
				action_feature "target"
			))
	))

	(print "Warning about calling analyze() with 'target' as action features:")
	(call assert_true (assoc
		obs (= 1 (size (get result "warnings")))
	))


	(call_entity "howso" "analyze" (assoc
		trainee "iris"
		action_features (list (last features))
		context_features (trunc features)
	))

	(assign (assoc
		result
			(call_entity "howso" "react_into_trainee" (assoc
				trainee "iris"
				mda (true)
				action_feature "target"
			))
	))

	(print "No warning about calling analyze() after targeted analysis:")
	(call assert_true (assoc
		obs (= (null) (get result "warnings"))
	))


	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				trainee "iris"
				case_indices (list "default" 10)
				preserve_feature_values (trunc features)
				action_features (list (last features))
				leave_case_out (true)
				details
					(assoc
						global_case_feature_residual_convictions (true)
						robust_residuals (true)
					)
			))
	))
	(print "Warning about calling react_into_trainee() with residuals_robust and 'target' as action feature: ")
	(call assert_same (assoc
		exp
			(concat
				"Cannot compute 'global_case_feature_residual_convictions' because Robust global residuals have not been computed for action_feature "
				"'target' without case weights.\nPlease call 'react_into_trainee()', with residuals_robust=true, action_feature='target'."
			)
		obs (get result (list "warnings" 0 "detail"))
	))

	(call_entity "howso" "analyze" (assoc
		trainee "iris"
		action_features (list (last features))
		context_features (trunc features)
		use_case_weights (true)
		weight_feature "target"
	))

	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				trainee "iris"
				case_indices (list "default" 10)
				preserve_feature_values (trunc features)
				action_features (list (last features))
				leave_case_out (true)
				details
					(assoc
						global_case_feature_residual_convictions (true)
						robust_residuals (true)
					)
				use_case_weights (true)
				weight_feature "target"
			))
	))

	(print "Warning about calling react_into_trainee() with residuals_robust, case weights and 'target' as action feature: ")
	(call assert_same (assoc
		exp
			(concat
				"Cannot compute 'global_case_feature_residual_convictions' because Robust global residuals have not been computed for "
				"action_feature 'target' with case weights from 'target'.\nPlease call 'react_into_trainee()', with residuals_robust=true, "
				"use_case_weights=true, weight_feature='target', action_feature='target'."
			)
		obs (get result (list "warnings" 0 "detail"))
	))

	(call exit_if_failures (assoc msg "react_into_trainee warnings" ))

	(print "Errors when specifying untrained features: ")
	(assign (assoc
		result
			(call_entity "howso" "analyze" (assoc
				trainee "iris"
				context_features (list "fake_feature1" "fake_feature2")
				action_features (list "fake_feature3")
			))
	))

	(call assert_same (assoc
		exp 2
		obs (size (get result "errors"))
	))

	(assign (assoc
		result
			(call_entity "howso" "react" (assoc
				trainee "iris"
				action_features (list "fake_feature3")
				desired_conviction 5.0
			))
	))

	(call assert_same (assoc
		exp 1
		obs (size (get result "errors"))
	))

	(assign (assoc
		result
			(call_entity "howso" "batch_react" (assoc
				trainee "iris"
				action_features (list "fake_feature3")
				desired_conviction 5.0
				num_cases_to_generate 20
			))
	))

	(call assert_same (assoc
		exp 1
		obs (size (get result "errors"))
	))

	(assign (assoc
		result
			(call_entity "howso" "batch_react" (assoc
				trainee "iris"
				action_features (list "familiarity_conviction_addition")
				desired_conviction 5.0
				num_cases_to_generate 5
			))
	))

	(call assert_same (assoc
		exp 0
		obs (size (get result "errors"))
	))

	(call assert_same (assoc
		exp 5
		obs (size (get result (list "payload" "action_values")))
	))

	(call_entity "howso" "remove_feature" (assoc
		trainee "iris"
		feature "familiarity_conviction_addition"
	))

	(assign (assoc
		result
			(call_entity "howso" "batch_react" (assoc
				trainee "iris"
				action_features (list "familiarity_conviction_addition")
				desired_conviction 5.0
				num_cases_to_generate 5
			))
	))

	(call assert_same (assoc
		exp 1
		obs (size (get result "errors"))
	))

	(call exit_if_failures (assoc msg unit_test_name ))
)
