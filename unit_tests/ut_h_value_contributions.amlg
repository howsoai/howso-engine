(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "unit_test.amlg")))
	(call (load "unit_test_howso.amlg") (assoc name "ut_h_value_contributions.amlg"))

	(call_entity "howso" "create_trainee" (assoc trainee "model"))

	(declare (assoc
		model_data (load "unit_test_data/student_tests.csv")
	))
	(declare (assoc
		data (tail model_data)
		features (first model_data)
		result (null)
	))

	(call_entity "howso" "set_feature_attributes" (assoc
		feature_attributes
			(assoc
				"name" (assoc "type" "nominal" "data_type" "string")
				"subject" (assoc "type" "nominal" "data_type" "string")
				"is_multiple_choice" (assoc "type" "nominal" "data_type" "string")
				"num_questions" (assoc "type" "continuous")
				"minutes" (assoc "type" "continuous")
				"study_time" (assoc "type" "continuous")
				"score" (assoc "type" "continuous")
			)
	))

	(call_entity "howso" "train" (assoc
		cases data
		features features
		session "session1"
	))

	(call_entity "howso" "analyze" (assoc convergence_threshold 0 ))

	(print "Robust Accuracy Contributions: ")
	(declare (assoc
		ac_map
			(get
				(call_entity "howso" "react_aggregate" (assoc
					details { "feature_robust_accuracy_contributions" .true }
					action_features [ "score"]
					convergence_threshold 0
					num_robust_accuracy_contributions_samples 100000
				))
				[1 "payload" "feature_robust_accuracy_contributions" "score"]
			)
	))
	(print ac_map "ANALYSIS:\n 'name', 'study_time' and 'subject' predict 'scores' the best\n(study_time is highly correlatedb/c it's derived from score and minutes)\n")


	(print "\nValue Robust Accuracy Contributions (long running test)\n---\n")
	(print "Computing for 'subject': ")
	(assign (assoc
	 	result
			(call_entity "howso" "react_aggregate" (assoc
				details { "value_robust_accuracy_contributions" .true}
				value_robust_contribution_action_features "score"
				value_robust_contribution_features [ "subject" ]
			))
	))
	(call keep_result_payload)
	(print
		"ANALYSIS:\n"
		"	everyone gets similar very high grades in 'art'\n"
		"	everyone does consistently well in 'math', even the poor performers\n\n"
	)
	(call assert_same (assoc
		exp [ ["art"] ["math"] ["science"] ["literature"] ["history"] ]
		obs (get result ["value_robust_contributions" "feature_values"])
	))

	(print "Average 'subject' AC matches its robust AC:")
	(call assert_approximate (assoc
		obs (generalized_mean (get result ["value_robust_contributions" "ac_values"]))
		exp (get ac_map "subject")
		thresh 0.2
	))

	(print "Computing for 'name': ")
	(assign (assoc
		result
			(call_entity "howso" "react_aggregate" (assoc
				details { "value_robust_accuracy_contributions" .true}
				value_robust_contribution_action_features "score"
				value_robust_contribution_features [ "name" ]
			))
	))
	(call keep_result_payload)
	(print
		"ANALYSIS:\n"
		"	'anne' gets perfect 'math' scores and does well overall\n"
		"	'bill' is consistently in the 90s across the board\n"
		"	'david' is consistently in the 80s across the board\n"
		"	'cora's scores vary widely\n\n"
	)
	(call assert_same (assoc
		exp  [ ["anne"] ["bill"] ["david"] ["fiona"] ["ed"] ["cora"] ]
		obs (get result ["value_robust_contributions" "feature_values"])
	))
	(print "Average 'name' AC matches its robust AC:")
	(call assert_approximate (assoc
		obs (generalized_mean (get result ["value_robust_contributions" "ac_values"]))
		exp (get ac_map "name")
		percent 0.1
	))

	(call exit_if_failures (assoc msg "Values A.C. computed correctly."))

	(print "Computing for 'name' and 'subject': ")
	(assign (assoc
	 	result
			(call_entity "howso" "react_aggregate" (assoc
				details
					{
						"value_robust_accuracy_contributions" .true
						"value_robust_prediction_contributions" .true
					}
				value_robust_contribution_action_features "score"
				value_robust_contribution_features [ "name" "subject" ]
			))
	))
 	(call keep_result_payload)
	(print
		"ANALYSIS:\ncombination 'name' and 'subject':\n"
		"	'anne' gets perfect 'math' (and almost perfect 'history') scores\n"
		"	'fiona' is a poor student, but does consistently bad at 'science'\n"
		"	'ed's scores vary, but are usually consistent within each subject\n\n"
	)
	(call assert_same (assoc
		obs (trunc (get result ["value_robust_contributions" "feature_values"]) 3)
		exp
			[
				["anne" "math"]
				["fiona" "science"]
				["anne" "history"]
			]
	))
	(call assert_same (assoc
		obs (trunc (get result ["value_robust_contributions" "feature_values"]) 6)
		exp
			[
				["anne" "math"]
				["fiona" "science"]
				["anne" "history"]
				["bill" "math"]
				["ed" "literature"]
				["ed" "history"]
			]
		unordered .true
	))

	(print "'Anne + math' and 'Fiona + science' both have high P.C. because they are so extreme: ")
	(call assert_approximate (assoc
		obs (trunc (get result ["value_robust_contributions" "pc_values"]) 2)
		exp [6 6]
		thresh 0.3
	))
	(print
		"'Anne + math' has very high directional P.C. because she always does so well, while\n"
		"'Fiona + science' has very low directional P.C. because she always does so poorly: "
	)
	(call assert_approximate (assoc
		obs (trunc (get result ["value_robust_contributions" "pc_directional_values"]) 2)
		exp [6 -6]
		thresh 0.3
	))
	(call exit_if_failures (assoc msg "A.C. and P.C. for two features."))

	(print "Computing 'study_time': ")
	(assign (assoc
	 	result
			(call_entity "howso" "react_aggregate" (assoc
				details { "value_robust_accuracy_contributions" .true}
				value_robust_contribution_action_features "score"
				value_robust_contribution_features [ "study_time" ]
			))
	))
 	(call keep_result_payload)
	(print
		"ANALYSIS:\n 'study_time':\n"
		"	high study time (e.g., for 'anne') gets consistently excellent scores\n"
		"	low study time (e.g.. for 'fiona') is consistently low scores, etc.\n\n"
	)
	(call assert_same (assoc
		obs (trunc (get result ["value_robust_contributions" "feature_values"]) 3)
		exp
			[
				[
					[150.7 153.7]
				]
				[
					[73.3 79]
				]
				[
					[134.2 148.5]
				]
			]
	))

	(print "Average 'study_time' AC matches its robust AC:")
	(call assert_approximate (assoc
		obs (generalized_mean (get result ["value_robust_contributions" "ac_values"]))
		exp (get ac_map "study_time")
		percent 0.2
	))

	(call exit_if_failures (assoc msg unit_test_name))
)