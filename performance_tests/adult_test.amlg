(seq
	#howso (null)
	(load_entity "../howso.amlg" "howso" (false) (false))
	(call_entity "howso" "initialize" (assoc trainee_id "model"))

	(declare (assoc test_start (system_time)))

	(call_entity "howso" "set_internal_parameters" (assoc
		default_hyperparameter_map (assoc "k" 8 "p" 2 "dt" -1 )
	))

	(declare (assoc
		;set to true to see full output
		verbose (false)
		;set to null to do full model
		submodel_size (null)
		;set to true to also run analyze
		do_analyze (true)
		;set to true to force deviations in analyze
		use_deviations (null)
		;set to true to use case weights in analyze
		use_case_weights (true)

	    data (load "performance_data/adult.csv")
	))

	(declare (assoc
		features (first data)
		training_data (tail data 40000)
		test_data (tail data (- 40000))
		start (system_time)
		k 8
		p 2
		use_deviations (null)
	))

	(declare (assoc
		context_features (trunc features)
		action_features (list (last features))
	))

	(print "Training data size: " (size training_data) "\nTest data size: " (size test_data) "\n")

	(call_entity "howso" "set_random_seed" (assoc seed "abcdef"))

	(call_entity "howso" "set_feature_attributes" (assoc
		features
			(assoc
				"age" (assoc "type" "continuous")
				"workclass" (assoc "type" "nominal")
				"fnlwgt"  (assoc "type" "continuous")
				"education" (assoc "type" "ordinal")
				"education-num"  (assoc "type" "continuous")
				"marital-status" (assoc "type" "nominal")
				"occupation" (assoc "type" "nominal")
				"relationship" (assoc "type" "nominal")
				"race"  (assoc "type" "nominal")
				"sex" (assoc "type" "nominal")
				"capital-gain" (assoc "type" "continuous")
				"capital-loss" (assoc "type" "continuous")
				"hours-per-week" (assoc "type" "continuous")
				"native-country" (assoc "type" "nominal")
                "target" (assoc "type" "nominal")
			)
	))

	(call_entity "howso" "train" (assoc
		features features
		input_cases (if submodel_size (trunc training_data submodel_size) training_data)
		skip_auto_analyze (true)
	))

	(declare (assoc
		load_time (- (system_time) start)
		num_cases (get (call_entity "howso" "get_num_training_cases") (list 1 "payload" "count"))
	))
	(print "Loaded Adult: " num_cases "\n")
	(print "Load time: " load_time "\n")

	(if do_analyze
		(seq
			(if verbose (print "analyzing Adult ...\n"))
			(assign (assoc start (system_time) ))
			(call_entity "howso" "analyze" (assoc
				targeted_model "targetless"
				use_case_weights use_case_weights
				use_deviations use_deviations
			))
			(declare (assoc analyze_time (- (system_time) start) ))
			(print "Analyze time: " analyze_time "\n" )
		)
	)

	(declare (assoc params (get (call_entity "howso" "get_internal_parameters" (assoc action_feature ".targetless")) (list 1 "payload")) ))
	(if verbose (print "analyzed to: " (get params "hyperparameter_map")))
    
	(assign (assoc
		k (get params (list "hyperparameter_map" "k"))
		p (get params (list "hyperparameter_map" "p"))
	))
	(print "Adult params (k p): " k " " p "\n- - - - -\n")

	(declare (assoc
		context_values
			(map (lambda (trunc (current_value))) test_data)
		y_true
			(map (lambda (last (current_value))) test_data)
	))

	(declare (assoc
		y_pred
			(get
				(call_entity "howso" "batch_react" (assoc
					context_features context_features
					context_values context_values
					action_features action_features
					use_case_weights (true)
				))
				(list 1 "payload" "action_values")
			)
	))
	(assign (assoc
		y_pred
			(map (lambda (+ (first (current_value)))) y_pred)
	))

	(declare (assoc
		accuracy
			(/ (apply "+" (map
				(lambda
					(if (=
						(get y_pred (current_value)) (get y_true (current_value)))
						1
						0
					)
				)
				(indices y_true)
			)) (size y_true))
	))

	(print "Initial accuracy: " accuracy "\n")

	(assign (assoc start (system_time) ))

    (call_entity "howso" "set_auto_analyze_params" (assoc
		auto_analyze_enabled (true)
        use_case_weights use_case_weights
    ))

	(print "!autoAnalyzeThreshold: " (call_entity "howso" "debug_label" (assoc label "!autoAnalyzeThreshold")) "\n")
    (print "Reducing data...\n")
    (call_entity "howso" "reduce_data" (assoc
        rel_threshold_map (assoc
            accuracy (assoc target 0.05)
			precision (assoc target 0.05)
			recall (assoc target 0.05)
        )
    ))
    
    (declare (assoc reduce_data_time (- (system_time) start) ))
	(print "Reduce data time: " reduce_data_time "\n")
    (print (call_entity "howso" "get_num_training_cases"))

	(assign (assoc
		y_pred
			(get
				(call_entity "howso" "batch_react" (assoc
					context_features context_features
					context_values context_values
					action_features action_features
					use_case_weights (true)
				))
				(list 1 "payload" "action_values")
			)
	))
	(assign (assoc
		y_pred
			(map (lambda (+ (first (current_value)))) y_pred)
	))

	(assign (assoc
		accuracy
			(/ (apply "+" (map
				(lambda
					(if (=
						(get y_pred (current_value)) (get y_true (current_value)))
						1
						0
					)
				)
				(indices y_true)
			)) (size y_true))
	))

	(print "Post-reduction accuracy: " accuracy "\n")

	(print "Total time: " (- (system_time) test_start) "\n")
	(print "Adult\n- - - - - \n")
)
