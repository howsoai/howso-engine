(seq
	#howso (null)
	(direct_assign_to_entities (assoc howso (load "../howso.amlg")))
	(assign_to_entities (assoc filepath "../"))

	(call create_trainee (assoc trainee "model"))
	(call set_internal_parameters (assoc
		trainee "model"
		default_hyperparameter_map (assoc "k" 8 "p" 2 "dt" -1 "useDeviations" (false))
	))

	(declare (assoc
		;set to true to see full output
		verbose (false)
		;set to null to do full model
		submodel_size (null)
		;set to true to also run analyze
		do_analyze (false)
		;set to true to force deviations in analyze
		use_deviations (null)
		;set to true to use case weights in analyze
		use_case_weights (true)

		data (load "performance_data/OnlineRetail.csv")
	))

	(declare (assoc
		features (first data)
		training_data (tail data)
		start (system_time)
	))


	(call set_feature_attributes (assoc
		trainee "model"
		features
			(assoc
				"InvoiceNo" (assoc "type" "nominal")
				"StockCode" (assoc "type" "nominal")
				"Description"  (assoc "type" "nominal")
				"CustomerID" (assoc "type" "nominal")
				"Country"  (assoc "type" "nominal")
				"Quantity" (assoc "type" "continuous")
				"UnitPrice" (assoc "type" "continuous")
				"InvoiceDate" (assoc "type" "continuous" "date_time_format" "%m/%d/%Y %H:%M")
			)
	))

	(if verbose (print "Loading model ...\n"))
	(call train (assoc
		trainee "model"
		features features
		input_cases (if submodel_size (trunc training_data submodel_size) training_data)
	))

	(print "Loaded Online Retail: "
		(get (call get_num_training_cases (assoc trainee "model")) (list "payload" "count"))
		"\n"
	)

	(declare (assoc load_time (- (system_time) start) ))
	(print "Load time: " load_time "\n")

	(if do_analyze
		(seq
			(if verbose (print "analyzing online retail ...\n"))
			(assign (assoc start (system_time) ))
			(call analyze (assoc
				trainee "model"
				context_features features
				targeted_model "targetless"
				p_values (list 0.5 1 2)
				k_values (list 5 8 13)
				k_folds 1
				num_analysis_samples 1000
				inverse_residuals_as_weights (true)
				use_case_weights use_case_weights
				use_deviations use_deviations
			))
			(declare (assoc analyze_time (- (system_time) start) ))
			(print "Analyze time: " analyze_time "\n" )
		)
	)

	(declare (assoc params (get (call get_internal_parameters (assoc trainee "model" action_feature ".targetless")) "payload") ))
	(if verbose (print "analyzed to: " (get params "hyperparameter_map")))

	(assign (assoc
		k (get params (list "hyperparameter_map" "k"))
		p (get params (list "hyperparameter_map" "p"))
		deviations (get params (list "hyperparameter_map" "useDeviations"))
	))

	(assign (assoc start (system_time) ))

	(if verbose (print "residuals for model ...\n"))
	(call react_into_trainee (assoc
		trainee "model"
		context_features features
		num_samples 1000
		residuals_robust (true)
	))

	(declare (assoc residuals_time (- (system_time) start) ))
	(print "Residuals time: " residuals_time "\n")

	(assign (assoc start (system_time) ))

	||(map
		(lambda (seq
			(call react (assoc
				trainee "model"
				action_features features
				desired_conviction 2
				use_regional_model_residuals (false)
			))

			(if verbose
				(if (= 0 (mod (target_index) 100))
					(print (target_index) "\n")

					(= 0 (mod (target_index) 10))
					(print (target_index) " ")
				)
			)
		))

		(range 1 2000)
	)

	(declare (assoc generate_time (- (system_time) start) ))
	(if verbose (print "test time: " generate_time "\n"))

	(print "Generate time: " generate_time "\n")
	(print "Online Retail params (k p deviations): " k " " p " " deviations "\n- - - - -\n")
	(print "Online Retail\n- - - - - \n")

	(destroy_entities "model")
)
