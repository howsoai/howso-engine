(seq
	(load_entity "../howso.amlg" "howso" (false) (false))
	(call_entity "howso" "initialize" (assoc trainee_id "model"))

	(declare (assoc test_start (system_time)))

	(call_entity "howso" "set_params" (assoc
		default_hyperparameter_map (assoc "k" 8 "p" 2 "dt" -1)
	))

	(declare (assoc
		;set to true to see full output
		verbose (false)
		;set to null to do full model
		submodel_size (null)
		;set to true to also run analyze
		do_analyze (false)
		;set to true to force deviations in analyze
		use_deviations (null)
		;set to true to use case weights in analyze
		use_case_weights (false)

		data (load "performance_data/OnlineRetail.csv")
	))

	(declare (assoc
		features (first data)
		training_data (tail data)
		start (system_time)
	))


	(call_entity "howso" "set_feature_attributes" (assoc
		feature_attributes
			(assoc
				"InvoiceNo" (assoc "type" "nominal")
				"StockCode" (assoc "type" "nominal")
				"Description"  (assoc "type" "nominal")
				"CustomerID" (assoc "type" "nominal")
				"Country"  (assoc "type" "nominal")
				"Quantity" (assoc "type" "continuous")
				"UnitPrice" (assoc "type" "continuous")
				"InvoiceDate" (assoc "type" "continuous" "date_time_format" "%m/%d/%Y %H:%M")
			)
	))

	(if verbose (print "Loading model ...\n"))
	(call_entity "howso" "train" (assoc
		features features
		cases (if submodel_size (trunc training_data submodel_size) training_data)
	))

	(print "Loaded Online Retail: "
		(get (call_entity "howso" "get_num_training_cases") (list 1 "payload" "count"))
		"\n"
	)

	(declare (assoc load_time (- (system_time) start) ))
	(print "Load time: " load_time "\n")

	(if do_analyze
		(seq
			(if verbose (print "analyzing online retail ...\n"))
			(assign (assoc start (system_time) ))
			(call_entity "howso" "analyze" (assoc
				context_features features
				targeted_model "targetless"
				use_case_weights use_case_weights
				use_deviations use_deviations
			))
			(declare (assoc analyze_time (- (system_time) start) ))
			(print "Analyze time: " analyze_time "\n" )
		)
	)

	(declare (assoc params (get (call_entity "howso" "get_params" (assoc action_feature "")) (list 1 "payload")) ))
	(if verbose (print "analyzed to: " (get params "hyperparameter_map")))

	(assign (assoc
		k (get params (list "hyperparameter_map" "k"))
		p (get params (list "hyperparameter_map" "p"))
	))
	(print "Online Retail params (k p): " k " " p "\n- - - - -\n")

	(assign (assoc start (system_time) ))

	(if verbose (print "residuals for model ...\n"))
	(call_entity "howso" "react_aggregate" (assoc
		context_features features
		num_samples 1000
		details (assoc feature_residuals_robust (true))
	))

	(declare (assoc residuals_time (- (system_time) start) ))
	(print "Residuals time: " residuals_time "\n")

	(assign (assoc start (system_time) ))

	||(map
		(lambda (seq
			(call_entity "howso" "single_react" (assoc
				action_features features
				desired_conviction 2
				use_regional_model_residuals (false)
			))

			(if verbose
				(if (= 0 (mod (current_index) 100))
					(print (current_index) "\n")

					(= 0 (mod (current_index) 10))
					(print (current_index) " ")
				)
			)
		))

		(range 1 2000)
	)

	(declare (assoc generate_time (- (system_time) start) ))
	(if verbose (print "test time: " generate_time "\n"))

	(print "Generate time: " generate_time "\n")

	(destroy_entities "model")
	(print "Total time: " (- (system_time) test_start) "\n")
	(print "Online Retail\n- - - - - \n")
	(destroy_entities "howso")
)
