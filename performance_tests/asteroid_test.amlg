(seq
	#howso (null)
	(direct_assign_to_entities (assoc howso (load "../howso.amlg")))
	(assign_to_entities (assoc filepath "../"))

	(call create_trainee (assoc trainee "asteroid"))
	(call set_internal_parameters (assoc
		trainee "asteroid"
		default_hyperparameter_map (assoc "k" 8 "p" 2 "dt" -1 "useDeviations" (false))
	))

	(declare (assoc
		;set to true to see full output
		verbose (false)
		;set to null to do full model
		submodel_size (null); 100000
		;set to true to also run analyze
		do_analyze (false)
		;set to true to force deviations in analyze
		use_deviations (null)
		;set to true to use case weights in analyze
		use_case_weights (false)

		data (load "performance_data/Asteroid.csv")
	))

	(declare (assoc
		features (first data)
		training_data (tail data)
		start (system_time)
	))

	(call set_feature_attributes (assoc
		trainee "asteroid"
		features
			(assoc
				"full_name" (assoc "type" "nominal")
				"condition_code" (assoc "type" "nominal" "data_type" "number")
				"extent" (assoc "type" "nominal")
				"IR" (assoc "type" "nominal" "data_type" "number")
				"spec_B" (assoc "type" "nominal")
				"spec_T" (assoc "type" "nominal")
				"neo" (assoc "type" "nominal")
				"pha" (assoc "type" "nominal")
			)
	))

	(if verbose (print "Loading Asteroid ...\n"))
	(call train (assoc
		trainee "asteroid"
		features features
		input_cases (if submodel_size (trunc training_data submodel_size) training_data)
	))

    ; test substitions too
	(call set_substitute_feature_values (assoc trainee "asteroid" substitution_value_map (assoc "full_name" (assoc))))

	(print
		"Loaded Asteroid: "
		(get (call get_num_training_cases (assoc trainee "asteroid"))  (list "payload" "count"))
		"\n"
	)

	(declare (assoc load_time (- (system_time) start) ))
	(print "Load time: " load_time "\n")

	(if do_analyze
		(seq
			(assign (assoc start (system_time) ))
			(if verbose (print "analyzing ...\n"))
			(call analyze (assoc
				trainee "asteroid"
				context_features features
				targeted_model "targetless"
				p_values (list 0.5 1 2)
				k_values (list 5 8 13)
				k_folds 1
				num_analysis_samples 1000
				inverse_residuals_as_weights (true)
				use_case_weights use_case_weights
				use_deviations use_deviations
			))
			(declare (assoc analyze_time (- (system_time) start) ))
			(print "Analyze time: " analyze_time "\n")
		)
	)

	(declare (assoc params (get (call get_internal_parameters (assoc trainee "model" action_feature ".targetless")) "payload") ))
	(if verbose (print "analyzed to: " (get params "hyperparameter_map")))

	(assign (assoc
		k (get params (list "hyperparameter_map" "k"))
		p (get params (list "hyperparameter_map" "p"))
		deviations (get params (list "hyperparameter_map" "useDeviations"))
	))

	(assign (assoc start (system_time) ))

	(if verbose (print "residuals for Asteroid ...\n"))
	(call react_into_trainee (assoc
		trainee "asteroid"
		context_features features
		num_samples 500
		residuals_robust (true)
	))

	(declare (assoc residuals_time (- (system_time) start) ))
	(print "Residuals time: " residuals_time "\n")

	(assign (assoc start (system_time) ))

	||(range
		(lambda (seq
			(call react (assoc
				trainee "asteroid"
				action_features features
				desired_conviction 5
				use_regional_model_residuals (false)
				generate_new_cases "no"
			))

			(if verbose
				(if (= 0 (mod (target_index) 100))
					(print (target_index) "\n")

					(= 0 (mod (target_index) 10))
					(print (target_index) " ")
				)
			)
		))

		1 2000 1
	)

	(declare (assoc generate_time (- (system_time) start) ))
	(if verbose (print "test time: " generate_time "\n"))

	(print "Generate time: " generate_time "\n")
	(print "Asteroid params (k p deviations): " k " " p " " deviations "\n- - - - -\n")
	(print "Asteroid\n- - - - - \n")
)
