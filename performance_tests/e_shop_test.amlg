(seq
	(load_entity
		"../howso.amlg"
		"howso"
		(null)
		(false)
		{escape_resource_name (false) escape_contained_resource_names (false)}
	)
	(set_entity_root_permission "howso" (true))
	(call_entity "howso" "initialize" (assoc trainee_id "model" filepath "../"))

	(declare (assoc test_start (system_time)))

	(call_entity "howso" "set_params" (assoc
		default_hyperparameter_map (assoc "k" 8 "p" 2 "dt" -1 )
	))

	(declare (assoc
		;set to true to see full output
		verbose (false)
		;set to null to do full model
		submodel_size (null)
		;set to true to also run analyze
		do_analyze (false)
		;set to true to force deviations in analyze
		use_deviations (null)
		;set to true to use case weights in analyze
		use_case_weights (false)

		data (load "performance_data/e_shop_clothing_2008.csv")
	))

	(declare (assoc
		features (first data)
		training_data (tail data)
		start (system_time)
		k 8
		p 2
		deviations (false)
	))

	(call_entity "howso" "set_feature_attributes" (assoc
		feature_attributes
			(assoc
				"month" (assoc "type" "ordinal" "data_type" "number")
				"day" (assoc "type" "ordinal" "data_type" "number")
				"country" (assoc "type" "nominal" "data_type" "number")
				"session ID" (assoc "type" "nominal" "data_type" "number")
				"page 1 (main category)" (assoc "type" "nominal" "data_type" "number")
				"page 2 (clothing model)" (assoc "type" "nominal")
				"colour" (assoc "type" "nominal" "data_type" "number")
				"location" (assoc "type" "nominal" "data_type" "number")
				"model photography" (assoc "type" "nominal" "data_type" "number")
				"price 2" (assoc "type" "nominal" "data_type" "number")
				"page" (assoc "type" "nominal" "data_type" "number")
			)
	))

	(if verbose (print "Loading e-shop ...\n"))
	(call_entity "howso" "train" (assoc
		features features
		cases (if submodel_size (trunc training_data submodel_size) training_data)
	))

	(print "Loaded e-shop: "
		(get (call_entity "howso" "get_num_training_cases") (list 1 "payload" "count"))
		"\n"
	)

	(declare (assoc load_time (- (system_time) start) ))
	(print "Load time: " load_time "\n")

	(if do_analyze
		(seq
			(if verbose (print "analyzing e-shop ...\n"))
			(assign (assoc start (system_time) ))
			(call_entity "howso" "analyze" (assoc
				context_features features
				targeted_model "targetless"
				use_case_weights use_case_weights
				use_deviations use_deviations
			))
			(declare (assoc analyze_time (- (system_time) start) ))
			(print "Analyze time: " analyze_time "\n" )
		)
	)

	(declare (assoc params (get (call_entity "howso" "get_params" (assoc action_feature "")) (list 1 "payload")) ))
	(if verbose (print "analyzed to: " (get params "hyperparameter_map")))

	(assign (assoc
		k (get params (list "hyperparameter_map" "k"))
		p (get params (list "hyperparameter_map" "p"))
	))
	(print "E-shop params (k p): " k " " p "\n- - - - -\n")

	(assign (assoc start (system_time) ))

	(if verbose (print "residuals for model ...\n"))
	(call_entity "howso" "react_aggreagate" (assoc
		context_features features
		num_samples 1000
		details (assoc feature_robust_residuals (true))
	))

	(declare (assoc residuals_time (- (system_time) start) ))
	(print "Residuals time: " residuals_time "\n")

	(assign (assoc start (system_time) ))

	||(range
		(lambda (seq
			(call_entity "howso" "single_react" (assoc
				action_features features
				desired_conviction 2
				use_regional_residuals (false)
			))
			(if verbose
				(if (= 0 (mod (current_index) 100))
					(print (current_index) "\n")

					(= 0 (mod (current_index) 10))
					(print (current_index) " ")
				)
			)
		))

		1 2000 1
	)

	(declare (assoc generate_time (- (system_time) start) ))
	(if verbose (print "test time: " generate_time "\n"))

	(print "Generate time: " generate_time "\n")

	(destroy_entities "model")
	(print "Total time: " (- (system_time) test_start) "\n")
	(print "E-Shop\n- - - - - \n")
	(destroy_entities "howso")
)
