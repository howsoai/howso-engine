(seq
	;standalone script that launches an instance of Howso and upgrades the specified trainee to the current version
	;should be called with four parameters:  trainee name, path to saved trainee, path to howso (where the howso.caml is) and the file type (json or caml)
	(declare (assoc
		trainee_id (get argv 1)
		path_to_trainee_files (get argv 2)
		path_to_howso (get argv 3)
		file_type (get argv 4)
	))

	(if (or (= (null) trainee_id) (= (null) path_to_trainee_files) (= (null) path_to_howso) (not (or (= "caml" file_type) (= "json" file_type))) )
		(conclude (print
			"Must provide parameters: <trainee_id> <path_to_trainee_files> <path_to_howso> <file_type>\n"
			"  trainee_id : required, id of saved trainee to upgrade (without the file extension)\n"
			"  path_to_trainee_files : required, path to directory containing the trainee json (or caml) files.\n"
			"  path_to_howso : required, path to where howso is deployed (location of howso.caml file)\n"
			"  file_type : required, file type of trainee being upgraded. Either \"json\" or \"caml\".\n\n"
			"Note: paths must end with a slash."
		))
	)

	(load_entity (concat path_to_howso "howso.caml") "howso")
	(set_entity_permissions "howso" {load .true store .true std_out_and_std_err .true })

	(declare (assoc
		result
			(call_entity "howso" "upgrade_trainee" (assoc
				trainee trainee_id
				trainee_json_filepath (if (= "json" file_type) path_to_trainee_files)
				trainee_amlg_filepath (if (= "caml" file_type) path_to_trainee_files)
				root_filepath path_to_howso
			))
	))

	;error during upgrade occured
	(if (= 0 (first result))
		(conclude (print "ERROR: " (get result [1 "detail"])))
	)

	;store in same folder as the original json/caml files
	(declare (assoc
		new_trainee_full_path
			(if (= "json" file_type)
				(concat path_to_trainee_files trainee_id ".caml")

				;ensure original caml files aren't overwritten
				(concat path_to_trainee_files trainee_id "_upgraded.caml")
			)
	))

	(print
		"Persisting as " new_trainee_full_path " : "
		;as one file
		(store new_trainee_full_path  (flatten_entity "howso" .false) )
	)
)
