(seq
	;standalone script that launches an instance of Howso and upgrades the specified trainee to the current version
	;should be called with three parameters:  trainee name, path to saved trainee, path to howso (where the howso.caml is)
	(declare (assoc
		trainee_id (get argv 1)
		path_to_trainee_json (get argv 2)
		path_to_howso (get argv 3)
		path_to_trainee_caml (get argv 4)
	))

	(if (or (= (null) trainee_id) (= (null) path_to_trainee_json) (= (null) path_to_howso) (= (null) path_to_trainee_caml))
		(conclude (print
			"Must provide parameters: <trainee_id> <path_to_trainee_json> <path_to_howso> <path_to_trainee_caml>\n"
			"  trainee_id : required, id of saved trainee to upgrade\n"
			"  path_to_trainee_json : required, path to where the trainee json files are located. Can be empty string if specifying 'path_to_trainee_caml'.\n"
			"  path_to_howso : required, path to where howso is deployed (location of howso.caml file)\n"
			"  path_to_trainee_caml : optional, path to where the trainee caml file is, ignored if 'path_to_trainee_json' is provided as a non-empty string.\n\n"
			"Note: paths must end with a slash."
		))
	)

	(load_entity (concat path_to_howso "howso.caml") "howso")
	(set_entity_permissions "howso" {load .true store .true std_out_and_std_err .true})

	(if (!= "" path_to_trainee_json)
		(seq
			(call_entity "howso" "upgrade_trainee" (assoc
				trainee trainee_id
				trainee_json_filepath path_to_trainee_json
				root_filepath path_to_howso
			))

			;persist upgraded trainee
			(print
				"persisting as " (concat path_to_trainee_caml trainee_id ".caml") " : "
				(store
					;store in same folder as the json files
					(concat path_to_trainee_json trainee_id ".caml")
					;as one file
					(flatten_entity "howso" .false)
				)
			)
		)

		;else use 'path_to_trainee_caml' and load as caml
		(seq
			(call_entity "howso" "upgrade_trainee" (assoc
				trainee trainee_id
				trainee_amlg_filepath path_to_trainee_caml
				root_filepath path_to_howso
			))


			(print
				"persisting as " (concat path_to_trainee_caml trainee_id "_upgraded.caml") " : "

				;persist upgraded trainee
				(store
					;store in same folder as the caml files
					(concat path_to_trainee_caml trainee_id "_upgraded.caml")
					;as one file
					(flatten_entity "howso" .false)
				)
			)
		)
	)
)
